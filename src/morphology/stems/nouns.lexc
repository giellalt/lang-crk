! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! ========================================================================== !
!!                           !!!Plains Cree noun stems                           
! ========================================================================== !

!! The file


!! !! The prefixes

LEXICON Nouns 
!! @LEXNAME@ redirects to AN-IN, BODY, INALIENABLE
! and EXPER

AN-IN       ; ! Animate and inanimate nouns
BODY        ; ! Inanimate, generic form with m-
INALIENABLE ;
!EXPER       ;

LEXICON AN-IN
!! @LEXNAME@ adds the prefixes ni-, ki-, o-. Redirects to STEMS

           @U.noun.abs@            STEMS ; 
< 0:n 0:i "@U.noun.1sg@"  0:"t2" > STEMS ; ! 1
< 0:k 0:i "@U.noun.2sg@"  0:"t2" > STEMS ; ! 2
< 0:o     "@U.noun.3sg@"  0:"t2" > STEMS ; ! 3
< 0:o     "@U.noun.3isg@" 0:"t2" > STEMS ; ! 3-singular-obviative
< 0:n 0:i "@U.noun.1pl@"  0:"t2" > STEMS ; ! exclusive Pl
< 0:k 0:i "@U.noun.12pl@" 0:"t2" > STEMS ; ! inclusive Pl
< 0:k 0:i "@U.noun.2pl@"  0:"t2" > STEMS ; ! 2pl 
< 0:o     "@U.noun.3pl@"  0:"t2" > STEMS ; ! 3pl
< 0:o     "@U.noun.3ipl@" 0:"t2" > STEMS ; ! 3-plural-obviate

!LEXICON EXPER
!< 0:i "@U.noun.exp@"  > BODY ; ! 1


LEXICON BODY
!! @LEXNAME@  adds the prefixes n-, k-, o-, and generic form m- . Redirects to BODYSTEMS.
< 0:m "@U.noun.abs@"  > BODYSTEMS ; !
< 0:n "@U.noun.1sg@"  > BODYSTEMS ; !
< 0:k "@U.noun.2sg@"  > BODYSTEMS ; !
< 0:o "@U.noun.3sg@"  > BODYSTEMS ; !
< 0:o "@U.noun.3isg@" > BODYSTEMS ; !
< 0:n "@U.noun.1pl@"  > BODYSTEMS ; !
< 0:k "@U.noun.12pl@" > BODYSTEMS ; !
< 0:k "@U.noun.2pl@"  > BODYSTEMS ; !
< 0:o "@U.noun.3pl@"  > BODYSTEMS ; !
< 0:o "@U.noun.3ipl@" > BODYSTEMS ; !



LEXICON INALIENABLE
!! @LEXNAME@  adds the prefixes n-, k-, o-. Redirects to INALIENABLESTEMS
       @U.noun.abs@     INALIENABLESTEMS ; 
< 0:n "@U.noun.1sg@"  > INALIENABLESTEMS ; ! 1
< 0:k "@U.noun.2sg@"  > INALIENABLESTEMS ; ! 2
< 0:o "@U.noun.3sg@"  > INALIENABLESTEMS ; ! 3
< 0:o "@U.noun.3isg@" > INALIENABLESTEMS ; ! 3indef
< 0:n "@U.noun.1pl@"  > INALIENABLESTEMS ; ! exclusive Pl
< 0:k "@U.noun.12pl@" > INALIENABLESTEMS ; ! inclusive Pl
< 0:k "@U.noun.2pl@"  > INALIENABLESTEMS ; ! 2pl 
< 0:o "@U.noun.3pl@"  > INALIENABLESTEMS ; ! 3pl note: is prefix o- correct?
< 0:o "@U.noun.3ipl@" > INALIENABLESTEMS ; ! 



!! !!The stems

!! !Animate and inanimate Nouns
!    =============

LEXICON STEMS
!! @LEXNAME@ adds an affixmark and redirects to STEMLIST
 0:%> STEMLIST ;

LEXICON STEMLIST
!! @LEXNAME@ for nouns getting prefixes ni-, ki-, o- \\
asikan ANDECL ; !!= * @CODE@ Wolv: NA1 
pahkwêsikan ANimDECL ; !!= * @CODE@  Wolv: NA1 + im
astotin INDECL ; !!= * @CODE@ Wolv: NI1
cîmân INDECL ; !!= @CODE@ Wolv: NI1 
maskisin INDECLis ; !!= * @CODE@ INDECL but only -is Dim
ayapiy ANDECL ; !!= * @CODE@ Wolv: NA2
kihcôkiniy ANDECL ; !!= @CODE@ Wolv: NA2
oskasâkay INDECL ; !!= * @CODE@ Wolv: NI2
mîkisasâkay INDECL ; !!= @CODE@ Wolv: NI2
masinahikanâhtik:masinahikanâhtikw ANDECL ; !!= * @CODE@ Wolv: NA3
pahkêkin:pahkêkinw INDECL ; !!= * @CODE@ Wolv: NI3w
askekin:askekinw INDECL ; !!= @CODE@ Wolv: NI3w
askihk:askihkw ANimDECL ; !!= * @CODE@ * Wolv: NA3w + im
esa ANimDECL ; !!= * @CODE@ Wolv: NA4 +im
niska ANimDECLis ; !!= * @CODE@ Wolv: NA4 +im, only Dim is
oskâyi INimDECL ; !!= * @CODE@ Wolv: NI4 +im
wâwi INimDECL ; !!= @CODE@ Wolv: NI4 +im
ôsi:ô INDECLosi ; !!= * @CODE@ Wolv: NI4 irr. stem
ihkwa:ihkw ANimDECLw ; !!= * @CODE@ Wolv: NA4w +im
wâhkwa:wâhkw ANimDECLw ; !!= @CODE@ Wolv: NA4w +im
mihko:mihk INimDECLw-ONESYLL-SG ; !!= * @CODE@ Wolv: NI4w -o, only Sg

LEXICON BODYSTEMS
 # ;
 
LEXICON INALIENABLESTEMS  
 # ;
