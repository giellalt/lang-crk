! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! ========================================================================== !
!!                           !!!Plains Cree noun stems                           
! ========================================================================== !

!! The file


!! !! The prefixes

LEXICON Nouns 

AN ; ! Animate nouns
IN ; ! Inanimate nouns
INALIENABLE ;
ANSTEM_kw ; ! Animate nouns whose stems end in kw
IRREGAN ; !mistatim, atim


LEXICON AN
!! @LEXNAME@ redirects to ANSTEMS
!! At the moment it is just a copy of INSTEMS, 
!! pretending animates and inanimates are identical
!! Flag diacritics direct to possessive declension later on

@U.noun.abs@ ANSTEMLIST ; 

< 0:n 0:i "@U.noun.1sg@" 0:"t2" > ANSTEMS ; ! 1
< 0:k 0:i "@U.noun.2sg@" 0:"t2" > ANSTEMS ; ! 2
< 0:o "@U.noun.3sg@" 0:"t2" > ANSTEMS ;     ! 3
< 0:o "@U.noun.3isg@" 0:"t2" > ANSTEMS ;    ! 3-singular-obviative
< 0:n 0:i "@U.noun.1pl@" 0:"t2" > ANSTEMS ;   ! exclusive Pl
< 0:k 0:i "@U.noun.12pl@" 0:"t2" > ANSTEMS ; ! inclusive Pl
< 0:k 0:i "@U.noun.2pl@" 0:"t2" > ANSTEMS ;  ! 2pl 
< 0:o "@U.noun.3pl@" 0:"t2" > ANSTEMS ;       ! 3pl
< 0:o "@U.noun.3ipl@" 0:"t2" > ANSTEMS ;       ! 3-plural-obviate


LEXICON IRREGAN

@U.noun.abs@ IRREGANSTEMSLIST ; 


< 0:n 0:i "@U.noun.1sg@" > IRREGANSTEMS ; ! 1
< 0:k 0:i "@U.noun.2sg@" > IRREGANSTEMS ; ! 2
< 0:o     "@U.noun.3sg@" > IRREGANSTEMS ;     ! 3
< 0:o     "@U.noun.3isg@" > IRREGANSTEMS ;    ! 3-singular-obviative
< 0:n 0:i "@U.noun.1pl@" > IRREGANSTEMS ;   ! exclusive Pl
< 0:k 0:i "@U.noun.12pl@" > IRREGANSTEMS ; ! inclusive Pl
< 0:k 0:i "@U.noun.2pl@" > IRREGANSTEMS ;  ! 2pl 
< 0:o "@U.noun.3pl@" > IRREGANSTEMS ;       ! 3pl
< 0:o "@U.noun.3ipl@" > IRREGANSTEMS ;       ! 3-plural-obviate

LEXICON IN
!! @LEXNAME@ redirects to INSTEMS in two ways:
!! Without prefixes, forming the absolutive (non-Px) declension

@U.noun.abs@  INSTEMS ; 

!! With prefixes, forming the possessive declension
< 0:n 0:i "@U.noun.1sg@" > INSTEMS ; !
< 0:k 0:i "@U.noun.2sg@" > INSTEMS ; !
< 0:o "@U.noun.3sg@" > INSTEMS ; !
< 0:o "@U.noun.3isg@" 0:"t2" > INSTEMS ;    ! 3-singular-obviative
< 0:n 0:i "@U.noun.1pl@" > INSTEMS ; ! exclusive Pl
< 0:k 0:i "@U.noun.12pl@" > INSTEMS ; ! inclusive Pl
< 0:k 0:i "@U.noun.2pl@" > INSTEMS ; !
< 0:o "@U.noun.3pl@" > INSTEMS ; !
< 0:o "@U.noun.3ipl@" 0:"t2" > INSTEMS ;       ! 3-plural-obviate



LEXICON INALIENABLE
!! @LEXNAME@ redirects to INALIENABLESTEMS

< "@U.noun.abs@" >  INALIENABLESTEMS ; 

< 0:n 0:i "@U.noun.1sg@"  > INALIENABLESTEMS ; ! 1
< 0:k 0:i "@U.noun.2sg@"  > INALIENABLESTEMS ; ! 2
< 0:o "@U.noun.3sg@"  > INALIENABLESTEMS ;     ! 3
< 0:m 0:i "@U.noun.3isg@"  > INALIENABLESTEMS ;     ! 3indef
< 0:n 0:i "@U.noun.1pl@"  > INALIENABLESTEMS ;   ! exclusive Pl
< 0:k 0:i "@U.noun.12pl@"  > INALIENABLESTEMS ; ! inclusive Pl
< 0:k 0:i "@U.noun.2pl@"  > INALIENABLESTEMS ;  ! 2pl 
< 0:o "@U.noun.3pl@"  > INALIENABLESTEMS;       ! 3pl note: is prefix o- correct?


LEXICON ANSTEM_kw
< "@U.noun.abs@" >  ANSTEMS_kw ;

< 0:n 0:i "@U.noun.1sg@" 0:"t2" > ANSTEMLIST_kw ; ! 1
< 0:k 0:i "@U.noun.2sg@" 0:"t2" > ANSTEMLIST_kw ; ! 2
< 0:o "@U.noun.3sg@" 0:"t2" > ANSTEMLIST_kw ;     ! 3
< 0:o "@U.noun.3isg@" 0:"t2" > ANSTEMLIST_kw ;    ! 3-singular-obviative
< 0:n 0:i "@U.noun.1pl@" 0:"t2" > ANSTEMLIST_kw ;   ! exclusive Pl
< 0:k 0:i "@U.noun.12pl@" 0:"t2" > ANSTEMLIST_kw ; ! inclusive Pl
< 0:k 0:i "@U.noun.2pl@" 0:"t2" > ANSTEMLIST_kw ;  ! 2pl 
< 0:o "@U.noun.3pl@" 0:"t2" > ANSTEMLIST_kw ;       ! 3pl
< 0:o "@U.noun.3ipl@" 0:"t2" > ANSTEMLIST_kw ;       ! 3-plural-obviative



!! !!The stems

!! !Animate Nouns
!    =============

LEXICON ANSTEMS
 :%> ANSTEMLIST ;

LEXICON ANSTEMLIST
inini ANDECL "man" ; 
nâpês ANDECL "boy" ;
apiscacihkos ANDECL "antelope" ;
mâyatihk ANDECL "bighorn sheep" ;
atihk ANDECL "caribou" ;
apisimôsos ANDECL "deer" ; ! apisimôsosak
mistatim IRREGULARDECL "horse" ; ! mistatimwak
môswa ANDECL "moose" ; ! môswak
maskwa ANDECL "bear" ; ! maskwak
okistatowân ANDECL "grizzly bear" ; ! okistatowânak
wâpask ANDECL "polar bear" ; ! wâpaskwak
sîsîp ANDECL "duck" ;
iskwêw ANDECL ;         !"a woman"
asikan ANDECL "sock" ; ! this really is animate
wâwaskesiw ANDECL "elk" ;
oskâtâsk ANDECL "carrot" ;
minôs ANDECL "cat" ;
pisiw	ANDECL "cougar" ;
nehiyaw ANDECL "Cree Person" ;
asiniy ANDECL "stone" ;
sikâk ANDECL "skunk" ;


LEXICON IRREGANSTEMS
 :%> IRREGANSTEMSLIST ;

LEXICON IRREGANSTEMSLIST
atim IRREGULARDECL "dog" ;
mistatim IRREGULARDECL "horse" ; ! mistatimwak

LEXICON ANSTEMS_kw
 :%> ANSTEMLIST_kw ;


LEXICON ANSTEMLIST_kw				
!!These nouns take an additional suffix in the
!! possessive paradigm
wâposw ANKWDECL ;
amiskw ANKWDECL ;
mistikw ANKWDECL ;
askihkw ANKWDECL ;


LEXICON INALIENABLESTEMS
maniway:%>niway INALIENABLEDECL ;        !"someone's cheek"


!! !Inanimate Nouns
!   ===============

LEXICON INSTEMS
môhkomân INDECL "knife" ;
sakâw INDECL "bush" ;
mîcimâpoy INDECL "soup" ;
sîsîpiwâwi INDECL "duck egg" ;
wâwi INDECL "egg" ; ! <== Analysed with hfst, but not with xfst.
ôtênaw INDECL "city" ;
maskisin INDECL "shoe" ;
mistikowat INDECL "box" ;
pikiw	INDECL "gum" ;
iskwâhtêm	INDECL "door" ;


! These are nonsens forms for debugging, to be removed after an evt.  xfst fix.
! The issue: There is one string, wâw, that does not work with xfst.
! With hfst, everything works. Thus, eventual xfst updates should be
! tested for wâwa etc.
! wâki	ANDECL "Trond testing" ;
! wâxa	ANDECL "Trond testing" ;
! xâwo	ANDECL "Trond testing" ;
! wêwa	ANDECL "Trond testing" ; ! works
! wâwa	ANDECL "Trond testing" ; ! the only one that does not work: w â w
! awâwa	ANDECL "Trond testing" ; ! the only one that does not work: w â w
! kwâwi	ANDECL "Trond testing" ; ! the only one that does not work: w â w




















