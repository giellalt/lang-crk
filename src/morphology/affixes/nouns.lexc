
! ========================================================================== !
!!                           !!!Plains Cree noun morphology                           
! ========================================================================== !

!! !!The declinations for Animates

LEXICON ANDECL !!= * @CODE@ for the animate declension
< "+N":0 "+AN":0 "+Sg":0 "@U.noun.abs@" >  # ; !
< "+N":0 "+AN":0         "@U.noun.abs@" >  OBVIATIVE ; !
< "+N":0 "+AN":0 "+Pl":0 "@U.noun.abs@" >  PL_AK ; !
< "+N":0 "+AN":0         "@U.noun.abs@" >  LOC ; !
+N+AN+Sg:%> AN_SUFF_SG ;
+N+AN+Pl:%> AN_SUFF_PL ;
+N+AN:%> AN_DIM_SG ;
+N+AN:%> AN_DIM_PL ;
+N+AN:%> AN_SUFF_LOC ;
+N+AN:%> AN_DIM_ABS ;


LEXICON ANDECLPROF !!= Declensions for nouns taking reduced diminutive at by more proficient speakers -> ie already carrying the reduced diminuitve suffixes
< "+N":0 "+AN":0 "+Sg":0 "@U.noun.abs@" >  # ; !
< "+N":0 "+AN":0 "+Pl":0 "@U.noun.abs@" >  PL_AK ; !
< "+N":0 "+AN":0 "@U.noun.abs@" >  LOC ; !
+N+AN+Sg:%> AN_SUFF_SG ;
+N+AN+Pl:%> AN_SUFF_PL ;
+N+AN:%> AN_SUFF_LOC ;

LEXICON IRREGULARDECL	!!= * @CODE@ is there to handle the highly irregular atim and mistatim
< "+N":0 "+AN":0 "+Sg":0 "@U.noun.abs@" >  # ; !
< "+N":0 "+AN":0 "+Pl":0 "@U.noun.abs@" >  PL_WAK ; !
!< "+N":0 "+AN":0 "@U.noun.abs@" >  LOC ; !
< "+N":0 "+AN":0 "@U.noun.abs@" >  IRREG_LOC ; !
< "+N":0 "+AN":0 "@U.noun.abs@" >  IRREG_OBVIATIVE ; !
+N+AN: AN_DIM_IRR_ABS ;

LEXICON IRREGULARINFL !!= * @CODE@ is also there to handle the highly irregular atim and mistatim
+N+AN+Sg: AN_IRREG_SUFF_SG ;
+N+AN+Pl: AN_IRREG_SUFF_PL ;
+N+AN: AN_DIM_IRR_SG ;
+N+AN: AN_DIM_IRR_PL ;
+N+AN:%> AN_IRREG_SUFF_LOC ;

LEXICON ANKWDECL !!= * @CODE@ takes -om as extra suffix in possessed form
< "+N":0 "+AN":0 "+Sg":0 "@U.noun.abs@" >  # ; !
< "+N":0 "+AN":0 "+Pl":0 "@U.noun.abs@" >  PL_AK ; !
< "+N":0 "+AN":0 "@U.noun.abs@" >  LOC ; !
+N+AN+Sg: ANKWDECL_SUFF_SG ;
+N+AN+Pl: ANKWDECL_SUFF_PL ;



!! !!The declinations for Inanimates

LEXICON INDECL !!= * @CODE@ for the inanimate declension
< "+N":0 "+IN":0 "+Sg":0 "@U.noun.abs@" >  # ; !
< "+N":0 "+IN":0 "+Pl":0 "@U.noun.abs@" >  PL_A ; !
< "+N":0 "+IN":0 "@U.noun.abs@" >  LOC ; !
+N+IN+Sg:%> IN_SUFF_SG ;
+N+IN+Pl:%> IN_SUFF_PL ;
+N+IN:%> IN_DIM_SG ;
+N+IN:%> IN_DIM_PL ;
+N+IN:%> IN_DIM_LOC ;
+N+IN:%> IN_SUFF_LOC ;

LEXICON INDECLPROF !!= Declensions for nouns taking reduced diminutive at by more proficient speakers -> ie already carrying the reduced diminuitve suffixes
< "+N":0 "+IN":0 "+Sg":0 "@U.noun.abs@" >  # ; !
< "+N":0 "+IN":0 "+Pl":0 "@U.noun.abs@" >  PL_A ; !
< "+N":0 "+IN":0 "@U.noun.abs@" >  LOC ; !
+N+IN+Sg:%> IN_SUFF_SG ;
+N+IN+Pl:%> IN_SUFF_PL ;
+N+IN:%> IN_SUFF_LOC ;






!! !!Diminutive suffixes

!!  !Diminutive suffixes in animate stems

LEXICON AN_DIM_SG !!= * @CODE@
< "+Dim":i 0:s 0:%^DIM 0:i 0:s "+Sg":0 "@U.noun.abs@" >  SG_ ; !
< "+Dim":i 0:s 0:%^DIM 0:i 0:s "+Sg":0 >  AN_SUFF_SG ;

LEXICON AN_DIM_PL !!= * @CODE@
< "+Dim":i 0:s 0:%^DIM 0:i 0:s "+Pl":0 "@U.noun.abs@" >  PL_AK ; !
< "+Dim":i 0:s 0:%^DIM 0:i 0:s "+Pl":0 >   AN_SUFF_PL ; !

LEXICON AN_DIM_ABS
< "+Dim":s 0:%^DIM 0:i 0:s >  AN_SUFF_LOC ; !

!! !Diminutive suffixes in animate stems taking the form -osis

LEXICON AN_DIM_IRR_SG !!= * @CODE@
< "+Dim":i 0:s 0:%^DIM 0:i 0:s "+Sg":0 >   AN_IRREG_SUFF_SG ; !
< "+Dim":i 0:s 0:%^DIM 0:i 0:s >   AN_IRREG_SUFF_LOC ;

LEXICON AN_DIM_IRR_PL !!= * @CODE@
< "+Dim":i 0:s 0:%^DIM 0:i 0:s "+Pl":0 >   AN_IRREG_SUFF_PL ; !

LEXICON AN_DIM_IRR_ABS !!= * @CODE@
< "+Dim":o 0:s 0:%^DIM 0:i 0:s "+Sg":0 "@U.noun.abs@" >  SG_ ; !
< "+Dim":o 0:s 0:%^DIM 0:i 0:s "+Pl":0 "@U.noun.abs@" >  PL_AK ; !
< "+Dim":o 0:s 0:%^DIM 0:i 0:s         "@U.noun.abs@" >  LOC ; !
< "+Dim":o 0:s 0:%^DIM 0:i 0:s         "@U.noun.abs@" >  LOC ; !

!!  !Diminutive suffixes in inanimate stems

LEXICON IN_DIM_SG !!= * @CODE@
< "+Dim":i 0:s 0:%^DIM 0:i 0:s "+Sg":0 "@U.noun.abs@" >  SG_ ; !
< "+Dim":i 0:s 0:%^DIM 0:i 0:s "+Sg":0 >  IN_SUFF_SG ;

LEXICON IN_DIM_PL !!= * @CODE@
< "+Dim":i 0:s 0:%^DIM 0:i 0:s "+Pl":0 "@U.noun.abs@" >  PL_A ; !
< "+Dim":i 0:s 0:%^DIM 0:i 0:s "+Pl":0 >   IN_SUFF_PL ; !

LEXICON IN_DIM_LOC !!= * @CODE@
< "+Dim":i 0:s 0:%^DIM 0:i 0:s "@U.noun.abs@" >  LOC ; !
< "+Dim":i 0:s 0:%^DIM 0:i 0:s > IN_SUFF_LOC ;








!! !!The inflectional suffixes


!! !Possessive suffixes

LEXICON INAL_SUFF_SG !!= * @CODE@ 
< "+Px3iSg":0 "@U.noun.3isg@" > SG_ ; !
< "+Px3iPl":0 "@U.noun.3ipl@" > SG_waaw ; !
AN_SUFF_SG_INAL ; ! To match yaml file, check whether it is correct, if not, return to AN_SUFF_SG



!! !!Animate inflectional suffixes

LEXICON AN_SUFF_SG !!= * @CODE@ 
< "+Px1Sg":0                  "@U.noun.1sg@"  > SG_ ; !
< "+Px2Sg":0                  "@U.noun.2sg@"  > SG_ ; !
< "+Px3Sg":0                  "@U.noun.3sg@"  > SG_ ; !
< "+Px3iSg":0 0:i 0:y 0:i 0:w "@U.noun.3isg@" > SG_ ; !
< "+Px1Pl":0  0:n 0:â 0:n     "@U.noun.1pl@"  > SG_ ; ! exclusive Pl -nân not -inân CHECK ??
< "+Px12Pl":0 0:n 0:â 0:n     "@U.noun.12pl@" > SG_ ; ! inclusive Pl CHECK OK? 
< "+Px2Pl":0  0:â 0:w         "@U.noun.2pl@"  > SG_ ; !
< "+Px3Pl":0  0:â 0:w         "@U.noun.3pl@"  > SG_ ; !
< "+Px3iPl":0 0:i 0:y 0:i 0:w "@U.noun.3ipl@" > SG_ ; ! obviative plural possessor - Okimasis corrected

LEXICON AN_SUFF_SG_INAL !!= * @CODE@ , see above for relation to AN_SUFF_SG
< "+Px1Sg":0                  "@U.noun.1sg@"  > SG_ ; !
< "+Px2Sg":0                  "@U.noun.2sg@"  > SG_ ; !
< "+Px3Sg":0                  "@U.noun.3sg@"  > SG_ ; !
< "+Px3iSg":0 0:i 0:y 0:i 0:w "@U.noun.3isg@" > SG_ ; !
< "+Px1Pl":0  0:n 0:â 0:n     "@U.noun.1pl@"  > SG_ ; ! exclusive Pl -nân not -inân CHECK ??
< "+Px12Pl":0 0:n 0:â 0:n     "@U.noun.12pl@" > SG_ ; ! inclusive Pl CHECK OK? 
< "+Px2Pl":0 0:w  0:â 0:w     "@U.noun.2pl@"  > SG_ ; !
< "+Px3Pl":0  0:w 0:â 0:w     "@U.noun.3pl@"  > SG_ ; !
! < "+Px3iPl":0 0:y 0:i 0:w     "@U.noun.3ipl@" > SG_ ; ! obviative plural possessor, not in yaml


LEXICON AN_SUFF_PL !!= * @CODE@ 
< "+Px1Sg":0                   "@U.noun.1sg@"  >  PL_AK ; !
< "+Px2Sg":0                   "@U.noun.2sg@"  >  PL_AK ; !
< "+Px3Sg":0                   "@U.noun.3sg@"  >  PL_A ; !3rd person  possessor, hence Pl is -a
< "+Px3iSg":0 0:i 0:y 0:i 0:w  "@U.noun.3isg@" >  PL_A ; ! 3rd person  possessor, hence Pl is -a
< "+Px1Pl":0  0:i 0:n 0:â 0:n  "@U.noun.1pl@"  >  PL_AK ;   ! exclusive Pl
< "+Px12Pl":0 0:n 0:a 0:w      "@U.noun.12pl@" >  PL_AK ; ! inclusive Pl
< "+Px2Pl":0  0:i 0:w 0:â 0:w  "@U.noun.2pl@"  >  PL_AK ; !
< "+Px3Pl":0  0:w 0:â 0:w      "@U.noun.3pl@"  >  PL_A ; ! 3rd person  possessor, hence Pl is -a
< "+Px3iPl":0 0:y 0:i 0:w      "@U.noun.3isg@" >  PL_A ; ! 3rd person  possessor, hence Pl is -a

LEXICON AN_SUFF_LOC
< "+Px1Sg":0                  "@U.noun.1sg@"  > LOC ; !
< "+Px2Sg":0                  "@U.noun.2sg@"  > LOC ; !
< "+Px3Sg":0                  "@U.noun.3sg@"  > LOC ; !
< "+Px3iSg":0 0:i 0:y 0:i 0:w "@U.noun.3isg@" > LOC ; !
< "+Px1Pl":0  0:i 0:n 0:â 0:n "@U.noun.1pl@"  > LOC ;   ! exclusive Pl
< "+Px12Pl":0 0:i 0:n 0:a 0:w "@U.noun.12pl@" > LOC ; ! inclusive Pl
< "+Px2Pl":0  0:i 0:w 0:â 0:w "@U.noun.2pl@"  > LOC ; !
< "+Px3Pl":0  0:i 0:w 0:â 0:w "@U.noun.3pl@"  > LOC ; !
< "+Px3iPl":i 0:y 0:i 0:w     "@U.noun.3isg@" > LOC ; ! obviative plural possessor




!! !!Inanimate inflectional suffixes


LEXICON IN_SUFF_SG !!= * @CODE@ 
< "+Px1Sg":0                  "@U.noun.1sg@"  > SG_ ; !
< "+Px2Sg":0                  "@U.noun.2sg@"  > SG_ ; !
< "+Px3Sg":0                  "@U.noun.3sg@"  > SG_ ; !
< "+Px3iSg":0 0:i 0:y 0:i 0:w "@U.noun.3isg@" > SG_ ; !
< "+Px1Pl":0  0:i 0:n 0:â 0:n "@U.noun.1pl@"  > SG_ ;   ! exclusive Pl
< "+Px12Pl":0 0:i 0:n 0:a 0:w "@U.noun.12pl@" > SG_ ; ! inclusive Pl
< "+Px2Pl":0  0:i 0:w 0:â 0:w "@U.noun.2pl@"  > SG_ ; !
< "+Px3Pl":0  0:i 0:w 0:â 0:w "@U.noun.3pl@"  > SG_ ; !
< "+Px3iPl":i 0:y 0:i 0:w     "@U.noun.3isg@" > SG_ ; ! obviative plural possessor


LEXICON IN_SUFF_PL !!= * @CODE@ 
< "+Px1Sg":0                   "@U.noun.1sg@"  > PL_A ; !
< "+Px2Sg":0                   "@U.noun.2sg@"  > PL_A ; !
< "+Px3Sg":0                   "@U.noun.3sg@"  > PL_A ; !
< "+Px3iSg":0 0:i 0:y 0:i 0:w  "@U.noun.3isg@" > PL_A ; !
< "+Px1Pl":0  0:i 0:n 0:â 0:n  "@U.noun.1pl@"  > PL_A ;   ! exclusive Pl
< "+Px12Pl":0 0:i 0:n 0:a 0:w  "@U.noun.12pl@" > PL_A ; ! inclusive Pl
< "+Px2Pl":0  0:i 0:w 0:â 0:w  "@U.noun.2pl@"  > PL_A ; !
< "+Px3Pl":0  0:i 0:w 0:â 0:w  "@U.noun.3pl@"  > PL_A ; !
< "+Px3iPl":i 0:y 0:i 0:w      "@U.noun.3isg@" > PL_A ; ! obviative plural possessor


LEXICON IN_SUFF_LOC
< "+Px1Sg":0                  "@U.noun.1sg@"  > LOC ; !
< "+Px2Sg":0                  "@U.noun.2sg@"  > LOC ; !
< "+Px3Sg":0                  "@U.noun.3sg@"  > LOC ; !
< "+Px3iSg":0 0:i 0:y 0:i 0:w "@U.noun.3isg@" > LOC ; !
< "+Px1Pl":0  0:i 0:n 0:â 0:n "@U.noun.1pl@"  > LOC ;   ! exclusive Pl
< "+Px12Pl":0 0:i 0:n 0:a 0:w "@U.noun.12pl@" > LOC ; ! inclusive Pl
< "+Px2Pl":0  0:i 0:w 0:â 0:w "@U.noun.2pl@"  > LOC ; !
< "+Px3Pl":0  0:i 0:w 0:â 0:w "@U.noun.3pl@"  > LOC ; !
< "+Px3iPl":i 0:y 0:i 0:w     "@U.noun.3isg@" > LOC ; ! obviative plural possessor



LEXICON ANKWDECL_SUFF_SG !!= * @CODE@ 
< "+Px1Sg":0  0:o 0:m                     "@U.noun.1sg@"  > SG_ ; !
< "+Px2Sg":0  0:o 0:m                     "@U.noun.2sg@"  > SG_ ; !
< "+Px3Sg":0  0:o 0:m 0:a                 "@U.noun.3sg@"  > SG_ ; !
< "+Px3iSg":0 0:o 0:m 0:i 0:y 0:i 0:w 0:a "@U.noun.3isg@" > SG_ ; !
< "+Px1Pl":0  0:o 0:m 0:i 0:n 0:â 0:n     "@U.noun.1pl@"  > SG_ ;   ! exclusive Pl
< "+Px12Pl":0 0:o 0:m 0:i 0:n 0:a 0:w     "@U.noun.12pl@" > SG_ ; ! inclusive Pl
< "+Px2Pl":0  0:o 0:m 0:i 0:w 0:â 0:w     "@U.noun.2pl@"  > SG_ ; !
< "+Px3Pl":0  0:o 0:m 0:i 0:w 0:â 0:w 0:a "@U.noun.3pl@"  > SG_ ; !
< "+Px3iPl":0 0:o 0:m 0:i 0:y 0:i 0:w 0:a "@U.noun.3isg@" > SG_ ; ! obviative plural possessor


LEXICON ANKWDECL_SUFF_PL !!= * @CODE@ 
< "+Px1Sg":0  0:o 0:m                     "@U.noun.1sg@"  > PL_AK ; !
< "+Px2Sg":0  0:o 0:m                     "@U.noun.2sg@"  > PL_AK ; !
< "+Px3Sg":0  0:o 0:m 0:a                 "@U.noun.3sg@"  > SG_ ; !
< "+Px3iSg":0 0:o 0:m 0:i 0:y 0:i 0:w 0:a "@U.noun.3isg@" > SG_ ; !
< "+Px1Pl":0  0:o 0:m 0:i 0:n 0:â 0:n     "@U.noun.1pl@"  > PL_AK ;   ! exclusive Pl
< "+Px12Pl":0 0:o 0:m 0:i 0:n 0:a 0:w     "@U.noun.12pl@" > PL_AK ; ! inclusive Pl
< "+Px2Pl":0  0:o 0:m 0:i 0:w 0:â 0:w     "@U.noun.2pl@"  > PL_AK ; !
< "+Px3Pl":0  0:o 0:m 0:i 0:w 0:â 0:w 0:a "@U.noun.3pl@"  > SG_ ; !
< "+Px3iPl":0 0:o 0:m 0:i 0:y 0:i 0:w 0:a "@U.noun.3isg@" > SG_ ; ! obviative plural possessor


LEXICON INAL_SUFF_PL !!= * @CODE@ 
< "+Px3iSg":0 "@U.noun.3isg@" > PL_AK ; !
< "+Px3iPl":0 "@U.noun.3ipl@" > PL_waaw_AK ;
                               AN_SUFF_PL ;

LEXICON AN_IRREG_SUFF_SG !!= * @CODE@ 
< "+Px1Sg":0                  "@U.noun.1sg@"  > SG_ ; !
< "+Px2Sg":0                  "@U.noun.2sg@"  > SG_ ; !
< "+Px3Sg":0  "+Obv":0                "@U.noun.3sg@"  > PL_A ; !The continuation lexicon adds an -a as a suffix to mark the obviative, since this is a 3-3 situation
< "+Px3iSg":0 0:i 0:y 0:i 0:w "+Obv":0 "@U.noun.3isg@" > PL_A ; !The continuation lexicon adds an -a as a suffix to mark the obviative, since this is a 3-3 situation
< "+Px1Pl":0  0:i 0:n 0:â 0:n "@U.noun.1pl@"  > SG_ ; ! exclusive Pl
< "+Px12Pl":0 0:i 0:n 0:a 0:w "@U.noun.12pl@" > SG_ ; ! inclusive Pl
< "+Px2Pl":0  0:i 0:w 0:â 0:w "@U.noun.2pl@"  > SG_ ; !
< "+Px3Pl":0 0:i  0:w 0:â 0:w "+Obv":0   "@U.noun.3pl@"  > PL_A ; !The continuation lexicon adds an -a as a suffix to mark the obviative, since this is a 3-3 situation
< "+Px3iPl":0 0:i 0:y 0:i 0:w "+Obv":0   "@U.noun.3ipl@" > PL_A ; ! obviative plural possessor, requires final -a


LEXICON AN_IRREG_SUFF_PL !!= * @CODE@ 
< "+Px1Sg":0                  "@U.noun.1sg@"  >  PL_AK ; !
< "+Px2Sg":0                  "@U.noun.2sg@"  >  PL_AK ; !
< "+Px3Sg":0 "+Obv":0         "@U.noun.3sg@"  >  PL_A ; !
< "+Px3iSg":0 0:i 0:y 0:i 0:w "+Obv":0 "@U.noun.3isg@" >  PL_A ; !obviative plural possessor, hence Pl is -a
< "+Px1Pl":0  0:i 0:n 0:â 0:n "@U.noun.1pl@"  >  PL_AK ;   ! exclusive Pl
< "+Px12Pl":0 0:i 0:n 0:a 0:w     "@U.noun.12pl@" >  PL_AK ; ! inclusive Pl
< "+Px2Pl":0 0:i 0:w 0:â 0:w "@U.noun.2pl@"  >  PL_AK ; !
< "+Px3Pl":0 0:i 0:w 0:â 0:w "+Obv":0    "@U.noun.3pl@"  >  PL_A ; !
< "+Px3iPl":0 0:i 0:y 0:i 0:w "+Obv":0   "@U.noun.3isg@" >  PL_A ; ! obviative plural possessor, hence Pl is -a


LEXICON AN_IRREG_SUFF_LOC
< "+Px1Sg":0                  "@U.noun.1sg@"  > LOC ; !
< "+Px2Sg":0                  "@U.noun.2sg@"  > LOC ; !
< "+Px3Sg":0                  "@U.noun.3sg@"  > LOC ; !
< "+Px3iSg":0 0:i 0:y 0:i 0:w "@U.noun.3isg@" > LOC ; !
< "+Px1Pl":0  0:i 0:n 0:â 0:n "@U.noun.1pl@"  > LOC ;   ! exclusive Pl
< "+Px12Pl":0 0:i 0:n 0:a 0:w "@U.noun.12pl@" > LOC ; ! inclusive Pl
< "+Px2Pl":0  0:i 0:w 0:â 0:w "@U.noun.2pl@"  > LOC ; !
< "+Px3Pl":0  0:i 0:w 0:â 0:w "@U.noun.3pl@"  > LOC ; !
< "+Px3iPl":i 0:y 0:i 0:w     "@U.noun.3isg@" > LOC ; ! obviative plural possessor

!! !Singular/plural possessed suffixes


LEXICON SG_naan !!= * @CODE@ 
:%>nân SG_ ;


LEXICON SG_waaw !!= * @CODE@ 
:%>wâw SG_ ;


LEXICON PL_naan_AK !!= * @CODE@ 
:%>nânak # ;


LEXICON PL_waaw_AK !!= * @CODE@ 
:%>wâwak # ;


LEXICON PL_waaw_A !!= * @CODE@ 
:%>wâwa # ;


LEXICON SG_ !!= * @CODE@ 
 # ;


LEXICON PL_A !!= * @CODE@ 
:%>a # ;


LEXICON PL_AK !!= * @CODE@ 
:%>ak # ;


LEXICON PL_WAK !!= * @CODE@ 
:%>wak # ;


!! !Locative suffixes

LEXICON LOC !!= * @CODE@ 
+Loc:%>i%^LChk  # ;

LEXICON IRREG_LOC !!= * @CODE@ 
+Loc:%>ohk # ;


!!Obviative suffix
LEXICON OBVIATIVE
+Obv:%>a # ;

LEXICON IRREG_OBVIATIVE
+Obv:%>wa # ;
