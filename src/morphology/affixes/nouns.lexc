
! ========================================================================== !
!!                           !!! Plains Cree noun morphology                           
! ========================================================================== !

!! !!The declinations for animates and inanimates

LEXICON ANDECL 
< "+N":0 "+AN":0 "+Sg":0 "@U.noun.abs@" >  # ; !
< "+N":0 "+AN":0 "+Pl":0 "@U.noun.abs@" >  PL_AK ; !
< "+N":0 "+AN":0 "@U.noun.abs@" >  LOC ; !
+N+AN+Sg: AN_SUFF_SG ;
+N+AN+Pl: AN_SUFF_PL ;

LEXICON INDECL
< "+N":0 "+IN":0 "+Sg":0 "@U.noun.abs@" >  # ; !
< "+N":0 "+IN":0 "+Pl":0 "@U.noun.abs@" >  PL_A ; !
< "+N":0 "+IN":0 "@U.noun.abs@" >  LOC ; !

+N+IN+Sg: IN_SUFF_SG ;
+N+IN+Pl: IN_SUFF_PL ;

LEXICON INALIENABLEDECL
+N+AN+Sg: INAL_SUFF_SG ;
+N+AN+Pl: INAL_SUFF_PL ;

!! !!The suffixes

LEXICON INAL_SUFF_SG
< "+Px3iSg":0 "@U.noun.3isg@" > SG_ ; !
< "+Px3iPl":0 "@U.noun.3ipl@" > SG_waaw ; !
AN_SUFF_SG ;

LEXICON AN_SUFF_SG
< "+Px1Sg":0 "@U.noun.1sg@" > SG_ ; !
< "+Px2Sg":0 "@U.noun.2sg@" > SG_ ; !
< "+Px3Sg":0 "@U.noun.3sg@" > SG_ ; !
< "+Px3iSg":0 "@U.noun.3isg@" > SG_ ; !
< "+Px1Pl":0 "@U.noun.1pl@" > SG_naan ;   ! exclusive Pl
< "+Px12Pl":0 "@U.noun.12pl@" > SG_naan ; ! inclusive Pl
< "+Px2Pl":0 "@U.noun.2pl@" > SG_waaw ; !
< "+Px3Pl":0 "@U.noun.3pl@" > SG_waaw ; !

LEXICON INAL_SUFF_PL
< "+Px3iSg":0 "@U.noun.3isg@" > PL_AK ; !
< "+Px3iPl":0 "@U.noun.3ipl@" > PL_waaw_AK ;
AN_SUFF_PL ;

LEXICON AN_SUFF_PL
< "+Px1Sg":0 "@U.noun.1sg@" > PL_AK ; !
< "+Px2Sg":0 "@U.noun.2sg@" > PL_AK ; !
< "+Px3Sg":0 "@U.noun.3sg@" > PL_AK ; !
< "+Px1Pl":0 "@U.noun.1pl@" > PL_naan_AK ;   ! exclusive Pl
< "+Px12Pl":0 "@U.noun.12pl@" > PL_naan_AK ; ! inclusive Pl
< "+Px2Pl":0 "@U.noun.2pl@" > PL_waaw_AK ; !
< "+Px3Pl":0 "@U.noun.3pl@" > PL_waaw_AK ;

LEXICON IN_SUFF_SG
< "+Px1Sg":0 "@U.noun.1sg@" > SG_ ; !
< "+Px2Sg":0 "@U.noun.2sg@" > SG_ ; !
< "+Px3Sg":0 "@U.noun.3sg@" > SG_ ; !
< "+Px1Pl":0 "@U.noun.1pl@" 0:i > SG_naan ;   ! exclusive Pl
< "+Px12Pl":0 "@U.noun.12pl@" 0:i  > SG_naan ; ! inclusive Pl
< "+Px2Pl":0 "@U.noun.2pl@" 0:i  > SG_waaw ; !
< "+Px3Pl":0 "@U.noun.3pl@" 0:i  > SG_waaw ; !

LEXICON IN_SUFF_PL
< "+Px1Sg":0 "@U.noun.1sg@" > PL_AK ; !
< "+Px2Sg":0 "@U.noun.2sg@" > PL_AK ; !
< "+Px3Sg":0 "@U.noun.3sg@" > PL_A ; !
< "+Px1Pl":0 "@U.noun.1pl@" 0:i  > PL_naan_AK ;   ! exclusive Pl
< "+Px12Pl":0 "@U.noun.12pl@" 0:i  > PL_naan_AK ; ! inclusive Pl
< "+Px2Pl":0 "@U.noun.2pl@" 0:i  > PL_waaw_AK ; !
< "+Px3Pl":0 "@U.noun.3pl@" 0:i  > PL_waaw_A ;

! Suffixes

LEXICON SG_naan
:%>nân SG_ ;

LEXICON SG_waaw
:%>wâw SG_ ;

LEXICON PL_naan_AK
:%>nânak # ;

LEXICON PL_waaw_AK
:%>wâwak # ;

LEXICON PL_waaw_A
:%>wâwa # ;


LEXICON SG_
 # ;
LOC ;

LEXICON PL_A
:%>a # ;

LEXICON PL_AK
:%>ak # ;

LEXICON LOC
+Loc:%>i%^LChk  # ; ! taken from test
