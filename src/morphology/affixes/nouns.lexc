
! ========================================================================== !
!!                           !!! Plains Cree noun morphology                           
! ========================================================================== !

!! !!The declinations for animates and inanimates

LEXICON ANDECL 
< "+N":0 "+AN":0 "+Sg":0 "@U.noun.abs@" >  # ; !
< "+N":0 "+AN":0 "+Pl":0 "@U.noun.abs@" >  PL_AK ; !
< "+N":0 "+AN":0 "@U.noun.abs@" >  LOC ; !
+N+AN: AN_SUFF_SG ;
+N+AN+Pl: AN_SUFF_PL ;
+N+AN: DIM_SG ;
+N+AN: DIM_PL ;

LEXICON DIM_SG
< "+Dim":is "+Sg":0 "@U.noun.abs@" >  SG_ ; !
< "+Dim":is "+Sg":0 >  AN_SUFF_SG ; !

LEXICON DIM_PL
< "+Dim":is "+Pl":0 "@U.noun.abs@" >  PL_AK ; !
< "+Dim":is "+Pl":0 >  AN_SUFF_PL ; !


LEXICON INDECL
< "+N":0 "+IN":0 "+Sg":0 "@U.noun.abs@" >  # ; !
< "+N":0 "+IN":0 "+Pl":0 "@U.noun.abs@" >  PL_A ; !
< "+N":0 "+IN":0 "@U.noun.abs@" >  LOC ; !
+N+IN+Sg: IN_SUFF_SG ;
+N+IN+Pl: IN_SUFF_PL ;

LEXICON ANKWDECL 									 
!!These take om as extra suffix in possessed form
< "+N":0 "+AN":0 "+Sg":0 "@U.noun.abs@" >  # ; !
< "+N":0 "+AN":0 "+Pl":0 "@U.noun.abs@" >  PL_AK ; !
< "+N":0 "+AN":0 "@U.noun.abs@" >  LOC ; !
+N+AN+Sg: ANKWDECL_SUFF_SG ;
+N+AN+Pl: ANKWDECL_SUFF_PL ;

LEXICON INALIENABLEDECL
+N+AN+Sg: INAL_SUFF_SG ;
+N+AN+Pl: INAL_SUFF_PL ;

LEXICON IRREGULARDERIVATION
 : IRREGULARDECL ;

LEXICON IRREGULARDECL				!!This category is there to handle the highly irregular atim and mistatim
< "+N":0 "+AN":0 "+Sg":0 "@U.noun.abs@" >  # ; !
< "+N":0 "+AN":0 "+Pl":0 "@U.noun.abs@" >  PL_WAK ; !
< "+N":0 "+AN":0 "@U.noun.abs@" >  LOC ; !
+N+AN+Sg: AN_IRREG_SUFF_SG ;
+N+AN+Pl: AN_IRREG_SUFF_PL ;


!! !!The suffixes

LEXICON INAL_SUFF_SG
< "+Px3iSg":0 "@U.noun.3isg@" > SG_ ; !
< "+Px3iPl":0 "@U.noun.3ipl@" > SG_waaw ; !
AN_SUFF_SG ;


LEXICON AN_SUFF_SG
< "+Px1Sg":0 "@U.noun.1sg@"  > SG_ ; !
< "+Px2Sg":0                  "@U.noun.2sg@"  > SG_ ; !
< "+Px3Sg":0                  "@U.noun.3sg@"  > SG_ ; !
< "+Px3iSg":0 0:i 0:y 0:i 0:w "@U.noun.3isg@" > SG_ ; !
< "+Px1Pl":0  0:n 0:â 0:n     "@U.noun.1pl@"  > SG_ ; ! exclusive Pl -nân not -inân CHECK ??
< "+Px12Pl":0 0:n 0:â 0:n     "@U.noun.12pl@" > SG_ ; ! inclusive Pl CHECK OK? 
< "+Px2Pl":0  0:â 0:w         "@U.noun.2pl@"  > SG_ ; !
< "+Px3Pl":0  0:â 0:w         "@U.noun.3pl@"  > SG_ ; !
< "+Px3iPl":0 0:y 0:i 0:w     "@U.noun.3ipl@" > SG_ ; ! obviative plural possessor


LEXICON AN_SUFF_PL
< "+Px1Sg":0 "@U.noun.1sg@" >  PL_AK ; !
< "+Px2Sg":0 "@U.noun.2sg@" >  PL_AK ; !
< "+Px3Sg":0 "@U.noun.3sg@" >  PL_AK ; !
< "+Px3iSg":0 0:i 0:y 0:i 0:w "@U.noun.3isg@" >  PL_A ; !obviative plural possessor, hence Pl is -a
< "+Px1Pl":0  0:i 0:n 0:â 0:n "@U.noun.1pl@"  >  PL_AK ;   ! exclusive Pl
< "+Px12Pl":0 0:n 0:a 0:w     "@U.noun.12pl@" >  PL_AK ; ! inclusive Pl
< "+Px2Pl":0  0:i 0:w 0:â 0:w "@U.noun.2pl@"  >  PL_AK ; !
< "+Px3Pl":0  0:w 0:â 0:w     "@U.noun.3pl@"  >  PL_AK ; !
< "+Px3iPl":0 0:y 0:i 0:w     "@U.noun.3isg@" >  PL_A ; ! obviative plural possessor, hence Pl is -a


LEXICON IN_SUFF_SG
< "+Px1Sg":0 "@U.noun.1sg@" > SG_ ; !
< "+Px2Sg":0 "@U.noun.2sg@" > SG_ ; !
< "+Px3Sg":0 "@U.noun.3sg@" > SG_ ; !
< "+Px3iSg":0 0:i 0:y 0:i 0:w "@U.noun.3isg@" > SG_ ; !
< "+Px1Pl":0  0:i 0:n 0:â 0:n "@U.noun.1pl@"  > SG_ ;   ! exclusive Pl
< "+Px12Pl":0 0:n 0:a 0:w     "@U.noun.12pl@" > SG_ ; ! inclusive Pl
< "+Px2Pl":0  0:i 0:w 0:â 0:w "@U.noun.2pl@"  > SG_ ; !
< "+Px3Pl":0  0:w 0:â 0:w     "@U.noun.3pl@"  > SG_ ; !
< "+Px3iPl":0 0:y 0:i 0:w     "@U.noun.3isg@" > SG_ ; ! obviative plural possessor


LEXICON IN_SUFF_PL
< "+Px1Sg":0 "@U.noun.1sg@" > PL_A ; !
< "+Px2Sg":0 "@U.noun.2sg@" > PL_A ; !
< "+Px3Sg":0 "@U.noun.3sg@" > PL_A ; !
< "+Px3iSg":0 0:i 0:y 0:i 0:w "@U.noun.3isg@" > PL_A ; !
< "+Px1Pl":0  0:i 0:n 0:â 0:n "@U.noun.1pl@" > PL_A ;   ! exclusive Pl
< "+Px12Pl":0 0:n 0:a 0:w     "@U.noun.12pl@" > PL_A ; ! inclusive Pl
< "+Px2Pl":0  0:i 0:w 0:â 0:w "@U.noun.2pl@" > PL_A ; !
< "+Px3Pl":0  0:w 0:â 0:w     "@U.noun.3pl@" > PL_A ; !
< "+Px3iPl":0 0:y 0:i 0:w     "@U.noun.3isg@" > PL_A ; ! obviative plural possessor


LEXICON ANKWDECL_SUFF_SG
< "+Px1Sg":0  0:o 0:m                     "@U.noun.1sg@"  > SG_ ; !
< "+Px2Sg":0  0:o 0:m                     "@U.noun.2sg@"  > SG_ ; !
< "+Px3Sg":0  0:o 0:m 0:a                 "@U.noun.3sg@"  > SG_ ; !
< "+Px3iSg":0 0:o 0:m 0:i 0:y 0:i 0:w 0:a "@U.noun.3isg@" > SG_ ; !
< "+Px1Pl":0  0:o 0:m 0:i 0:n 0:â 0:n     "@U.noun.1pl@"  > SG_ ;   ! exclusive Pl
< "+Px12Pl":0 0:o 0:m 0:i 0:n 0:a 0:w     "@U.noun.12pl@" > SG_ ; ! inclusive Pl
< "+Px2Pl":0  0:o 0:m 0:i 0:w 0:â 0:w     "@U.noun.2pl@"  > SG_ ; !
< "+Px3Pl":0  0:o 0:m 0:i 0:w 0:â 0:w 0:a "@U.noun.3pl@"  > SG_ ; !
< "+Px3iPl":0 0:o 0:m 0:i 0:y 0:i 0:w 0:a "@U.noun.3isg@" > SG_ ; ! obviative plural possessor


LEXICON ANKWDECL_SUFF_PL
< "+Px1Sg":0  0:o 0:m                     "@U.noun.1sg@"  > PL_AK ; !
< "+Px2Sg":0  0:o 0:m                     "@U.noun.2sg@"  > PL_AK ; !
< "+Px3Sg":0  0:o 0:m 0:a                 "@U.noun.3sg@"  > SG_ ; !
< "+Px3iSg":0 0:o 0:m 0:i 0:y 0:i 0:w 0:a "@U.noun.3isg@" > SG_ ; !
< "+Px1Pl":0  0:o 0:m 0:i 0:n 0:â 0:n     "@U.noun.1pl@"  > PL_AK ;   ! exclusive Pl
< "+Px12Pl":0 0:o 0:m 0:i 0:n 0:a 0:w     "@U.noun.12pl@" > PL_AK ; ! inclusive Pl
< "+Px2Pl":0  0:o 0:m 0:i 0:w 0:â 0:w     "@U.noun.2pl@"  > PL_AK ; !
< "+Px3Pl":0  0:o 0:m 0:i 0:w 0:â 0:w 0:a "@U.noun.3pl@"  > SG_ ; !
< "+Px3iPl":0 0:o 0:m 0:i 0:y 0:i 0:w 0:a "@U.noun.3isg@" > SG_ ; ! obviative plural possessor


LEXICON INAL_SUFF_PL
< "+Px3iSg":0 "@U.noun.3isg@" > PL_AK ; !
< "+Px3iPl":0 "@U.noun.3ipl@" > PL_waaw_AK ;
                               AN_SUFF_PL ;


LEXICON AN_IRREG_SUFF_SG
< "+Px1Sg":0                  "@U.noun.1sg@"  > SG_ ; !
< "+Px2Sg":0                  "@U.noun.2sg@"  > SG_ ; !
< "+Px3Sg":0                  "@U.noun.3sg@"  > SG_ ; !
< "+Px3iSg":0 0:i 0:y 0:i 0:w "@U.noun.3isg@" > SG_ ; !
< "+Px1Pl":0  0:i 0:n 0:â 0:n "@U.noun.1pl@"  > SG_ ; ! exclusive Pl
< "+Px12Pl":0 0:n 0:a 0:w     "@U.noun.12pl@" > SG_ ; ! inclusive Pl
< "+Px2Pl":0  0:i 0:w 0:â 0:w "@U.noun.2pl@"  > SG_ ; !
< "+Px3Pl":0  0:w 0:â 0:w     "@U.noun.3pl@"  > SG_ ; !
< "+Px3iPl":0 0:y 0:i 0:w     "@U.noun.3ipl@" > SG_ ; ! obviative plural possessor


LEXICON AN_IRREG_SUFF_PL
< "+Px1Sg":0                  "@U.noun.1sg@"  >  PL_WAK ; !
< "+Px2Sg":0                  "@U.noun.2sg@"  >  PL_WAK ; !
< "+Px3Sg":0                  "@U.noun.3sg@"  >  PL_WAK ; !
< "+Px3iSg":0 0:i 0:y 0:i 0:w "@U.noun.3isg@" >  PL_WAK ; !obviative plural possessor, hence Pl is -a
< "+Px1Pl":0  0:i 0:n 0:â 0:n "@U.noun.1pl@"  >  PL_WAK ;   ! exclusive Pl
< "+Px12Pl":0 0:n 0:a 0:w     "@U.noun.12pl@" >  PL_WAK ; ! inclusive Pl
< "+Px2Pl":0  0:i 0:w 0:â 0:w "@U.noun.2pl@"  >  PL_WAK ; !
< "+Px3Pl":0  0:w 0:â 0:w     "@U.noun.3pl@"  >  PL_WAK ; !
< "+Px3iPl":0 0:y 0:i 0:w     "@U.noun.3isg@" >  PL_WAK ; ! obviative plural possessor, hence Pl is -a


! Suffixes


LEXICON SG_naan
:%>nân SG_ ;


LEXICON SG_waaw
:%>wâw SG_ ;


LEXICON PL_naan_AK
:%>nânak # ;


LEXICON PL_waaw_AK
:%>wâwak # ;


LEXICON PL_waaw_A
:%>wâwa # ;


LEXICON SG_
 # ;
 LOC ;


LEXICON PL_A
:%>a # ;


LEXICON PL_AK
:%>ak # ;


LEXICON PL_WAK
:%>wak # ;


LEXICON LOC
+Loc:%>i%^LChk  # ; ! taken from test
