
! ========================================================================== !
!!                           !!!Plains Cree noun morphology                           
! ========================================================================== !

!! !!The declinations for Animates


LEXICON ANDECL !!= * @CODE@ for the animate declension, both -isis and -is diminutives
ANABSDECL ;
+N+AN+Sg:%> ANSUFF_SG ;
+N+AN+Pl:%> ANSUFF_PL ;
+N+AN:%> ANSUFF_LOC ;
+N+AN+Der/Dim:%>is%^DIM%> ANDIM ;
+N+AN+Der/Dim:%>is%^DIMis%> ANDIM ;


LEXICON ANimDECL !!= * @CODE@ for the animate declension, poss.-im-, dim. -is
                                     ANABSDECL  ;
             +N+AN+Sg:%>im          ANSUFF_SG  ;
             +N+AN+Pl:%>im          ANSUFF_PL  ;
                +N+AN:%>im          ANSUFF_LOC ;
        +N+AN+Der/Dim:%>is%^DIM%>    ANABSDECL  ; !
        +N+AN+Der/Dim:%>is%^DIMis%>    ANABSDECL  ; !ihkosis
   +N+AN+Der/Dim+N+AN:%>im%^DIMis%> ANSUFF_LOC ; !
+N+AN+Der/Dim+N+AN+Sg:%>im%^DIMis%> ANSUFF_SG  ; !
+N+AN+Der/Dim+N+AN+Pl:%>im%^DIMis%> ANSUFF_PL  ; !

LEXICON ANimDECLis !!= * @CODE@ for the animate declension, poss.-im-, dim. -is
                                     ANABSDECL  ;
             +N+AN+Sg:%>im          ANSUFF_SG  ;
             +N+AN+Pl:%>im          ANSUFF_PL  ;
                +N+AN:%>im          ANSUFF_LOC ;
        +N+AN+Der/Dim:%>is%^DIMis%>    ANABSDECL  ; !ihkosis
   +N+AN+Der/Dim+N+AN:%>im%^DIMisis%> ANSUFF_LOC ; !
+N+AN+Der/Dim+N+AN+Sg:%>im%^DIMisis%> ANSUFF_SG  ; !
+N+AN+Der/Dim+N+AN+Pl:%>im%^DIMisis%> ANSUFF_PL  ; !


LEXICON ANimDECLw !!= * @CODE@ for the animate declension, poss.-im-, dim. -is
< "+N":a "+AN":0 "+Sg":0 "@U.noun.abs@" > SG_  ; ! -a
< "+N":0 "+AN":0 "+Sg":0 "@U.noun.abs@" > OBVIATIVE  ;
< "+N":0 "+AN":0 "+Pl":0 "@U.noun.abs@" > PLak  ;
< "+N":0 "+AN":0 "@U.noun.abs@" > LOC  ;
             +N+AN+Sg:%>im          ANSUFF_SG  ;
             +N+AN+Pl:%>im          ANSUFF_PL  ;
                +N+AN:%>im          ANSUFF_LOC ;
!        +N+AN+Der/Dim:%>is%^DIM%>    ANABSDECL  ; !
        +N+AN+Der/Dim:%>is%^DIMis%>    ANABSDECL  ; !ihkosis
   +N+AN+Der/Dim+N+AN:%>im%^DIMis%> ANSUFF_LOC ; !
+N+AN+Der/Dim+N+AN+Sg:%>im%^DIMis%> ANSUFF_SG  ; !
+N+AN+Der/Dim+N+AN+Pl:%>im%^DIMis%> ANSUFF_PL  ; !


LEXICON ANDIM !!= * @CODE@ both for lexicalised and derivated diminutives
   +N+AN: ANSUFF_LOC ;
          ANABSDECL  ;
+N+AN+Sg: ANSUFF_SG  ;
+N+AN+Pl: ANSUFF_PL  ;

LEXICON ANABSDECL !!= * @CODE@ for the animate absolute declension
< "+N":0 "+AN":0 "@U.noun.abs@" >  NON_LOCahk ; !
!< "+N":i "+AN":n "@U.noun.abs@" >  LOCahk ; !

LEXICON NON_LOCahk !!= * @CODE@ for the animate absolute except LOC on ahk
 +Sg: SG_       ;
      OBVIATIVE ;
 +Pl: PLak      ;
      LOC       ;

!! !!The declinations for Inanimates

LEXICON INimDECLw-ONESYLL-SG !!= * @CODE@ for the inanimate declension, poss.-im-, dim. -is
< "+N":o "+IN":0 "+Sg":0 "@U.noun.abs@" >  SG_ ; ! Adds -o
< "+N":w "+IN":0 "@U.noun.abs@" >  LOC ; !
+N+IN+Sg:w%>im INSUFF_SG ;
+N+IN:w%>im INSUFF_LOC ;
+N+IN+Der/Dim:w%> INimDIM-SG ;

LEXICON INimDECL !!= * @CODE@ for the inanimate declension, poss.-im-, dim. -is
INABSDECL ;
+N+IN+Sg:%>im INSUFF_SG ;
+N+IN+Pl:%>im INSUFF_PL ;
+N+IN:%>im INSUFF_LOC ;
+N+IN+Der/Dim:%> INimDIM ;


LEXICON INDECL !!= * @CODE@ for the inanimate declension, diminutive: -is- and -isis-
INABSDECL ;
+N+IN+Sg:%> INSUFF_SG ;
+N+IN+Pl:%> INSUFF_PL ;
+N+IN:%> INSUFF_LOC ;
+N+IN+Der/Dim:%>is%^DIMis%> INDIM ;
+N+IN+Der/Dim:%>is%^DIM%> INDIM ;

LEXICON INDECLis !!= * @CODE@ for the inanimate declension, diminutive: -is- and -isis-
INABSDECL ;
+N+IN+Sg:%> INSUFF_SG ;
+N+IN+Pl:%> INSUFF_PL ;
+N+IN:%> INSUFF_LOC ;
+N+IN+Der/Dim:%>is%^DIM%> INDIM ;

LEXICON INDECLosi !!= * @CODE@ for the inanimate declension of ôsi, an irregular noun
< "+N":s "+IN":i "+Sg":0 "@U.noun.abs@" >  SG_ ; !
< "+N":s "+IN":0 "+Pl":0 "@U.noun.abs@" >  PLa ; !
< "+N":s "+IN":0 "@U.noun.abs@" >  LOC ; !
+N+IN+Sg:t%> INSUFF_SG ;
+N+IN+Pl:t%> INSUFF_PL ;
+N+IN:t%> INSUFF_LOC ;
+N+IN+Der/Dim:t%>is%^DIMis%> INDIM ; 


LEXICON INDIM !!= * @CODE@ for the inanimate diminutives
INABSDECL ;
+N+IN: INSUFF_LOC ;
+N+IN+Sg: INSUFF_SG ;
+N+IN+Pl: INSUFF_PL ;

LEXICON INABSDECL !!= * @CODE@ for inanimate absolute declension
< "+N":0 "+IN":0 "+Sg":0 "@U.noun.abs@" >  SG_ ; !
< "+N":0 "+IN":0 "+Pl":0 "@U.noun.abs@" >  PLa ; !
< "+N":0 "+IN":0 "@U.noun.abs@" >  LOC ; !
!< "+N":0 "+IN":0 "@U.noun.abs@" >  LOCahk ; !

LEXICON INimDIM !!= * @CODE@ the diminutives with poss.-im-, dim. -is
< "+N":i "+IN":s 0:%^DIM "@U.noun.abs@" >  LOC ; !
+N+IN:im%^DIMis%> INSUFF_LOC ;
< "+N":i "+IN":s "+Sg":%^DIM "@U.noun.abs@" >  SG_ ; !
< "+N":i "+IN":s "+Pl":%^DIM "@U.noun.abs@" >  PLak ; !
+N+IN+Sg:im%^DIM%>is INSUFF_SG ;
+N+IN+Pl:im%^DIM%>is INSUFF_PL ;

LEXICON INimDIM-SG !!= * @CODE@ the diminutives with poss.-im-, dim. -is
< "+N":i "+IN":s 0:%^DIM "@U.noun.abs@" >  LOC ; !
+N+IN:im%^DIMis%> INSUFF_LOC ;
< "+N":i "+IN":s "+Sg":%^DIM "@U.noun.abs@" >  SG_ ; !
+N+IN+Sg:im%^DIM%>is INSUFF_SG ;


!! LEXICON INDIMDECL Declensions inanimate diminutives, adding +Dim , not in use
!< "+N":0 "+IN":0 "+Sg":0 "+Dim":0 "@U.noun.abs@" >  # ; !
!< "+N":0 "+IN":0 "+Pl":0 "+Dim":0 "@U.noun.abs@" >  PLa ; !
!< "+N":0 "+IN":0 "+Dim":0 "@U.noun.abs@" >  LOC ; !
!+N+IN+Sg+Dim:%> INSUFF_SG ;
!+N+IN+Pl+Dim:%> INSUFF_PL ;
!+N+IN+Dim:%> INSUFF_LOC ;


!! !!Animate possessive suffixes

LEXICON ANSUFF_SG !!= * @CODE@
< "+Px1Sg":%^POS                 "@U.noun.1sg@"  > SG_ ; !
< "+Px2Sg":%^POS                 "@U.noun.2sg@"  > SG_ ; !
< "+Px3Sg":%^POS                 "@U.noun.3sg@"  > OBVIATIVE ; !
< "+Px4Sg":%^POS 0:i 0:y 0:i 0:w "@U.noun.3isg@" > OBVIATIVE ; !
< "+Px1Pl":%^POS 0:i 0:n 0:â 0:n "@U.noun.1pl@"  > SG_ ; ! exclusive Pl -nân not -inân CHECK ??
<"+Px12Pl":%^POS 0:i 0:n 0:a 0:w "@U.noun.12pl@" > SG_ ; ! inclusive Pl CHECK OK? 
< "+Px2Pl":%^POS 0:i 0:w 0:â 0:w "@U.noun.2pl@"  > SG_ ; !
< "+Px3Pl":%^POS 0:i 0:w 0:â 0:w "@U.noun.3pl@"  > OBVIATIVE ; !
< "+Px4Pl":%^POS 0:i 0:y 0:i 0:w "@U.noun.3ipl@" > OBVIATIVE ; ! obviative plural possessor - Okimasis corrected


LEXICON ANSUFF_PL !!= * @CODE@
< "+Px1Sg":%^POS                 "@U.noun.1sg@"  >  PLak ; !
< "+Px2Sg":%^POS                 "@U.noun.2sg@"  >  PLak ; !
< "+Px3Sg":%^POS                 "@U.noun.3sg@"  >  OBVIATIVE ; !
< "+Px4Sg":%^POS 0:i 0:y 0:i 0:w "@U.noun.3isg@" >  OBVIATIVE ; ! 
< "+Px1Pl":%^POS 0:i 0:n 0:â 0:n "@U.noun.1pl@"  >  PLak ;   ! 
<"+Px12Pl":%^POS 0:i 0:n 0:a 0:w "@U.noun.12pl@" >  PLak ; ! 
< "+Px2Pl":%^POS 0:i 0:w 0:â 0:w "@U.noun.2pl@"  >  PLak ; !
< "+Px3Pl":%^POS 0:i 0:w 0:â 0:w "@U.noun.3pl@"  >  OBVIATIVE ; ! 
< "+Px4Pl":%^POS 0:i 0:y 0:i 0:w "@U.noun.3isg@" >  OBVIATIVE ; ! 

LEXICON ANSUFF_LOC !!= * @CODE@
< "+Px1Sg":%^POS         "@U.noun.1sg@"  > LOC ; !
< "+Px2Sg":%^POS         "@U.noun.2sg@"  > LOC ; !
< "+Px3Sg":%^POS         "@U.noun.3sg@"  > LOC ; !
< "+Px4Sg":%^POS 0:i 0:y "@U.noun.3isg@" > LOC ; !
< "+Px1Pl":%^POS 0:i 0:n "@U.noun.1pl@"  > LOCahk ;   ! 
<"+Px12Pl":%^POS 0:i 0:n "@U.noun.12pl@" > LOCahk ; ! 
< "+Px2Pl":%^POS 0:i 0:w "@U.noun.2pl@"  > LOCahk ; !
< "+Px3Pl":%^POS 0:i 0:w "@U.noun.3pl@"  > LOCahk ; !
< "+Px4Pl":%^POS 0:i 0:y "@U.noun.3isg@" > LOC ; ! 




!! !!Inanimate possessive suffixes


LEXICON INSUFF_SG !!= * @CODE@ 
< "+Px1Sg":%^POS                  "@U.noun.1sg@"  > SG_ ; !
< "+Px2Sg":%^POS                  "@U.noun.2sg@"  > SG_ ; !
< "+Px3Sg":%^POS                  "@U.noun.3sg@"  > SG_ ; !
< "+Px4Sg":%^POS 0:i 0:y 0:i 0:w "@U.noun.3isg@" > SG_ ; !
< "+Px1Pl":%^POS  0:i 0:n 0:â 0:n "@U.noun.1pl@"  > SG_ ;   ! exclusive Pl
< "+Px12Pl":%^POS 0:i 0:n 0:a 0:w "@U.noun.12pl@" > SG_ ; ! inclusive Pl
< "+Px2Pl":%^POS  0:i 0:w 0:â 0:w "@U.noun.2pl@"  > SG_ ; !
< "+Px3Pl":%^POS  0:i 0:w 0:â 0:w "@U.noun.3pl@"  > SG_ ; !
< "+Px4Pl":%^POS 0:i 0:y 0:i 0:w "@U.noun.3isg@" > SG_ ; !


LEXICON INSUFF_PL !!= * @CODE@ 
< "+Px1Sg":%^POS                   "@U.noun.1sg@"  > PLa ; !
< "+Px2Sg":%^POS                   "@U.noun.2sg@"  > PLa ; !
< "+Px3Sg":%^POS                   "@U.noun.3sg@"  > PLa ; !
< "+Px4Sg":%^POS 0:i 0:y 0:i 0:w  "@U.noun.3isg@" > PLa ; !
< "+Px1Pl":%^POS  0:i 0:n 0:â 0:n  "@U.noun.1pl@"  > PLa ;   ! exclusive Pl
< "+Px12Pl":%^POS 0:i 0:n 0:a 0:w  "@U.noun.12pl@" > PLa ; ! inclusive Pl
< "+Px2Pl":%^POS  0:i 0:w 0:â 0:w  "@U.noun.2pl@"  > PLa ; !
< "+Px3Pl":%^POS  0:i 0:w 0:â 0:w  "@U.noun.3pl@"  > PLa ; !
< "+Px4Pl":%^POS 0:i 0:y 0:i 0:w      "@U.noun.3isg@" > PLa ; ! obviative plural possessor


LEXICON INSUFFohk_LOC !!= * @CODE@
< "+Px1Sg":%^POS                  "@U.noun.1sg@"  > ohk_LOC ; !
< "+Px2Sg":%^POS                  "@U.noun.2sg@"  > ohk_LOC ; !
< "+Px3Sg":%^POS                  "@U.noun.3sg@"  > ohk_LOC ; !
< "+Px4Sg":%^POS 0:o 0:y "@U.noun.3isg@" > LOC ; !
< "+Px1Pl":%^POS  0:o 0:n "@U.noun.1pl@"  > LOCahk ;   !
< "+Px12Pl":%^POS 0:o 0:n "@U.noun.12pl@" > LOCahk ; ! 
< "+Px2Pl":%^POS  0:o 0:w "@U.noun.2pl@"  > LOCahk ; !
< "+Px3Pl":%^POS  0:o 0:w "@U.noun.3pl@"  > LOCahk ; !
< "+Px4Pl":%^POS 0:o 0:y "@U.noun.3isg@" > LOC ; ! 

LEXICON INSUFFoSG !!= * @CODE@ 
< "+Px1Sg":%^POS                  "@U.noun.1sg@"  > SG_ ; !
< "+Px2Sg":%^POS                  "@U.noun.2sg@"  > SG_ ; !
< "+Px3Sg":%^POS                  "@U.noun.3sg@"  > SG_ ; !
< "+Px4Sg":%^POS 0:o 0:y 0:i 0:w "@U.noun.3isg@" > SG_ ; !
< "+Px1Pl":%^POS  0:o 0:n 0:â 0:n "@U.noun.1pl@"  > SG_ ;   ! 
< "+Px12Pl":%^POS 0:o 0:n 0:a 0:w "@U.noun.12pl@" > SG_ ; ! 
< "+Px2Pl":%^POS 0:o 0:w 0:â 0:w "@U.noun.2pl@"  > SG_ ; !
< "+Px3Pl":%^POS 0:o 0:w 0:â 0:w "@U.noun.3pl@"  > SG_ ; !
< "+Px4Pl":%^POS 0:o 0:y 0:i 0:w "@U.noun.3isg@" > SG_ ; !

LEXICON INSUFFoPL !!= * @CODE@ 
< "+Px1Sg":%^POS 0:w            "@U.noun.1sg@"  > PLa ; !
< "+Px2Sg":%^POS 0:w              "@U.noun.2sg@"  > PLa ; !
< "+Px3Sg":%^POS 0:w                  "@U.noun.3sg@"  > PLa ; !
< "+Px4Sg":%^POS 0:o 0:y 0:i 0:w  "@U.noun.3isg@" > PLa ; !
< "+Px1Pl":%^POS  0:o 0:n 0:â 0:n  "@U.noun.1pl@"  > PLa ;   !
< "+Px12Pl":%^POS 0:o 0:n 0:a 0:w  "@U.noun.12pl@" > PLa ; ! 
< "+Px2Pl":%^POS  0:o 0:w 0:â 0:w  "@U.noun.2pl@"  > PLa ; !
< "+Px3Pl":%^POS  0:o 0:w 0:â 0:w  "@U.noun.3pl@"  > PLa ; !
< "+Px4Pl":%^POS  0:o 0:y 0:i 0:w      "@U.noun.3isg@" > PLa ; ! 


LEXICON INSUFF_LOC !!= * @CODE@
< "+Px1Sg":%^POS                  "@U.noun.1sg@"  > LOC ; !
< "+Px2Sg":%^POS                  "@U.noun.2sg@"  > LOC ; !
< "+Px3Sg":%^POS                  "@U.noun.3sg@"  > LOC ; !
< "+Px4Sg":%^POS 0:i 0:y "@U.noun.3isg@" > LOC ; !
< "+Px1Pl":%^POS  0:i 0:n "@U.noun.1pl@"  > LOCahk ;   ! exclusive Pl
< "+Px12Pl":%^POS 0:i 0:n "@U.noun.12pl@" > LOCahk ; ! inclusive Pl
< "+Px2Pl":%^POS  0:i 0:w "@U.noun.2pl@"  > LOCahk ; !
< "+Px3Pl":%^POS  0:i 0:w "@U.noun.3pl@"  > LOCahk ; !
< "+Px4Pl":%^POS 0:i 0:y "@U.noun.3isg@" > LOC ; ! obviative plural possessor


LEXICON ANiySUFF_SG  !!= * @CODE@
< "+Px1Sg":%^POS 0:i 0:y                 "@U.noun.1sg@"  > SG_ ; !
< "+Px2Sg":%^POS 0:i 0:y                 "@U.noun.2sg@"  > SG_ ; !
< "+Px3Sg":%^POS 0:i 0:y                "@U.noun.3sg@"  > OBVIATIVE ; !The continuation lexicon adds an -a as a suffix to mark the obviative, since this is a 3-3 situation
< "+Px4Sg":%^POS 0:i 0:y 0:i 0:w "@U.noun.3isg@" > OBVIATIVE ; !The continuation lexicon adds an -a as a suffix to mark the obviative, since this is a 3-3 situation
< "+Px1Pl":%^POS  0:i 0:n 0:â 0:n "@U.noun.1pl@"  > SG_ ; ! exclusive Pl
< "+Px12Pl":%^POS 0:i 0:n 0:a 0:w "@U.noun.12pl@" > SG_ ; ! inclusive Pl
< "+Px2Pl":%^POS  0:i 0:w 0:â 0:w "@U.noun.2pl@"  > SG_ ; !
< "+Px3Pl":%^POS 0:i  0:w 0:â 0:w    "@U.noun.3pl@"  > OBVIATIVE ; !The continuation lexicon adds an -a as a suffix to mark the obviative, since this is a 3-3 situation
< "+Px4Pl":%^POS 0:i 0:y 0:i 0:w   "@U.noun.3ipl@" > OBVIATIVE ; ! obviative plural possessor, requires final -a


LEXICON ANiySUFF_PL !!= * @CODE@
< "+Px1Sg":%^POS 0:i 0:y                 "@U.noun.1sg@"  >  PLak ; !
< "+Px2Sg":%^POS 0:i 0:y                  "@U.noun.2sg@"  >  PLak ; !
< "+Px3Sg":%^POS 0:i 0:y         "@U.noun.3sg@"  >  OBVIATIVE ; !
< "+Px4Sg":%^POS 0:i 0:y 0:i 0:w  "@U.noun.3isg@" >  OBVIATIVE ; !obviative plural possessor, hence Pl is -a
< "+Px1Pl":%^POS  0:i 0:n 0:â 0:n "@U.noun.1pl@"  >  PLak ;   ! exclusive Pl
< "+Px12Pl":%^POS 0:i 0:n 0:a 0:w     "@U.noun.12pl@" >  PLak ; ! inclusive Pl
< "+Px2Pl":%^POS 0:i 0:w 0:â 0:w "@U.noun.2pl@"  >  PLak ; !
< "+Px3Pl":%^POS 0:i 0:w 0:â 0:w     "@U.noun.3pl@"  >  OBVIATIVE ; !
< "+Px4Pl":%^POS 0:i 0:y 0:i 0:w    "@U.noun.3isg@" >  OBVIATIVE ; ! obviative plural possessor, hence Pl is -a


!! !!Singular/plural suffixes


!LEXICON SG_naan  Not in use
!:%>nân SG_ ;


!LEXICON SGwaaw Not in use
!:%>wâw SG_ ;


!LEXICON PL_naan_ak  Not in use
!:%>nânak # ;


!LEXICON PLwaaw_ak  Not in use
!:%>wâwak # ;


!LEXICON PL_waaw_A  Not in use
!:%>wâwa # ;


LEXICON SG_ !!= * @CODE@ 
 # ;


LEXICON PLa !!= * @CODE@ 
:%>a # ;


LEXICON PLak !!= * @CODE@ 
:%>ak # ;


LEXICON PLwak !!= * @CODE@ 
:%>wak # ;


!! !!Locative suffixes

LEXICON LOC !!= * @CODE@ 
+Loc:%>i%^LChk  # ;

LEXICON ohk_LOC !!= * @CODE@ 
+Loc:%>o%^LChk # ;

LEXICON LOCahk !!= * @CODE@ 
+Loc:%>â%^LChk  # ;



!! !!Obviative suffix
LEXICON OBVIATIVE !!= * @CODE@
+Obv:%>a # ;

LEXICON ANwa_OBV !!= * @CODE@
+Obv:%>wa # ;

!! !!Special cases
LEXICON NOPREFIX !!= * @CODE@ gives diacr.flag for no prefix. Used for hardcoded forms in the lexicon
< "@U.noun.abs@" > # ;

