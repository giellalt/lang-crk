! Copyright © 2015–2019 Alberta Language Technology Lab (ALTLab)
! http://altlab.artsrn.ualberta.ca/
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU Affero General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! This program is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU Affero General Public License for more details.
!
! You should have received a copy of the GNU Affero General Public License
! along with this program. If not, see <https://www.gnu.org/licenses/>.

! ========================================================================== !
!! Plains Cree verb morphology                  
! ========================================================================== !




!! The Plains Cree verbs are divided in four groups:

!! 1. AI: Animate intransitive 
!! 1. II: Inanimate intransitive
!! 1. TA: Transitive animate
!! 1. TI: Transitive inanimate



!! # Prefixes


LEXICON VerbPrefixes  !!= @CODE@ divides the lexicon into four modes: independent, conjunctive, imperative and future conditional

@U.order.indep@ INDEPENDENT ;         !!= * @CODE@
@U.order.cnj@   CONJUNCT ;            !!= * @CODE@ 
                IMPERATIVE ;          !!= * @CODE@
                FUTURE_CONDITIONAL ;  !!= * @CODE@


LEXICON INDEPENDENT !!= @CODE@ gives flags and prefixes for personprefix
@U.person.NI@:@U.person.NI@nit2%< IND_TENSE ;
@U.person.KI@:@U.person.KI@kit2%< IND_TENSE ;
@U.person.NULL@                   IND_TENSE ;


LEXICON IND_TENSE !!= @CODE@ gives flags and prefixes for tense 
@U.tense.Prs@                                                             VERBPREFIXES ;
@U.tense.Prt@@P.joiner.NULL@:@U.tense.Prt@@P.joiner.NULL@kî^EGLOT%<       VERBPREFIXES ;
@U.tense.Prt@@P.joiner.hyphen@:@U.tense.Prt@@P.joiner.hyphen@kî-%<        VERBPREFIXES ;
@U.tense.FutDef@@P.joiner.NULL@:@U.tense.FutDef@@P.joiner.NULL@ka^EGLOT%< VERBPREFIXES ;
@U.tense.FutDef@@P.joiner.hyphen@:@U.tense.FutDef@@P.joiner.hyphen@ka-%<  VERBPREFIXES ;
@U.tense.FutInt@@P.joiner.NULL@:@U.tense.FutInt@@P.joiner.NULL@wî^EGLOT%< VERBPREFIXES ;
@U.tense.FutInt@@P.joiner.hyphen@:@U.tense.FutInt@@P.joiner.hyphen@wî-%<  VERBPREFIXES ;


LEXICON FUTURE_CONDITIONAL !!= @CODE@ gives flags for future conditional (no prefix)
@U.order.FutCon@ VERBPREFIXES ;


LEXICON CONJUNCT !!= @CODE@ gives flag for conjunct and prefix ê-, kâ-, kâki-
@P.joiner.hyphen@PV/e+:@P.joiner.hyphen@ê-%<          CNJ_TENSE ;
@P.joiner.NULL@PV/e+:@P.joiner.NULL@ê^EGLOT%<         CNJ_TENSE ;
@P.joiner.hyphen@PV/ka+:@P.joiner.hyphen@ka-%<        CNJ_TENSE ;
@P.joiner.NULL@PV/ka+:@P.joiner.NULL@ka^EGLOT%<       CNJ_TENSE ;
@P.joiner.hyphen@PV/kaa+:@P.joiner.hyphen@kâ-%<       CNJ_TENSE ;
@P.joiner.NULL@PV/kaa+:@P.joiner.NULL@kâ^EGLOT%<      CNJ_TENSE ;

0 CNJ_TENSE ; ! Option to allow for the occasional omission of the grammatical conjunct prefix without a changed conjunct

@P.cnj.CC@IC+:@P.cnj.CC@ CNJ_TENSE ; ! Changed conjunct forms with initial change (IC) and no grammatical conjunct prefix


LEXICON CNJ_TENSE   !!= @CODE@ gives prefixes and flags for tense in conjunct
@U.tense.Prs@                                                                           VERBPREFIXES ;
@U.tense.Prt@@U.joiner.hyphen@@D.cnj.CC@:@U.tense.Prt@@U.joiner.hyphen@@D.cnj.CC@kî-%<        VERBPREFIXES ;
@U.tense.Prt@@U.joiner.NULL@@D.cnj.CC@:@U.tense.Prt@@U.joiner.NULL@@D.cnj.CC@kî^EGLOT%<       VERBPREFIXES ;
@U.tense.FutInt@@U.joiner.hyphen@@D.cnj.CC@:@U.tense.FutInt@@U.joiner.hyphen@@D.cnj.CC@wî-%<  VERBPREFIXES ;
@U.tense.FutInt@@U.joiner.NULL@@D.cnj.CC@:@U.tense.FutInt@@U.joiner.NULL@@D.cnj.CC@wî^EGLOT%< VERBPREFIXES ;


LEXICON IMPERATIVE   !!= @CODE@ gives flag for imperative (no prefixes)
@U.order.imp@  VERBPREFIXES ;

!! Words marked GG are from Plains Cree Grammar guide and glossary


!! Preverbs
!     ==========

LEXICON VERBPREFIXES  !!= @CODE@ just adds the prefix boundary
@U.wici.NULL@ VERBSTEMS ;
@U.wici.wici@ WICI ;
REDUPLICATION ;
PREVERBS ;

LEXICON REDUPLICATION
RdplW+:d1ay2 REDUPL_BOUND ;
RdplS+:d1âh  REDUPL_BOUND ;
@U.joiner.NULL@RdplW+RdplS+:@U.joiner.NULL@d2ay3d1âh      REDUPL_BOUND ;
@U.joiner.hyphen@RdplW+RdplS+:@U.joiner.hyphen@d2ay3-d1âh REDUPL_BOUND ;

LEXICON REDUPL_BOUND
@U.joiner.NULL@@D.cnj.CC@:@U.joiner.NULL@@D.cnj.CC@%<      REDUPL_CONT ;
@U.joiner.hyphen@@D.cnj.CC@:@U.joiner.hyphen@@D.cnj.CC@-%< REDUPL_CONT ;

@U.joiner.NULL@@R.cnj.CC@@C.cnj@:@U.joiner.NULL@@R.cnj.CC@@C.cnj@%^IC%<      REDUPL_CONT ;
@U.joiner.hyphen@@R.cnj.CC@@C.cnj@:@U.joiner.hyphen@@R.cnj.CC@@C.cnj@%^IC-%< REDUPL_CONT ;

! @U.joiner.NULL@@C.cnj@:@U.joiner.NULL@@C.cnj@%<      REDUPL_CONT ;
! @U.joiner.hyphen@@C.cnj@:@U.joiner.hyphen@@C.cnj@-%< REDUPL_CONT ;

LEXICON REDUPL_CONT
@U.wici.NULL@ VERBSTEMS ;
@U.wici.wici@ WICI ;
PREVERBS ;

LEXICON PREVERBS
PV/acici+:acici PREVERBS_BOUND "head down; upside down" ; ! Position
PV/ahcahkowi+:ahcahkowi PREVERBS_BOUND "spiritual" ; ! Spirituality
PV/akawaci+:akâwâci PREVERBS_BOUND "wishfully" ; ! Manner
PV/ako+:ako PREVERBS_BOUND "sticking on; attached" ; 
PV/akoci+:akoci PREVERBS_BOUND "hanging" ; ! Position
PV/apihci+:apihci PREVERBS_BOUND "small" ; ! Size
PV/apisci+:apisci PREVERBS_BOUND "small, little" ; ! Size
PV/apisi+:apisi PREVERBS_BOUND "small, tiny" ; ! Size
PV/apisisi+:apisîsi PREVERBS_BOUND "small" ; ! Size
PV/ase+:asê PREVERBS_BOUND "backwards, back, motion going backwards" ; 
PV/aseya+:asêya PREVERBS_BOUND "in a group" ; 
PV/ati+:ati PREVERBS_BOUND "begin to, beginning; progressively, proceed to, going on, progressing; on the way" ; 
PV/atimi+:atimi PREVERBS_BOUND "going away from speaker, towards addressee" ; 
PV/ayamihawi+:ayamihâwi PREVERBS_BOUND "prayer, pertaining to Christian prayer; of the Christian religion, of church" ; 
PV/ayamihewi+:ayamihêwi PREVERBS_BOUND "of the Christian religion, of church" ; 
PV/ayaso+:ayâsô PREVERBS_BOUND "a little here and there" ; 
PV/ayi+:ayi PREVERBS_BOUND "ah, hm" ; 
PV/ahci+:âhci PREVERBS_BOUND "to a different place" ; 
PV/ahkami+:âhkami PREVERBS_BOUND "persistently, untiringly, unfailingly, unceasingly, unwaveringly; keep on, persevere" ; 
PV/ako+:âkô PREVERBS_BOUND "covered, shielded" ; 
PV/akwa+:âkwâ PREVERBS_BOUND "quite a lot; well on its way, in progress of time" ; 
PV/ami+:âmi PREVERBS_BOUND "almost" ; 
PV/aniski+:âniski PREVERBS_BOUND "succession, extension" ; 
PV/apihci+:âpihci PREVERBS_BOUND "halfway" ; 
PV/apihtawi+:âpihtawi PREVERBS_BOUND "half" ; 
PV/apihta+:âpihtâ PREVERBS_BOUND "half" ; 
@U.joiner.NULL@PV/apihta_kisikawi+:@U.joiner.NULL@âpihtâkîsikâwi PREVERBS_BOUND "at noon" ; 
@U.joiner.hyphen@PV/apihta_kisikawi+:@U.joiner.hyphen@âpihtâ-kîsikâwi PREVERBS_BOUND "at noon" ; 
PV/asawi+:âsawi PREVERBS_BOUND "in passing something on" ; 
PV/ase+:âsê PREVERBS_BOUND "see: asê-" ; 
PV/asowi+:âsowi PREVERBS_BOUND "in turn, in succession" ; 
PV/aste+:âstê PREVERBS_BOUND "ceasing; quit" ; 
PV/ata+:âta PREVERBS_BOUND "although, in VAIn" ; 
PV/ayici+:âyîci PREVERBS_BOUND "firmly, tightly" ; 
PV/cipehtako+:cîpêhtako PREVERBS_BOUND "blue, greyish blue, grey" ; 
! PV/e+:ê PREVERBS_BOUND "[grammatical preverb: complementizer; defines a changed conjunct clause]" ; 
PV/isi+:isi PREVERBS_BOUND "so, this way, thus, thither, such" ; 
PV/isko+:isko PREVERBS_BOUND "so far, to such an extent" ; 
PV/ispi+:ispi PREVERBS_BOUND "then, when" ; 
PV/ispihci+:ispîhci PREVERBS_BOUND "meanwhile, beforehand" ; 
PV/itahto+:itahto PREVERBS_BOUND "in such number" ; 
PV/ikate+:îkatê PREVERBS_BOUND "aside, off to one side" ; 
! PV/ka+:ka PREVERBS_BOUND "grammatical preverb: future will, shall; infinitive; optative ought, should" ; 
! PV/ka_ki+:ka-kî PREVERBS_BOUND "can, be able to; may; should, ought to" ; 
@U.joiner.hyphen@PV/ka_ki+:@U.joiner.hyphen@ka-kî PREVERBS_BOUND ;
@U.joiner.NULL@PV/ka_ki+:@U.joiner.NULL@kakî PREVERBS_BOUND ;
PV/kakepaci+:kakêpâci PREVERBS_BOUND "stupidly" ; 
PV/kakwahyaki+:kakwâhyaki PREVERBS_BOUND "greatly, extremely, overwhelmingly; with startled surprise" ; 
PV/kakwayaki+:kakwâyaki PREVERBS_BOUND "greatly, extremely" ; 
PV/kakwe+:kakwê PREVERBS_BOUND "try to, attempt to" ; 
PV/kakweci+:kakwêci PREVERBS_BOUND "inquire" ; 
PV/kanaci+:kanâci PREVERBS_BOUND "clean" ; 
PV/kape+:kapê PREVERBS_BOUND "for the full duration of; always" ; 
PV/kaski+:kaski PREVERBS_BOUND "dense" ; 
PV/kaskitewi+:kaskitêwi PREVERBS_BOUND "black" ; 
PV/kayasi+:kayâsi PREVERBS_BOUND "old" ; 
! PV/kaa+:kâ PREVERBS_BOUND "[grammatical preverb; defines a changed conjunct clause; often a relative clause, i.e. who, which]" ; 
PV/kah+:kâh PREVERBS_BOUND "would, ought to; likely to" ; 
@U.joiner.hyphen@PV/kah-kape+:@U.joiner.hyphen@kâh-kapê PREVERBS_BOUND "all the time" ; 
@U.joiner.NULL@PV/kah-kape+:@U.joiner.NULL@kâhkapê PREVERBS_BOUND "all the time" ; 
PV/kamwaci+:kâmwâci PREVERBS_BOUND "quietly" ; 
PV/kaspi+:kâspi PREVERBS_BOUND "crunching" ; 
PV/kawi+:kâwi PREVERBS_BOUND "again; back, in return" ; 
PV/ke+:kê PREVERBS_BOUND "grammatical preverb; future, conditional: shall; defines a changed conjunct clause" ; 
PV/kekaci+:kêkâci PREVERBS_BOUND "almost" ; 
PV/ket+:kêt PREVERBS_BOUND "take off, remove" ; 
PV/ketiski+:kêtiski PREVERBS_BOUND "to exact measure" ; 
PV/kici+:kici PREVERBS_BOUND "so that" ; 
PV/kihci+:kihci PREVERBS_BOUND "great, big, important" ; 
PV/kika+:kika PREVERBS_BOUND "grammatical preverb; future; defines a conjunct clause" ; 
PV/kipi+:kipi PREVERBS_BOUND "blocking" ; 
PV/kisci+:kisci PREVERBS_BOUND "big" ; 
PV/kise+:kisê PREVERBS_BOUND "great, gentle" ; 
PV/kisipi+:kisipi PREVERBS_BOUND "end, finish, ending" ; 
PV/kisiska+:kisiskâ PREVERBS_BOUND "fast, quickly" ; 
PV/kisiwi+:kisiwi PREVERBS_BOUND "anger" ; 
PV/kisiwi+:kisîwi PREVERBS_BOUND "angrily, in anger" ; 
PV/kita+:kita PREVERBS_BOUND "[grammatical preverb]; future: will, shall; infinitive; optative: ought, should" ; 
@U.joiner.hyphen@PV/kita_ki+:@U.joiner.hyphen@kita-kî PREVERBS_BOUND "can, be able to; may; ought to" ; 
@U.joiner.NULL@PV/kita_ki+:@U.joiner.NULL@kitakî PREVERBS_BOUND "can, be able to; may; ought to" ; 
PV/kitimaki+:kitimâki PREVERBS_BOUND "pitiable, pitiably" ; 
PV/kiye+:kiyê PREVERBS_BOUND "[grammatical preverb; future; in changed conjunct clauses]" ; 
PV/kiyi+:kiyi PREVERBS_BOUND "[grammatical preverb; future; defines a conjunct clause]" ; 
! PV/ki+:kî PREVERBS_BOUND "[grammatical preverb; past, completion]" ; 
PV/kimoci+:kîmôci PREVERBS_BOUND "secretly, by stealth" ; 
PV/kipi+:kîpi PREVERBS_BOUND "quickly" ; 
PV/kisi+:kîsi PREVERBS_BOUND "finish; completely, to completion; completing, having done" ; 
PV/kiski+:kîski PREVERBS_BOUND "severing, breaking, cutting off" ; 
PV/kisopwe+:kîsopwê PREVERBS_BOUND "with hot weather" ; 
PV/kiwaci+:kîwâci PREVERBS_BOUND "orphaned, stray; lonely, desolately" ; 
PV/kiwe+:kîwê PREVERBS_BOUND "back, homeward" ; 
PV/koci+:koci PREVERBS_BOUND "try to" ; 
PV/kota+:kôta PREVERBS_BOUND "long for, be deprived of" ; 
PV/kotate+:kôtatê PREVERBS_BOUND "at a loss, making no attempt" ; 
PV/kwataki+:kwataki PREVERBS_BOUND "distressing, tormenting, tortuous" ; 
PV/kwayako+:kwayako PREVERBS_BOUND "taking out, directly" ; 
PV/kwayaski+:kwayaski PREVERBS_BOUND "properly" ; 
PV/kwayasko+:kwayasko PREVERBS_BOUND "straight; properly, correctly" ; 
PV/kwayaci+:kwayâci PREVERBS_BOUND "in readiness, already; in preparation; beforehand" ; 
PV/kwahci+:kwâhci PREVERBS_BOUND "far off" ; 
PV/kwaskwe+:kwâskwê PREVERBS_BOUND "upwards" ; 
PV/kwataki+:kwâtaki PREVERBS_BOUND "distressing, tormenting, tortuous" ; 
PV/kweski+:kwêski PREVERBS_BOUND "turning" ; 
PV/kwetawi+:kwêtawi PREVERBS_BOUND "doubtful" ; 
PV/kwitawi+:kwîtawi PREVERBS_BOUND "impatiently" ; 
PV/maci+:maci PREVERBS_BOUND "bad, evil, wicked, wrong, ill" ; 
@U.joiner.hyphen@PV/maci_manitowi+:@U.joiner.hyphen@maci-manitowi PREVERBS_BOUND "evil, devilish; Devil's" ; 
@U.joiner.NULL@PV/maci_manitowi+:@U.joiner.NULL@macimanitowi PREVERBS_BOUND "evil, devilish; Devil's" ; 
PV/mahti+:mahti PREVERBS_BOUND "Let us; hortatory" ; ! From NS 152 
PV/mana+:manâ PREVERBS_BOUND "beware of, careful not to, avoid doing" ; 
PV/mani+:mani PREVERBS_BOUND "with the intent of, with the purpose of" ; 
PV/masta+:mastâ PREVERBS_BOUND "later" ; 
PV/matwe+:matwê PREVERBS_BOUND "audibly, visibly; perceptibly; loudly; heard or seen from a distance" ; 
PV/maaci+:mâci PREVERBS_BOUND "begin to, start to; commencement; initially" ; 
PV/mahi+:mâhi PREVERBS_BOUND "downstream" ; 
PV/mamawi+:mâmawi PREVERBS_BOUND "together; in full number of them all, altogether, all together as a group" ; 
PV/maninakisko+:mâninâkisko PREVERBS_BOUND "without hesitation, entirely" ; 
PV/maski+:mâski PREVERBS_BOUND "crippled; defective, deformed" ; 
PV/mayaci+:mâyâci PREVERBS_BOUND "bad" ; 
PV/mayi+:mâyi PREVERBS_BOUND "bad, evil; badly, evilly, wickedly" ; 
PV/mecimwaci+:mêcimwâci PREVERBS_BOUND "permanently, for good" ; 
PV/mêkwa+:mêkwâ PREVERBS_BOUND "currently, presently, right now; while, during, in the meantime, in the act or place" ; 
PV/mesci+:mêsci PREVERBS_BOUND "exhaustively, till all is gone" ; 
PV/mesti+:mêsti PREVERBS_BOUND "it is used up, it is burnt out, it is spent" ; 
PV/mihko+:mihko PREVERBS_BOUND "red" ; 
PV/mihyawe+:mihyawê PREVERBS_BOUND "with fur or body hair" ; 
PV/misakame+:misakâmê PREVERBS_BOUND "all the way" ; 
PV/misi+:misi PREVERBS_BOUND "big, greatly; much, a lot; very, extremely, to the extreme" ; 
PV/mistiko+:mistiko PREVERBS_BOUND "wood, of wood, wooden" ; 
PV/miyo+:miyo PREVERBS_BOUND "good, well, beautifully" ; 
PV/mosci+:mosci PREVERBS_BOUND "just, merely, plainly, simply, directly, without mediation, without instrumentality; by hand" ; 
PV/mohco+:môhco PREVERBS_BOUND "silly, crazy" ; 
PV/mwayi+:mwayî PREVERBS_BOUND "before, prior to" ; 
PV/nahi+:nahi PREVERBS_BOUND "well, fair" ; 
PV/nahta+:nahtâ PREVERBS_BOUND "able; good at, competent, practised, experienced, skillful at, expert at, known as one who does s.t. habitually; well" ; 
PV/naki+:naki PREVERBS_BOUND "stopping" ; 
PV/napaki+:napaki PREVERBS_BOUND "flat" ; 
PV/naspaci+:naspâci PREVERBS_BOUND "opposite, contrarily" ; 
PV/natawi+:natawi PREVERBS_BOUND "go to" ; 
PV/nawaci+:nawaci PREVERBS_BOUND "better" ; 
PV/naci+:nâci PREVERBS_BOUND "getting" ; 
PV/nanitaw+:nânitaw PREVERBS_BOUND "[in negative clauses:] any" ; 
PV/natakasi+:nâtakâsi PREVERBS_BOUND "into the bush country" ; 
PV/nawayawi+:nâwayawi PREVERBS_BOUND "one behind the other, in a row" ; 
PV/nehiyawi+:nêhiyawi PREVERBS_BOUND "Cree, in Cree" ; 
PV/nihta+:nihtâ PREVERBS_BOUND "able; good at, competent, practised, experienced, skillful at, expert at, known as one who does s.t. habitually; well" ; 
PV/nipahi+:nipahi PREVERBS_BOUND "really, very, extremely" ; 
PV/nisihkaci+:nisîhkâci PREVERBS_BOUND "gently, carefully, slowly" ; 
PV/nitawi+:nitawi PREVERBS_BOUND "go and, go to; engaged in" ; 
PV/nihci+:nîhci PREVERBS_BOUND "down, downwards; bottom" ; 
PV/nihtaciwe+:nîhtaciwê PREVERBS_BOUND "downhill, down" ; 
PV/nikani+:nîkâni PREVERBS_BOUND "in front, ahead, at the head, in the future" ; 
PV/nimi+:nîmi PREVERBS_BOUND "take along" ; 
PV/nipa+:nîpâ PREVERBS_BOUND "at night, in the dark" ; 
PV/niso+:nîso PREVERBS_BOUND "two" ; 
PV/nohte+:nôhtê PREVERBS_BOUND "want to, desire to; lack" ; 
PV/ocihci+:ocihci PREVERBS_BOUND "back in time; far back; from before, earlier; having lived long enough for something; chance to, live to, live to see" ; 
PV/ohci+:ohci PREVERBS_BOUND "from there; with, by means of; because, for that reason, therefore, thence; [grammatical preverb; past preverb in negative clauses]" ; 
PV/ohtiskawi+:ohtiskawi PREVERBS_BOUND "facing" ; 
PV/osami+:osâmi PREVERBS_BOUND "too much, because of excess, for" ; 
PV/osawaski+:osâwaski PREVERBS_BOUND "green, blue-green" ; 
PV/osawi+:osâwi PREVERBS_BOUND "yellow, brown" ; 
PV/oski+:oski PREVERBS_BOUND "new, young, first" ; 
PV/otami+:otami PREVERBS_BOUND "busy, occupied, delayed" ; 
PV/o+:ô PREVERBS_BOUND "from there, for that reason; with, by means of; [grammatical preverb]" ; 
PV/oh+:ôh PREVERBS_BOUND "from there, for that reason; with, by means of; [past preverb in negative clauses]" ; 
PV/paci+:paci PREVERBS_BOUND "wrongly, in error" ; 
PV/pahki+:pahki PREVERBS_BOUND "portion of" ; 
PV/pakwano+:pakwâno PREVERBS_BOUND "from memory" ; 
PV/papa+:papâ PREVERBS_BOUND "go around, about, all over" ; 
PV/papami+:papâmi PREVERBS_BOUND "around, about, all over, here and there, hither and thither" ; 
PV/papasi+:papâsi PREVERBS_BOUND "fast, in a hurry, quick" ; 
PV/pasiko+:pasikô PREVERBS_BOUND "get up (from sitting)" ; 
PV/paspi+:paspi PREVERBS_BOUND "through narrow opening" ; 
PV/pahkaci+:pâhkaci PREVERBS_BOUND "fixed, not moving; locked in to a previously stated obligation" ; 
PV/pahko+:pâhko PREVERBS_BOUND "dry" ; 
PV/pasci+:pâsci PREVERBS_BOUND "over, beyond" ; 
PV/pasici+:pâsici PREVERBS_BOUND "stepping over, skipping" ; 
PV/pe+:pê PREVERBS_BOUND "come and; towards, approaching; hither; thence, from there on down; [towards focus]" ; 
PV/peyako+:pêyako PREVERBS_BOUND "alone, only; as one" ; 
PV/pimi+:pimi PREVERBS_BOUND "along, in linear progression; while moving in linear progression" ; 
PV/pimici+:pimici PREVERBS_BOUND "crosswise, from side, at right angle" ; 
PV/pimipahci+:pimipahci PREVERBS_BOUND "running, on the run" ; 
PV/pisci+:pisci PREVERBS_BOUND "accidentally, mistakenly, erroneously" ; 
PV/pihci+:pîhci PREVERBS_BOUND "inside, in, within" ; 
PV/pihtawe+:pîhtawê PREVERBS_BOUND "under; double" ; 
PV/pihtoke+:pîhtokê PREVERBS_BOUND "entering to the inside, entering house" ; 
PV/pikiskaci+:pîkiskâci PREVERBS_BOUND "sorry, sad, lonely" ; 
PV/pimi+:pîmi PREVERBS_BOUND "twisted" ; 
PV/pitosi+:pîtosi PREVERBS_BOUND "different, foreign" ; 
PV/piwi+:pîwi PREVERBS_BOUND "small" ; 
PV/poni+:pôni PREVERBS_BOUND "stop, terminate, cease; after" ; 
PV/posko+:pôsko PREVERBS_BOUND "the same" ; 
PV/pwana+:pwâna PREVERBS_BOUND "unable, fail to" ; 
PV/pwatawi+:pwâtawi PREVERBS_BOUND "unable to do, failing" ; 
PV/saskaci+:saskaci PREVERBS_BOUND "weary, tired of" ; 
PV/sakewe+:sâkêwê PREVERBS_BOUND "behind obstacle to vision, into view" ; 
PV/sapo+:sâpo PREVERBS_BOUND "fully, exhaustively, through, through and through, right through, from end to end; beyond" ; 
PV/saposci+:sâposci PREVERBS_BOUND "through" ; 
PV/sasakici+:sâsakici PREVERBS_BOUND "backwards; supine" ; 
PV/sehke+:sêhkê PREVERBS_BOUND "by itself, of own accord; move ahead of own accord; willingly" ; 
PV/sesawi+:sêsâwi PREVERBS_BOUND "stretching, exercising" ; 
PV/sikaci+:sikaci PREVERBS_BOUND "impatiently, bored with" ; 
PV/simaci+:simaci PREVERBS_BOUND "upright; straightened" ; 
PV/sipwe+:sipwê PREVERBS_BOUND "leaving, departing, going away; starting out, commencement" ; 
PV/sisikoci+:sisikoci PREVERBS_BOUND "suddenly" ; 
PV/sisiwe+:sisiwê PREVERBS_BOUND "spread, scatter" ; 
PV/sikawi+:sîkawi PREVERBS_BOUND "pouring, flooding" ; 
PV/sohkahat+:sôhkahât PREVERBS_BOUND "greatly, powerfully" ; 
PV/sohki+:sôhki PREVERBS_BOUND "hard, strongly, intensively, vigourously, properly" ; 
PV/soniyawi+:sôniyâwi PREVERBS_BOUND "with respect to money, in financial matters" ; 
PV/ta+:ta PREVERBS_BOUND "[grammatical preverb: future; infinitive]" ; 
@U.joiner.hyphen@PV/ta_ki+:@U.joiner.hyphen@ta-kî PREVERBS_BOUND "can, be able to; should, ought to" ; 
@U.joiner.NULL@PV/ta_ki+:@U.joiner.NULL@takî PREVERBS_BOUND "can, be able to; should, ought to" ; 
PV/tahci+:tahci PREVERBS_BOUND "coming apart, undone" ; 
PV/tahki+:tahki PREVERBS_BOUND "repeated action, always, constantly ;; receiving a chill, cold" ; 
PV/tahkohci+:tahkohci PREVERBS_BOUND "on top" ; 
PV/tahto+:tahto PREVERBS_BOUND "as many as" ; 
PV/takahki+:takahki PREVERBS_BOUND "good, nice" ; 
PV/tako+:tako PREVERBS_BOUND "in addition to, on top, over and above; with, having along, also" ; 
PV/tasi+:tasi PREVERBS_BOUND "for such a time, for the duration; an action at a time, while" ; 
PV/tah+:tâh PREVERBS_BOUND "would, ought to; likely to" ; 
PV/tahci+:tâhci PREVERBS_BOUND "on the top" ; 
PV/tapi+:tâpi PREVERBS_BOUND "through the same place" ; 
PV/tapisi+:tâpisi PREVERBS_BOUND "threading, inserting" ; 
PV/tehci+:têhci PREVERBS_BOUND "on, on top of something" ; 
PV/tepi+:têpi PREVERBS_BOUND "sufficiently; enough; reaching" ; 
PV/tewi+:têwi PREVERBS_BOUND "pain" ; 
PV/teyi+:têyi PREVERBS_BOUND "pain" ; 
PV/tipi+:tipi PREVERBS_BOUND "equal" ; 
PV/tita+:tita PREVERBS_BOUND "[grammatical preverb; future]" ; 
PV/tomi+:tômi PREVERBS_BOUND "oily, greasy" ; 
PV/wahke+:wahkê PREVERBS_BOUND "addicted to, easily led to" ; 
PV/wani+:wani PREVERBS_BOUND "dim, dark, indistinctly, blurred; lose; destroy" ; 
PV/wayawi+:wayawî PREVERBS_BOUND "outside; out from inside, out from the tipi" ; 
PV/wayesi+:wayêsi PREVERBS_BOUND "deceitfully" ; 
PV/wa+:wâ PREVERBS_BOUND "intend to, be about to" ; 
PV/wah+:wâh PREVERBS_BOUND "whenever in future" ; 
PV/waki+:wâki PREVERBS_BOUND "curving" ; 
PV/wapani+:wâpani PREVERBS_BOUND "dawn; at, until dawn" ; 
PV/wapohki+:wâpôhki PREVERBS_BOUND "white" ; 
PV/wasa+:wâsâ PREVERBS_BOUND "around" ; 
PV/wase+:wâsê PREVERBS_BOUND "clear; bright" ; 
PV/wawiye+:wâwiyê PREVERBS_BOUND "round, spherical" ; 
PV/wehci+:wêhci PREVERBS_BOUND "easily" ; 
PV/wiyipi+:wiyîpi PREVERBS_BOUND "dirty looking; dull brown, drab" ; 
PV/wihpi+:wîhpi PREVERBS_BOUND "hollowing out" ; 
PV/wini+:wîni PREVERBS_BOUND "soil, dirty" ; 
PV/wipi+:wîpi PREVERBS_BOUND "dirty, unclean" ; 
PV/wipo+:wîpo PREVERBS_BOUND "narrow" ; 
PV/wisaki+:wîsaki PREVERBS_BOUND "painfully" ; 
PV/wito+:wîto PREVERBS_BOUND "want, intend" ; 
PV/yahki+:yahki PREVERBS_BOUND "act of pushing forward; increasing, increasingly, adding, more and more" ; 
PV/yikate+:yîkatê PREVERBS_BOUND "aside" ; 
PV/yipe+:yîpê PREVERBS_BOUND "on a slant, on a slope, on the side, sideways" ; 
PV/yoski+:yôski PREVERBS_BOUND "soft, softly" ; 

LEXICON PREVERBS_BOUND
@U.joiner.NULL@@D.cnj.CC@:@U.joiner.NULL@@D.cnj.CC@%^EGLOT%< VERBPREFIXES ;
@U.joiner.hyphen@@D.cnj.CC@:@U.joiner.hyphen@@D.cnj.CC@-%<   VERBPREFIXES ;

@U.joiner.NULL@@R.cnj.CC@@C.cnj@:@U.joiner.NULL@@R.cnj.CC@@C.cnj@%^IC%^EGLOT%< VERBPREFIXES ;
@U.joiner.hyphen@@R.cnj.CC@@C.cnj@:@U.joiner.hyphen@@R.cnj.CC@@C.cnj@%^IC-%<   VERBPREFIXES ;

! @U.joiner.NULL@@C.cnj@:@U.joiner.NULL@@C.cnj@%^EGLOT%< VERBPREFIXES ;
! @U.joiner.hyphen@@C.cnj@:@U.joiner.hyphen@@C.cnj@-%<   VERBPREFIXES ;


LEXICON WICI
@U.joiner.NULL@@D.cnj.CC@:@U.joiner.NULL@@D.cnj.CC@wîci%^EGLOT%<      VERBSTEMS ; !!!! We need to deal with the wît- allomorph
@U.joiner.hyphen@@D.cnj.CC@:@U.joiner.hyphen@@D.cnj.CC@wîci-%<        VERBSTEMS ; !!!! We need to deal with the wît- allomorph

@U.joiner.NULL@@R.cnj.CC@@C.cnj@:@U.joiner.NULL@@R.cnj.CC@@C.cnj@wîci%^IC%^EGLOT%<      VERBSTEMS ; !!!! We need to deal with the wît- allomorph
@U.joiner.hyphen@@R.cnj.CC@@C.cnj@:@U.joiner.hyphen@@R.cnj.CC@@C.cnj@wîci%^IC-%<        VERBSTEMS ; !!!! We need to deal with the wît- allomorph

!! Now, lexc directs us to the ../stems/verbs_stems.lexc file,
!! where we find all the verbal stems. The suffixes are then
!! found in the section "Suffixes" right underneath.

! ===============================================================
! ===============================================================
! ===============================================================
! ===============================================================
! ===============================================================

!! # Suffixes

!! fst:
!! AICONJ:  stem
!! AICONJw: stem (excl w)
! 
!! Wolv:
!! AICONJ_n
!! AICONJ_w

! Auxiliary lexica for the verb dump

! LEXICON AICONJ_n 
! VAIn ;
! LEXICON AICONJ_v 
! VAIw ;


!! LEXICON TACONJ_3 
!! TACONJ_2 ;
!! 
!! LEXICON TACONJ_4 
!! TACONJ_2 ;

! LEXICON TICONJi  ! Trond dymmy delete
! # ;


!! Intransitive inanimate (II)
!      ===========================
!      ===========================

!!! AEW == Ours: VIIv == VIIw, VIIn == VIIn

!!! STEM CLASS

LEXICON VIIn !!= @CODE@  
+V+II:  VIIn_SGPL_WICI ;

LEXICON VIIn_SG !!= @CODE@  
+V+II:  VIIn_SG_WICI ;

LEXICON VIIw_PL !!= @CODE@  
+V+II:  VIIw_PL_WICI ;

!! LEXICON VIIw_PL !!= @CODE@  NO LONGER NEEDED FROM AROK
!! +V+II: VIIw_PL_WICI ;	   NO LONGER NEEDED FROM AROK

LEXICON VIIw !!= @CODE@  
+V+II:  VIIw_SGPL_WICI ;

LEXICON VIIw_SG !!= @CODE@  
+V+II:  VIIw_SG_WICI ;

LEXICON VIIn_PL !!= @CODE@  NO LONGER NEEDED FROM AROK
+V+II: VIIn_PL_WICI ;	   !! NO LONGER NEEDED FROM AROK

!!! WICI 
!!!NOTE: II do not take this morpheme, so all forms should be null flagged.

! ===========================================================
! ===========================================================
!!!!! -w final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VIIw_SGPL_WICI !!!! 
@U.wici.NULL@ VIIw_SGPL_ORDER ;

LEXICON VIIw_SG_WICI !!!! 
@U.wici.NULL@ VIIw_SG_ORDER ;

LEXICON VIIw_PL_WICI !!!! 
@U.wici.NULL@ VIIw_PL_ORDER ;

!! 	 NO LONGER NEEDED FROM AROK
!! @U.wici.NULL@ VIIw_PL_ORDER ; NO LONGER NEEDED FROM AROK

! ===========================================================
! ===========================================================
!!!!! -n final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VIIn_SGPL_WICI !!!! 
@U.wici.NULL@ VIIn_SGPL_ORDER ;

LEXICON VIIn_SG_WICI !!!! 
@U.wici.NULL@ VIIn_SG_ORDER ;

!! 
!! @U.wici.NULL@ VIIw_PL_ORDER ;

LEXICON VIIn_PL_WICI !!!! 	 NO LONGER NEEDED FROM AROK
@U.wici.NULL@ VIIn_PL_ORDER ; !!!! NO LONGER NEEDED FROM AROK

!!! ORDERS

! ===========================================================
! ===========================================================
!!!!! -w final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VIIw_SGPL_ORDER !!= @CODE@ 
@U.order.indep@+Ind:@U.order.indep@ VIIw_SGPL_IND_TENSE ;
@U.order.cnj@+Cnj:@U.order.cnj@ VIIw_SGPL_CNJ_TENSE ;
@U.order.FutCon@+Fut+Cond:@U.order.FutCon@ VIIw_SGPL_FUT_CON_PERSON ;

! @U.order.indep@+Ind:@U.order.indep@ VIIw_IND_TENSE ; !
! @U.order.cnj@+Cnj:@U.order.cnj@ VIIw_CNJ_TENSE ; !
! @U.order.FutCon@+Fut+Cond:@U.order.FutCon@ VIIw_FUT_CON_PERSON ;!

LEXICON VIIw_SG_ORDER !!= @CODE@ singular only
@U.order.indep@+Ind:@U.order.indep@  VIIw_SG_IND_TENSE ; !
@U.order.cnj@+Cnj:@U.order.cnj@ VIIw_SG_CNJ_TENSE ; !
@U.order.FutCon@+Fut+Cond:@U.order.FutCon@ VIIw_SG_FUT_CON_PERSON ;!

LEXICON VIIw_PL_ORDER !!= @CODE@ singular only
@U.order.indep@+Ind:@U.order.indep@  VIIw_PL_IND_TENSE ; !
@U.order.cnj@+Cnj:@U.order.cnj@ VIIw_PL_CNJ_TENSE ; !
@U.order.FutCon@+Fut+Cond:@U.order.FutCon@ VIIw_PL_FUT_CON_PERSON ;!

!! LEXICON VIIw_PL_ORDER !!= @CODE@ plural only 
!! @U.order.indep@+Ind:@U.order.indep@ VIIw_PL_IND_TENSE ; !
!! @U.order.cnj@+Cnj:@U.order.cnj@ VIIw_PL_CNJ_TENSE ; !
!! @U.order.FutCon@+Fut+Cond:@U.order.FutCon@ VIIw_PL_FUT_CON_PERSON ;!

! ===========================================================
! ===========================================================
!!!!! -n final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VIIn_SGPL_ORDER !!= @CODE@ 
@U.order.indep@+Ind:@U.order.indep@ VIIn_SGPL_IND_TENSE ; !
@U.order.cnj@+Cnj:@U.order.cnj@ VIIn_SGPL_CNJ_TENSE ; !
@U.order.FutCon@+Fut+Cond:@U.order.FutCon@ VIIn_SGPL_FUT_CON_PERSON ;!

LEXICON VIIn_SG_ORDER !!= @CODE@ singular only
@U.order.indep@+Ind:@U.order.indep@ VIIn_SG_IND_TENSE ; !
@U.order.cnj@+Cnj:@U.order.cnj@ VIIn_SG_CNJ_TENSE ; !
@U.order.FutCon@+Fut+Cond:@U.order.FutCon@ VIIn_SG_FUT_CON_PERSON ;!

LEXICON VIIn_PL_ORDER !!= @CODE@ plural only
@U.order.indep@+Ind:@U.order.indep@ VIIn_PL_IND_TENSE ; !
@U.order.cnj@+Cnj:@U.order.cnj@ VIIn_PL_CNJ_TENSE ; !
@U.order.FutCon@+Fut+Cond:@U.order.FutCon@ VIIn_PL_FUT_CON_PERSON ;!

!!! TENSES

! ===========================================================
! ===========================================================
!!!!! -w final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VIIw_SGPL_IND_TENSE
@U.tense.Prs@+Prs:@U.tense.Prs@ VIIw_SGPL_IND_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VIIw_SGPL_IND_PERSON ; !
@U.tense.FutDef@+Fut+Def:@U.tense.FutDef@ VIIw_SGPL_IND_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VIIw_SGPL_IND_PERSON ; !

LEXICON VIIw_SGPL_CNJ_TENSE
@U.tense.Prs@+Prs:@U.tense.Prs@ VIIw_SGPL_CNJ_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VIIw_SGPL_CNJ_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VIIw_SGPL_CNJ_PERSON ; !

LEXICON VIIw_SG_IND_TENSE !!= @CODE@ plural only
@U.tense.Prs@+Prs:@U.tense.Prs@ VIIw_SG_IND_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VIIw_SG_IND_PERSON ; !
@U.tense.FutDef@+Fut+Def:@U.tense.FutDef@ VIIw_SG_IND_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VIIw_SG_IND_PERSON ; !

LEXICON VIIw_SG_CNJ_TENSE !!= @CODE@ plural only
@U.tense.Prs@+Prs:@U.tense.Prs@ VIIw_SG_CNJ_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VIIw_SG_CNJ_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VIIw_SG_CNJ_PERSON ; !

LEXICON VIIw_PL_IND_TENSE !!= @CODE@ plural only
@U.tense.Prs@+Prs:@U.tense.Prs@ VIIw_PL_IND_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VIIw_PL_IND_PERSON ; !
@U.tense.FutDef@+Fut+Def:@U.tense.FutDef@ VIIw_PL_IND_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VIIw_PL_IND_PERSON ; !

LEXICON VIIw_PL_CNJ_TENSE !!= @CODE@ plural only
@U.tense.Prs@+Prs:@U.tense.Prs@ VIIw_PL_CNJ_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VIIw_PL_CNJ_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VIIw_PL_CNJ_PERSON ; !

!! LEXICON VIIw_PL_IND_TENSE !!= @CODE@ plural only
!! @U.tense.Prs@+Prs:@U.tense.Prs@ VIIw_PL_IND_PERSON ; !
!! @U.tense.Prt@+Prt:@U.tense.Prt@ VIIw_PL_IND_PERSON ; !
!! @U.tense.FutDef@+Fut+Def:@U.tense.FutDef@ VIIw_PL_IND_PERSON ; !
!! @U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VIIw_PL_IND_PERSON ; !
!! 
!! LEXICON VIIw_PL_CNJ_TENSE !!= @CODE@ plural only
!! @U.tense.Prs@+Prs:@U.tense.Prs@ VIIw_PL_CNJ_PERSON ; !
!! @U.tense.Prt@+Prt:@U.tense.Prt@ VIIw_PL_CNJ_PERSON ; !
!! @U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VIIw_PL_CNJ_PERSON ; !

! ===========================================================
! ===========================================================
!!!!! -n final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VIIn_SGPL_IND_TENSE
@U.tense.Prs@+Prs:@U.tense.Prs@ VIIn_SGPL_IND_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VIIn_SGPL_IND_PERSON ; !
@U.tense.FutDef@+Fut+Def:@U.tense.FutDef@ VIIn_SGPL_IND_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VIIn_SGPL_IND_PERSON ; !

LEXICON VIIn_SGPL_CNJ_TENSE
@U.tense.Prs@+Prs:@U.tense.Prs@ VIIn_SGPL_CNJ_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VIIn_SGPL_CNJ_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VIIn_SGPL_CNJ_PERSON ; !

LEXICON VIIn_SG_IND_TENSE !!= @CODE@ plural only
@U.tense.Prs@+Prs:@U.tense.Prs@ VIIn_SG_IND_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VIIn_SG_IND_PERSON ; !
@U.tense.FutDef@+Fut+Def:@U.tense.FutDef@ VIIn_SG_IND_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VIIn_SG_IND_PERSON ; !

LEXICON VIIn_SG_CNJ_TENSE !!= @CODE@ plural only
@U.tense.Prs@+Prs:@U.tense.Prs@ VIIn_SG_CNJ_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VIIn_SG_CNJ_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VIIn_SG_CNJ_PERSON ; !

LEXICON VIIn_PL_IND_TENSE !!= @CODE@ plural only
@U.tense.Prs@+Prs:@U.tense.Prs@ VIIn_PL_IND_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VIIn_PL_IND_PERSON ; !
@U.tense.FutDef@+Fut+Def:@U.tense.FutDef@ VIIn_PL_IND_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VIIn_PL_IND_PERSON ; !

LEXICON VIIn_PL_CNJ_TENSE !!= @CODE@ plural only
@U.tense.Prs@+Prs:@U.tense.Prs@ VIIn_PL_CNJ_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VIIn_PL_CNJ_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VIIn_PL_CNJ_PERSON ; !

!!! PERSON

! ===========================================================
! ===========================================================
!!!!! -w final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VIIw_SGPL_IND_PERSON !!= @CODE@ 
@U.person.NULL@ VIIw_SGPL_IND_NULL ;

LEXICON VIIw_SGPL_CNJ_PERSON !!= @CODE@ 
@U.person.NULL@ VIIw_SGPL_CNJ_NULL ;

LEXICON VIIw_SGPL_FUT_CON_PERSON !!= @CODE@ 
@U.person.NULL@ VIIw_SGPL_FUT_CON_NULL ;

LEXICON VIIw_SG_IND_PERSON !!= @CODE@ 
@U.person.NULL@ VIIw_SG_IND_SUFFIX ;

LEXICON VIIw_SG_CNJ_PERSON !!= @CODE@ 
@U.person.NULL@ VIIw_SG_CNJ_SUFFIX ;

LEXICON VIIw_SG_FUT_CON_PERSON !!= @CODE@ 
@U.person.NULL@ VIIw_SG_FUT_CON_SUFFIX ;

LEXICON VIIw_PL_IND_PERSON !!= @CODE@ 
@U.person.NULL@ VIIw_PL_IND_SUFFIX ;

LEXICON VIIw_PL_CNJ_PERSON !!= @CODE@ 
@U.person.NULL@ VIIw_PL_CNJ_SUFFIX ;

LEXICON VIIw_PL_FUT_CON_PERSON !!= @CODE@ 
@U.person.NULL@ VIIw_PL_FUT_CON_SUFFIX ;

!! LEXICON VIIw_PL_IND_PERSON !!= @CODE@ plural only
!! @U.person.NULL@ VIIw_IND_PL_SUFFIX ;
!! 
!! LEXICON VIIw_PL_CNJ_PERSON !!= @CODE@ plural only
!! @U.person.NULL@ VIIw_CNJ_PL_SUFFIX ;
!! 
!! LEXICON VIIw_PL_FUT_CON_PERSON !!= @CODE@ plural only
!! @U.person.NULL@ VIIw_FUT_CON_PL_SUFFIX ;

! ===========================================================
! ===========================================================
!!!!! -n final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VIIn_SGPL_IND_PERSON !!= @CODE@ 
@U.person.NULL@ VIIn_SGPL_IND_NULL ;

LEXICON VIIn_SGPL_CNJ_PERSON !!= @CODE@ 
@U.person.NULL@ VIIn_SGPL_CNJ_NULL ;

LEXICON VIIn_SGPL_FUT_CON_PERSON !!= @CODE@ 
@U.person.NULL@ VIIn_SGPL_FUT_CON_NULL ;

LEXICON VIIn_SG_IND_PERSON !!= @CODE@ 
@U.person.NULL@ VIIn_SG_IND_SUFFIX ;

LEXICON VIIn_SG_CNJ_PERSON !!= @CODE@ 
@U.person.NULL@ VIIn_SG_CNJ_SUFFIX ;

LEXICON VIIn_SG_FUT_CON_PERSON !!= @CODE@ 
@U.person.NULL@ VIIn_SG_FUT_CON_SUFFIX ;

LEXICON VIIn_PL_IND_PERSON !!= @CODE@ plural only
@U.person.NULL@ VIIn_PL_IND_SUFFIX ;

LEXICON VIIn_PL_CNJ_PERSON !!= @CODE@ plural only
@U.person.NULL@ VIIn_PL_CNJ_SUFFIX ;

LEXICON VIIn_PL_FUT_CON_PERSON !!= @CODE@ plural only
@U.person.NULL@ VIIn_PL_FUT_CON_SUFFIX ;

!!! SUFFIXES

! ********************
!!!! INDEPENDENT !!!!
! *********************


! ===========================================================
! ===========================================================
!!!!! -n final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VIIn_SGPL_IND_NULL !!= @CODE@
VIIn_SG_IND_SUFFIX ;
VIIn_PL_IND_SUFFIX ;

LEXICON VIIn_SG_IND_SUFFIX   !!= @CODE@ singular
+3Sg:%>0 VERB_ENDLEX ;
+4Sg:%>iyiw VERB_ENDLEX ;

LEXICON VIIn_PL_IND_SUFFIX  !!= @CODE@ plural
+3Pl:%>wa VERB_ENDLEX ;
+4Pl:%>iyiwa VERB_ENDLEX ;

! ===========================================================
! ===========================================================
!!!!! -w final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================


LEXICON VIIw_SGPL_IND_NULL !!= @CODE@
VIIw_SG_IND_SUFFIX ;
VIIw_PL_IND_SUFFIX ;

LEXICON VIIw_SG_IND_SUFFIX   !!= @CODE@ w final singular
+3Sg:%>w VERB_ENDLEX ;
+4Sg:%>yiw VERB_ENDLEX ;

LEXICON VIIw_PL_IND_SUFFIX  !!= @CODE@ w final plural
+3Pl:%>wa VERB_ENDLEX ;
+4Pl:%>yiwa VERB_ENDLEX ;


! ******************
!!!! CONJUNCT !!!!
! ******************


! ===========================================================
! ===========================================================
!!!!! -n final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VIIn_SGPL_CNJ_NULL !!= @CODE@
VIIn_SG_CNJ_SUFFIX ;
VIIn_PL_CNJ_SUFFIX ;

LEXICON VIIn_SG_CNJ_SUFFIX   !!= @CODE@ singular
+3Sg:%>k VERB_ENDLEX ;
+4Sg:%>iyik VERB_ENDLEX ;

LEXICON VIIn_PL_CNJ_SUFFIX  !!= @CODE@ plural
+3Pl:%>ki VERB_ENDLEX ;
+4Pl:%>iyiki VERB_ENDLEX ;

! ===========================================================
! ===========================================================
!!!!! -w final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VIIw_SGPL_CNJ_NULL !!= @CODE@
VIIw_SG_CNJ_SUFFIX ;
VIIw_PL_CNJ_SUFFIX ;

LEXICON VIIw_SG_CNJ_SUFFIX   !!= @CODE@ w final singular
+3Sg:%>k VERB_ENDLEX ;
+4Sg:%>yik VERB_ENDLEX ;

LEXICON VIIw_PL_CNJ_SUFFIX   !!= @CODE@ w final plural
+3Pl:%>ki VERB_ENDLEX ;
+4Pl:%>yiki VERB_ENDLEX ;

! ****************************
!!!! FUTURE CONDITIONAL !!!!
! ****************************


! ===========================================================
! ===========================================================
!!!!! -n final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VIIn_SGPL_FUT_CON_NULL !!= @CODE@
VIIn_SG_FUT_CON_SUFFIX ;
VIIn_PL_FUT_CON_SUFFIX ;

LEXICON VIIn_SG_FUT_CON_SUFFIX   !!= @CODE@ singular
+3Sg:%>ki VERB_ENDLEX ;
+4Sg:%>iyiki VERB_ENDLEX ;

LEXICON VIIn_PL_FUT_CON_SUFFIX  !!= @CODE@ plural
+3Pl:%>kwâwi VERB_ENDLEX ;
+4Pl:%>iyikwâwi VERB_ENDLEX ;

! ===========================================================
! ===========================================================
!!!!! -w final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VIIw_SGPL_FUT_CON_NULL !!= @CODE@
VIIw_SG_FUT_CON_SUFFIX ;
VIIw_PL_FUT_CON_SUFFIX ;

LEXICON VIIw_SG_FUT_CON_SUFFIX   !!= @CODE@ w final singular
+3Sg:%>ki VERB_ENDLEX ;
+4Sg:%>yiki VERB_ENDLEX ;

LEXICON VIIw_PL_FUT_CON_SUFFIX   !!= @CODE@ w final plural
+3Pl:%>kwâwi VERB_ENDLEX ;
+4Pl:%>yikwâwi VERB_ENDLEX ;


!! Intransitive animate (AI)
!      ===========================
!      ===========================

!!! AEW == Ours: VAIv == VAIw, VAIn == VAIn

!!! STEM CLASS
LEXICON VAIw
+V+AI: VAIae_WICI ;

LEXICON VAIw_PL !!= @CODE@ stems that end in â or ê
+V+AI: VAIw_PL_WICI ;

LEXICON VAIae !!= @CODE@ stems that end in â or ê
+V+AI: VAIae_WICI ;

LEXICON VAIio !!= @CODE@ stems that end in i, î, o, ô
+V+AI: VAIio_WICI ;

LEXICON VAIn !!= @CODE@ 
+V+AI: VAIn_WICI ;

LEXICON VAIn_PL !!= @CODE@ 
+V+AI: VAIn_PL_WICI ;

LEXICON VAIm !!= @CODE@ These are VTI3 in Arok's database
+V+AI:  VTIm_WICI ;

!!! WICI

! ===========================================================
! ===========================================================
!!!!! -w final verbs (refers to the 3 SG form ending) !!!!!
!!!@@@!!! root.lexc: add Der/Com to multi char symbols
! ===========================================================
! ===========================================================

LEXICON VAIw_PL_WICI !!!! plural only
@U.wici.NULL@ VAIw_PL_ORDER ;
@U.wici.wici@+Der/Com+V+TA:@U.wici.wici@%^COM%>i2m VTA_PL_ORDER ; ! ^COM used to trigger vowel lengthening at stem-im juncture

! ===========================================================
! ===========================================================
!!!!! -n final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VAIn_WICI !!!!
@U.wici.NULL@ VAIn_ORDER ;
@U.wici.wici@+Der/Com+V+TA:@U.wici.wici@%>im VTA_ORDER ;

LEXICON VAIn_PL_WICI !!!! plural only
@U.wici.NULL@ VAIn_PL_ORDER ;
@U.wici.wici@+Der/Com+V+TA:@U.wici.wici@%>im VTA_PL_ORDER ;

! ===========================================================
! ===========================================================
!!!!! -â and -ê final stems !!!!!
! ===========================================================
! ===========================================================

LEXICON VAIae_WICI !!!!
@U.wici.NULL@ VAIae_ORDER ;
@U.wici.wici@+Der/Com+V+TA:@U.wici.wici@%^COM%>i2m VTA_ORDER ; ! ^COM used to trigger vowel lengthening at stem-im juncture

! ===========================================================
! ===========================================================
!!!!! -î/i and -ô/o final stems !!!!!
! ===========================================================
! ===========================================================

LEXICON VAIio_WICI !!!!
@U.wici.NULL@ VAIio_ORDER ;
@U.wici.wici@+Der/Com+V+TA:@U.wici.wici@%>im VTA_ORDER ;


!!! ORDERS


! ===========================================================
! ===========================================================
!!!!! -n final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! =========================================================== ! NOTE: imperative and fut con do not take tense

LEXICON VAIn_ORDER !!= @CODE@
@U.order.indep@+Ind:@U.order.indep@ VAIn_IND_TENSE ; !
@U.order.cnj@+Cnj:@U.order.cnj@ VAIn_CNJ_TENSE ; !
@U.order.imp@+Imp:@U.order.imp@  VAIn_IMP_PERSON ; !
@U.order.FutCon@+Fut+Cond:@U.order.FutCon@ VAIn_FUT_CON_PERSON ; !

LEXICON VAIn_PL_ORDER !!= @CODE@ plural only  
@U.order.indep@+Ind:@U.order.indep@ VAIn_PL_IND_TENSE ; !
@U.order.cnj@+Cnj:@U.order.cnj@ VAIn_PL_CNJ_TENSE ; !
@U.order.imp@+Imp:@U.order.imp@  VAIn_PL_IMP_PERSON ; !
@U.order.FutCon@+Fut+Cond:@U.order.FutCon@ VAIn_PL_FUT_CON_PERSON ; !




! ===========================================================
! ===========================================================
!!!!! -â and -ê final stems !!!!!
! ===========================================================
! =========================================================== ! NOTE: imperative and fut con do not take tense

LEXICON VAIae_ORDER !!= @CODE@
@U.order.indep@+Ind:@U.order.indep@ VAIae_IND_TENSE ; !
@U.order.cnj@+Cnj:@U.order.cnj@ VAIae_CNJ_TENSE ; !
@U.order.imp@+Imp:@U.order.imp@  VAIw_IMP_PERSON ; !
@U.order.FutCon@+Fut+Cond:@U.order.FutCon@ VAIw_FUT_CON_PERSON ; !

LEXICON VAIw_PL_ORDER !!= @CODE@ plural only 
@U.order.indep@+Ind:@U.order.indep@ VAIw_PL_IND_TENSE ; !
@U.order.cnj@+Cnj:@U.order.cnj@ VAIw_PL_CNJ_TENSE ; !
@U.order.imp@+Imp:@U.order.imp@  VAIw_PL_IMP_PERSON ; !
@U.order.FutCon@+Fut+Cond:@U.order.FutCon@ VAIw_PL_FUT_CON_PERSON ; !

! ===========================================================
! ===========================================================
!!!!! -î/i and -ô/o final stems !!!!!
! ===========================================================
! =========================================================== ! NOTE: imperative and fut con do not take tense

LEXICON VAIio_ORDER !!= @CODE@
@U.order.indep@+Ind:@U.order.indep@ VAIio_IND_TENSE ; !
@U.order.cnj@+Cnj:@U.order.cnj@ VAIio_CNJ_TENSE ; !
@U.order.imp@+Imp:@U.order.imp@  VAIw_IMP_PERSON ; !
@U.order.FutCon@+Fut+Cond:@U.order.FutCon@ VAIw_FUT_CON_PERSON ; !



!!! TENSES


! ===========================================================
! ===========================================================
!!!!! -n final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VAIn_IND_TENSE
@U.tense.Prs@+Prs:@U.tense.Prs@ VAIn_IND_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VAIn_IND_PERSON ; !
@U.tense.FutDef@+Fut+Def:@U.tense.FutDef@ VAIn_IND_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VAIn_IND_PERSON ; !

LEXICON VAIn_CNJ_TENSE
@U.tense.Prs@+Prs:@U.tense.Prs@ VAIn_CNJ_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VAIn_CNJ_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VAIn_CNJ_PERSON ; !

LEXICON VAIn_PL_IND_TENSE !!= @CODE@ plural only

@U.tense.Prs@+Prs:@U.tense.Prs@ VAIn_PL_IND_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VAIn_PL_IND_PERSON ; !
@U.tense.FutDef@+Fut+Def:@U.tense.FutDef@ VAIn_PL_IND_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VAIn_PL_IND_PERSON ; !

LEXICON VAIn_PL_CNJ_TENSE !!= @CODE@ plural only
@U.tense.Prs@+Prs:@U.tense.Prs@ VAIn_PL_CNJ_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VAIn_PL_CNJ_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VAIn_PL_CNJ_PERSON ; !


! ===========================================================
! ===========================================================
!!!!! -w final verbs (refers to the 3 SG form ending) Plural only !!!!!
! ===========================================================
! ===========================================================
LEXICON VAIw_PL_IND_TENSE !!= @CODE@ plural only

@U.tense.Prs@+Prs:@U.tense.Prs@ VAIw_PL_IND_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VAIw_PL_IND_PERSON ; !
@U.tense.FutDef@+Fut+Def:@U.tense.FutDef@ VAIw_PL_IND_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VAIw_PL_IND_PERSON ; !

LEXICON VAIw_PL_CNJ_TENSE !!= @CODE@ plural only
@U.tense.Prs@+Prs:@U.tense.Prs@ VAIw_PL_CNJ_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VAIw_PL_CNJ_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VAIw_PL_CNJ_PERSON ; !



! ===========================================================
! ===========================================================
!!!!! -â and -ê final stems !!!!!
! ===========================================================
! =========================================================== ! NOTE: imperative and fut con do not take tense

LEXICON VAIae_IND_TENSE
@U.tense.Prs@+Prs:@U.tense.Prs@ VAIae_IND_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VAIae_IND_PERSON ; !
@U.tense.FutDef@+Fut+Def:@U.tense.FutDef@ VAIae_IND_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VAIae_IND_PERSON ; !

LEXICON VAIae_CNJ_TENSE
@U.tense.Prs@+Prs:@U.tense.Prs@ VAIae_CNJ_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VAIae_CNJ_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VAIae_CNJ_PERSON ; !


! ===========================================================
! ===========================================================
!!!!! -î/i and -ô/o final stems !!!!!
! ===========================================================
! =========================================================== ! NOTE: imperative and fut con do not take tense

LEXICON VAIio_IND_TENSE
@U.tense.Prs@+Prs:@U.tense.Prs@ VAIio_IND_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VAIio_IND_PERSON ; !
@U.tense.FutDef@+Fut+Def:@U.tense.FutDef@ VAIio_IND_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VAIio_IND_PERSON ; !

LEXICON VAIio_CNJ_TENSE
@U.tense.Prs@+Prs:@U.tense.Prs@ VAIio_CNJ_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VAIio_CNJ_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VAIio_CNJ_PERSON ; !



!!! PERSON



! ===========================================================
! ===========================================================
!!!!! -n final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VAIn_IND_PERSON !!= @CODE@ 
@U.person.NI@ VAIn_IND_NI ;
@U.person.KI@ VAIn_IND_KI ;
@U.person.NULL@ VAIn_IND_NULL ;

LEXICON VAIn_CNJ_PERSON !!= @CODE@ 
@U.person.NULL@ VAIn_CNJ_NULL ;

LEXICON VAIn_FUT_CON_PERSON !!= @CODE@ 
@U.person.NULL@ VAIn_FUT_CON_NULL ;

LEXICON VAIn_IMP_PERSON !!= @CODE@ 
@U.person.NULL@ VAIn_IMP_NULL ;

LEXICON VAIn_PL_IND_PERSON !!= @CODE@ plural only
@U.person.NI@ VAIn_IND_NI_PL_SUFFIX ;
@U.person.KI@ VAIn_IND_KI_PL_SUFFIX ;
@U.person.NULL@ VAIn_IND_NULL_PL_SUFFIX ;

LEXICON VAIn_PL_CNJ_PERSON !!= @CODE@ plural only
@U.person.NULL@ VAIn_CNJ_NULL_PL_SUFFIX ;

LEXICON VAIn_PL_FUT_CON_PERSON !!= @CODE@ plural only
@U.person.NULL@ VAIn_FUT_CON_NULL_PL_SUFFIX ;

LEXICON VAIn_PL_IMP_PERSON !!= @CODE@ plural only
@U.person.NULL@ VAIn_IMP_NULL_PL_SUFFIX ;

! ===========================================================
! ===========================================================
!!!!! -w final verbs (refers to the 3 SG form ending) plural only!!!!!
! ===========================================================
! ===========================================================

LEXICON VAIw_PL_IND_PERSON !!= @CODE@ plural only
@U.person.NI@ VAIw_IND_NI_PL_SUFFIX ;
@U.person.KI@ VAIw_IND_KI_PL_SUFFIX ;
@U.person.NULL@ VAIw_IND_NULL_PL_SUFFIX ;

LEXICON VAIw_PL_CNJ_PERSON !!= @CODE@ plural only
@U.person.NULL@ VAIw_CNJ_NULL_PL_SUFFIX ;

LEXICON VAIw_PL_FUT_CON_PERSON !!= @CODE@ plural only
@U.person.NULL@ VAIw_FUT_CON_NULL_PL_SUFFIX ;

LEXICON VAIw_PL_IMP_PERSON !!= @CODE@ plural only
@U.person.NULL@ VAIw_IMP_NULL_PL_SUFFIX ;


! ===========================================================
! ===========================================================
!!!!! -â and -ê final stems !!!!!
! ===========================================================
! =========================================================== ! NOTE: imperative and fut con do not take tense
LEXICON VAIae_IND_PERSON !!= @CODE@ 
@U.person.NI@ VAIw_IND_NI ;
@U.person.KI@ VAIw_IND_KI ;
@U.person.NULL@ VAIae_IND_NULL ;! only need for unspecified actor

LEXICON VAIae_CNJ_PERSON !!= @CODE@ 
@U.person.NULL@ VAIae_CNJ_NULL ;

LEXICON VAIw_FUT_CON_PERSON !!= @CODE@ 

@U.person.NULL@ VAIae_FUT_CON_NULL ;

LEXICON VAIw_IMP_PERSON !!= @CODE@ 
@U.person.NULL@ VAIw_IMP_NULL ;


! ===========================================================
! ===========================================================
!!!!! -î/i and -ô/o final stems !!!!!
! ===========================================================
! =========================================================== ! NOTE: imperative and fut con do not take tense
LEXICON VAIio_IND_PERSON !!= @CODE@ 
@U.person.NI@ VAIw_IND_NI ;
@U.person.KI@ VAIw_IND_KI ;
@U.person.NULL@ VAIio_IND_NULL ;! only need for unspecified actor

LEXICON VAIio_CNJ_PERSON !!= @CODE@ 
@U.person.NULL@ VAIio_CNJ_NULL ;

!!! SUFFIXES

! *********************
!!!! INDEPENDENT !!!!
! *********************


! ===========================================================
! ===========================================================
!!!!! -w final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

!!!@@@!!! n2 == ee->aa case

LEXICON VAIw_IND_NI   !!= @CODE@  
VAIw_IND_NI_SG_SUFFIX ;
VAIw_IND_NI_PL_SUFFIX ;

LEXICON VAIw_IND_NI_SG_SUFFIX   !!= @CODE@ 
+1Sg:%>n2 VERB_ENDLEX ;

LEXICON VAIw_IND_NI_PL_SUFFIX   !!= @CODE@
+1Pl:%>n2ân VERB_ENDLEX ;

LEXICON VAIw_IND_KI   !!= @CODE@  
VAIw_IND_KI_SG_SUFFIX ;
VAIw_IND_KI_PL_SUFFIX ;

LEXICON VAIw_IND_KI_SG_SUFFIX   !!= @CODE@ 
+2Sg:%>n2 VERB_ENDLEX ;

LEXICON VAIw_IND_KI_PL_SUFFIX   !!= @CODE@ 
+12Pl:%>n2aw VERB_ENDLEX ;
+12Pl:%>n2ânaw VERB_ENDLEX ;
+2Pl:%>n2âwâw VERB_ENDLEX ;

LEXICON VAIae_IND_NULL   !!= @CODE@  
VAIae_IND_NULL_SG_SUFFIX ;
VAIw_IND_NULL_PL_SUFFIX ;

LEXICON VAIio_IND_NULL   !!= @CODE@  
VAIio_IND_NULL_SG_SUFFIX ;
VAIw_IND_NULL_PL_SUFFIX ;

LEXICON VAIae_IND_NULL_SG_SUFFIX
+3Sg:%>w VERB_ENDLEX ;
!           ********
! **** Unspecified Actor ****
!!!@@@!!! probably don't need Pl or Obv Unspec for VAI. 
!           ********


+X:%>n2iwiw VERB_ENDLEX ;
+X:%>n2iwan VERB_ENDLEX ;

LEXICON VAIio_IND_NULL_SG_SUFFIX
!           ********
! **** Unspecified Actor ****
!           ********
+3Sg:%>w VERB_ENDLEX ;

+X:%>n2âniwiw VERB_ENDLEX ;
+X:%>n2âniwan VERB_ENDLEX ;



LEXICON VAIw_IND_NULL_PL_SUFFIX   !!= @CODE@
+3Pl:%>wak VERB_ENDLEX ;
+4Sg/Pl:%>yiwa VERB_ENDLEX ;


! ===========================================================
! ===========================================================
!!!!! -n final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VAIn_IND_NI   !!= @CODE@ 
VAIn_IND_NI_SG_SUFFIX ;
VAIn_IND_NI_PL_SUFFIX ;

LEXICON VAIn_IND_NI_SG_SUFFIX   !!= @CODE@ 
+1Sg:%>in VERB_ENDLEX ;

LEXICON VAIn_IND_NI_PL_SUFFIX   !!= @CODE@ 
+1Pl:%>inân VERB_ENDLEX ;

LEXICON VAIn_IND_KI   !!= @CODE@  
VAIn_IND_KI_SG_SUFFIX ;
VAIn_IND_KI_PL_SUFFIX ;

LEXICON VAIn_IND_KI_SG_SUFFIX   !!= @CODE@ 
+2Sg:%>in VERB_ENDLEX ;

LEXICON VAIn_IND_KI_PL_SUFFIX   !!= @CODE@ 
+12Pl:%>inaw VERB_ENDLEX ;
+12Pl:%>inânaw VERB_ENDLEX ;
+2Pl:%>inâwâw VERB_ENDLEX ;

LEXICON VAIn_IND_NULL   !!= @CODE@  
VAIn_IND_NULL_SG_SUFFIX ;
VAIn_IND_NULL_PL_SUFFIX ;

LEXICON VAIn_IND_NULL_SG_SUFFIX   !!= @CODE@ 
+3Sg:%>0 VERB_ENDLEX ;
+3Sg:%>iw VERB_ENDLEX ;




!           ********
! **** Unspecified Actor ****
!           ********


+X:%>inâniwiw VERB_ENDLEX ;
+X:%>inâniwan VERB_ENDLEX ;

LEXICON VAIn_IND_NULL_PL_SUFFIX   !!= @CODE@ 
+3Pl:%>wak VERB_ENDLEX ;
+3Pl:%>iwak VERB_ENDLEX ;
+4Sg/Pl:%>iyiwa VERB_ENDLEX ;


! ===================================================================================================================
! ===================================================================================================================
! ===================================================================================================================
! ===================================================================================================================


! ******************
!!!! CONJUNCT !!!!
! ******************

! ===========================================================
! ===========================================================
!!!!! -w final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================


LEXICON VAIae_CNJ_NULL   !!= @CODE@ 

VAIae_CNJ_NULL_SG_SUFFIX ;
VAIw_CNJ_NULL_PL_SUFFIX ;

LEXICON VAIio_CNJ_NULL   !!= @CODE@ 

VAIio_CNJ_NULL_SG_SUFFIX ;
VAIw_CNJ_NULL_PL_SUFFIX ;

LEXICON VAIae_CNJ_NULL_SG_SUFFIX   !!= @CODE@ 

+1Sg:%>yân VERB_ENDLEX ;
+2Sg:%>yan VERB_ENDLEX ;
+3Sg:%>t VERB_ENDLEX ;

!           ********
! **** Unspecified Actor ****
!           ********

+X:%>hk VERB_ENDLEX ;
+X:%>niwahk VERB_ENDLEX ;
+X:%>niwik VERB_ENDLEX ;

LEXICON VAIio_CNJ_NULL_SG_SUFFIX   !!= @CODE@ 

+1Sg:%>yân VERB_ENDLEX ;
+2Sg:%>yan VERB_ENDLEX ;
+3Sg:%>t VERB_ENDLEX ;

!           ********
! **** Unspecified Actor ****
!           ********

+X:%>hk VERB_ENDLEX ;
+X:%>nâniwahk VERB_ENDLEX ;
+X:%>nâniwik VERB_ENDLEX ;

LEXICON VAIw_CNJ_NULL_PL_SUFFIX   !!= @CODE@ 

+1Pl:%>yâhk VERB_ENDLEX ;
+12Pl:%>yahk VERB_ENDLEX ;
+2Pl:%>yêk VERB_ENDLEX ;
+3Pl:%>cik VERB_ENDLEX ;
+4Sg/Pl:%>yit VERB_ENDLEX ;


! ===========================================================
! ===========================================================
!!!!! -n final verbs (refers to the 3 SG form ending) !!!!!
!!!@@@!!! stems/verbs.lexc: make all VAI2 == n3 ending
! ===========================================================
! ===========================================================

LEXICON VAIn_CNJ_NULL   !!= @CODE@ 

VAIn_CNJ_NULL_SG_SUFFIX ;
VAIn_CNJ_NULL_PL_SUFFIX ;

LEXICON VAIn_CNJ_NULL_SG_SUFFIX   !!= @CODE@ 

+1Sg:%>iyân VERB_ENDLEX ;
+2Sg:%>iyan VERB_ENDLEX ;
+3Sg:%>it VERB_ENDLEX ;
+X:%>ihk VERB_ENDLEX ;
+X:%>inâniwahk VERB_ENDLEX ;


LEXICON VAIn_CNJ_NULL_PL_SUFFIX   !!= @CODE@ 

+1Pl:%>iyâhk VERB_ENDLEX ;
+12Pl:%>iyahk VERB_ENDLEX ;
+2Pl:%>iyêk VERB_ENDLEX ;
+3Pl:%>icik VERB_ENDLEX ;
+3Pl:%>kik VERB_ENDLEX ;
+4Sg/Pl:%>iyit VERB_ENDLEX ;



! ===================================================================================================================
! ===================================================================================================================
! ===================================================================================================================
! ===================================================================================================================


! ********************
!!!! IMPERATIVE !!!!
! ********************

! ===========================================================
! ===========================================================
!!!!! -w final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VAIw_IMP_NULL
VAIw_IMP_SG_SUFFIX ;
VAIw_IMP_NULL_PL_SUFFIX ;

LEXICON VAIw_IMP_SG_SUFFIX
+Imm+2Sg:%>0   VERB_ENDLEX ; ! 
+Del+2Sg:%>hkan   VERB_ENDLEX ; !


LEXICON VAIw_IMP_NULL_PL_SUFFIX
+Imm+12Pl:%>tân	  VERB_ENDLEX ; !
+Imm+2Pl:%>k	 VERB_ENDLEX ; !
+Del+12Pl:%>hkahk   VERB_ENDLEX ; !
+Del+2Pl:%>hkêk   VERB_ENDLEX ; !

! ===========================================================
! ===========================================================
!!!!! -n final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VAIn_IMP_NULL
VAIn_IMP_SG_SUFFIX ;
VAIn_IMP_NULL_PL_SUFFIX ;

LEXICON VAIn_IMP_SG_SUFFIX
+Imm+2Sg:%>i   VERB_ENDLEX ; ! 
+Del+2Sg:%>îhkan   VERB_ENDLEX ; !


LEXICON VAIn_IMP_NULL_PL_SUFFIX
+Imm+12Pl:%>itân	  VERB_ENDLEX ; !
+Imm+2Pl:%>ik	 VERB_ENDLEX ; !
+Del+12Pl:%>îhkahk   VERB_ENDLEX ; !
+Del+2Pl:%>îhkêk   VERB_ENDLEX ; !


! ===================================================================================================================
! ===================================================================================================================
! ===================================================================================================================
! ===================================================================================================================


! ****************************
!!!! FUTURE CONDITIONAL !!!!
! ****************************

! ===========================================================
! ===========================================================
!!!!! -w final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VAIae_FUT_CON_NULL   !!= @CODE@ 

VAIw_FUT_CON_NULL_SG_SUFFIX ;
VAIw_FUT_CON_NULL_PL_SUFFIX ;

LEXICON VAIw_FUT_CON_NULL_SG_SUFFIX   !!= @CODE@ 

+1Sg:%>yâni VERB_ENDLEX ;
+2Sg:%>yani VERB_ENDLEX ;
+3Sg:%>ci VERB_ENDLEX ;

!           ********
! **** Unspecified Actor ****
!           ********

+X:%>hki VERB_ENDLEX ;
!! +X+4Sg:%>yiki # ;

LEXICON VAIw_FUT_CON_NULL_PL_SUFFIX   !!= @CODE@ 

+1Pl:%>yâhki VERB_ENDLEX ;
+12Pl:%>yahko VERB_ENDLEX ;
+12Pl:%>yahki VERB_ENDLEX ;
+2Pl:%>yêko VERB_ENDLEX ;
+3Pl:%>twâwi VERB_ENDLEX ;
+4Sg/Pl:%>yici VERB_ENDLEX ;

!           ********
! **** Unspecified Actor ****
!           ********

+X:%>hkwâwi VERB_ENDLEX ;
!! +X+4Pl:%>yikwâwi # ;

! ===========================================================
! ===========================================================
!!!!! -n final verbs (refers to the 3 SG form ending) !!!!!
! ===========================================================
! ===========================================================

LEXICON VAIn_FUT_CON_NULL   !!= @CODE@ 

VAIn_FUT_CON_NULL_SG_SUFFIX ;
VAIn_FUT_CON_NULL_PL_SUFFIX ;

LEXICON VAIn_FUT_CON_NULL_SG_SUFFIX   !!= @CODE@ 

+1Sg:%>iyâni VERB_ENDLEX ;
+2Sg:%>iyani VERB_ENDLEX ;
+3Sg:%>ici VERB_ENDLEX ;
+3Sg:%>ki VERB_ENDLEX ;

!           ********
! **** Unspecified Actor ****
!           ********

+X:%>ki VERB_ENDLEX ;
!! +X+4Sg:%>iyiki # ;

LEXICON VAIn_FUT_CON_NULL_PL_SUFFIX   !!= @CODE@ 

+1Pl:%>iyâhki VERB_ENDLEX ;
+12Pl:%>iyahko VERB_ENDLEX ;
+12Pl:%>iyahki VERB_ENDLEX ;
+2Pl:%>iyêko VERB_ENDLEX ;
+3Pl:%>itwâwi VERB_ENDLEX ;
+3Pl:%>kwâwi VERB_ENDLEX ;
+4Sg/Pl:%>iyici VERB_ENDLEX ;

!           ********
! **** Unspecified Actor ****
!           ********

+X:%>kwâwi VERB_ENDLEX ;
!! +X+4Pl:%>iyikwâwi # ;



!! Transitive inanimate (TI)
!      ===========================
!      ===========================

!!! AEW == Ours: VTI1 == VTIm (we also have a plural only lexicon), VTI2 and VTI3 == VTIw (which goes to VAIw)

!!! STEM CLASS

LEXICON VTIm  !!= * @CODE@ 
+V+TI: VTIm_WICI ;

LEXICON VTIw
+V+TI: VAIae_WICI ;

LEXICON VTIm_PL  !!= * @CODE@  Plural
+V+TI: VTIm_PL_WICI ;

LEXICON VTIae  !!= * @CODE@ NOTE: These inflect just as VAI -w final stems, so they are redirected to those paradigms
+V+TI: VAIae_WICI ; 

LEXICON VTIio  !!= * @CODE@ NOTE: These inflect just as VAI -w final stems, so they are redirected to those paradigms
+V+TI: VAIio_WICI ; 
 

!!! WICI

! ===========================================================
! ===========================================================
!!!!! -t final verbs (refers to the stem ending) !!!!!
! ===========================================================
! ===========================================================


LEXICON VTIm_WICI
@U.wici.NULL@ VTIm_ORDER ;
@U.wici.wici@+Der/Com+V+TA:@U.wici.wici@môm VTA_ORDER ;

LEXICON VTIm_PL_WICI
@U.wici.NULL@ VTIm_PL_ORDER ;
@U.wici.wici@+Der/Com+V+TA:@U.wici.wici@môm VTA_PL_ORDER ;

!!! ORDERS

! ===========================================================
! ===========================================================
!!!!! -t final verbs (refers to the stem ending) !!!!!
! ===========================================================
! =========================================================== ! imperative and fut con do not take tense

LEXICON VTIm_ORDER !!= @CODE@ . 
@U.order.indep@+Ind:@U.order.indep@ VTIm_IND_TENSE ; !
@U.order.cnj@+Cnj:@U.order.cnj@ VTIm_CNJ_TENSE ; !
@U.order.imp@+Imp:@U.order.imp@  VTIm_IMP_PERSON ; !
@U.order.FutCon@+Fut+Cond:@U.order.FutCon@ VTIm_FUT_CON_PERSON ; !

LEXICON VTIm_PL_ORDER !!= @CODE@ plural only NOTE: imperative and fut con go straight to person lexica
@U.order.indep@+Ind:@U.order.indep@ VTIm_PL_IND_TENSE ; !
@U.order.cnj@+Cnj:@U.order.cnj@ VTIm_PL_CNJ_TENSE ; !
@U.order.imp@+Imp:@U.order.imp@  VTIm_PL_IMP_PERSON ; !
@U.order.FutCon@+Fut+Cond:@U.order.FutCon@ VTIm_PL_FUT_CON_PERSON ; !

!!! TENSES

! ===========================================================
! ===========================================================
!!!!! -t final verbs (refers to the stem ending) !!!!!
! ===========================================================
! ===========================================================


LEXICON VTIm_IND_TENSE
@U.tense.Prs@+Prs:@U.tense.Prs@ VTIm_IND_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VTIm_IND_PERSON ; !
@U.tense.FutDef@+Fut+Def:@U.tense.FutDef@ VTIm_IND_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VTIm_IND_PERSON ; !

LEXICON VTIm_CNJ_TENSE
@U.tense.Prs@+Prs:@U.tense.Prs@ VTIm_CNJ_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VTIm_CNJ_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VTIm_CNJ_PERSON ; !intentional

LEXICON VTIm_PL_IND_TENSE !!= @CODE@ plural only
@U.tense.Prs@+Prs:@U.tense.Prs@ VTIm_PL_IND_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VTIm_PL_IND_PERSON ; !
@U.tense.FutDef@+Fut+Def:@U.tense.FutDef@ VTIm_PL_IND_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VTIm_PL_IND_PERSON ; !

LEXICON VTIm_PL_CNJ_TENSE !!= @CODE@ plural only
@U.tense.Prs@+Prs:@U.tense.Prs@ VTIm_PL_CNJ_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VTIm_PL_CNJ_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VTIm_PL_CNJ_PERSON ; !intentional


!!! PERSON

! ===========================================================
! ===========================================================
!!!!! -t final verbs (refers to the stem ending) !!!!!
! ===========================================================
! ===========================================================


LEXICON VTIm_IND_PERSON !!= @CODE@ 
@U.person.NI@ VTIm_IND_NI ;
@U.person.KI@ VTIm_IND_KI ;
@U.person.NULL@ VTIm_IND_NULL ;

LEXICON VTIm_CNJ_PERSON !!= @CODE@ 
@U.person.NULL@ VTIm_CNJ_NULL ;

LEXICON VTIm_FUT_CON_PERSON !!= @CODE@ 
@U.person.NULL@ VTIm_FUT_CON_NULL ;

LEXICON VTIm_IMP_PERSON !!= @CODE@ 
@U.person.NULL@ VTIm_IMP_NULL ;

LEXICON VTIm_PL_IND_PERSON !!= @CODE@ plural only
@U.person.NI@ VTIm_IND_NI_PL_SUFFIX ;
@U.person.KI@ VTIm_IND_KI_PL_SUFFIX ;
@U.person.NULL@ VTIm_IND_NULL_PL_SUFFIX ;

LEXICON VTIm_PL_CNJ_PERSON !!= @CODE@ plural only
@U.person.NULL@ VTIm_CNJ_NULL_PL_SUFFIX ;

LEXICON VTIm_PL_FUT_CON_PERSON !!= @CODE@ plural only
@U.person.NULL@ VTIm_FUT_CON_NULL_PL_SUFFIX ;

LEXICON VTIm_PL_IMP_PERSON !!= @CODE@ plural only
@U.person.NULL@ VTIm_IMP_NULL_PL_SUFFIX ;

!!! SUFFIXES

! *********************
!!!! INDEPENDENT !!!!
! *********************


! ===========================================================
! ===========================================================
!!!!! -t final verbs (refers to the stem ending) !!!!!
! ===========================================================
! ===========================================================


LEXICON VTIm_IND_NI   !!= @CODE@  
VTIm_IND_NI_SG_SUFFIX ;
VTIm_IND_NI_PL_SUFFIX ;

LEXICON VTIm_IND_NI_SG_SUFFIX   !!= @CODE@ 
+1Sg:%>n2 VERB_ENDLEX ;

LEXICON VTIm_IND_NI_PL_SUFFIX   !!= @CODE@ 
+1Pl:%>n2ân VERB_ENDLEX ;

LEXICON VTIm_IND_KI   !!= @CODE@  
VTIm_IND_KI_SG_SUFFIX ;
VTIm_IND_KI_PL_SUFFIX ;

LEXICON VTIm_IND_KI_SG_SUFFIX   !!= @CODE@ 
+2Sg:%>n2 VERB_ENDLEX ;

LEXICON VTIm_IND_KI_PL_SUFFIX   !!= @CODE@ 
+12Pl:%>n2aw VERB_ENDLEX ;
+12Pl:%>n2ânaw VERB_ENDLEX ;
+2Pl:%>n2âwâw VERB_ENDLEX ;

LEXICON VTIm_IND_NULL   !!= @CODE@  
VTIm_IND_NULL_SG_SUFFIX ;
VTIm_IND_NULL_PL_SUFFIX ;

LEXICON VTIm_IND_NULL_SG_SUFFIX   !!= @CODE@ NOTE: X actor will eventually derive to VII, so it is not yet included as per Arok's paradigm
+3Sg:%>m VERB_ENDLEX ;

!           ********
! **** Unspecified Actor ****
!           ********

+Der/X:%>ikâtê VIIn_SG ; !! Derives to VIIn



LEXICON VTIm_IND_NULL_PL_SUFFIX   !!= @CODE@ 
+3Pl:%>mwak VERB_ENDLEX ;
+4Sg/Pl:%>miyiwa VERB_ENDLEX ;

!           ********
! **** Unspecified Actor ****
!           ********

+Der/X:%>ikâtê VIIn_PL ; !! Derives to VIIn
! ===================================================================================================================
! ===================================================================================================================
! ===================================================================================================================
! ===================================================================================================================


! ******************
!!!! CONJUNCT !!!!
! ******************

! ===========================================================
! ===========================================================
!!!!! -t final verbs (refers to the stem ending) !!!!!
! ===========================================================
! ===========================================================


LEXICON VTIm_CNJ_NULL   !!= @CODE@ 

VTIm_CNJ_NULL_SG_SUFFIX ;
VTIm_CNJ_NULL_PL_SUFFIX ;

LEXICON VTIm_CNJ_NULL_SG_SUFFIX   !!= @CODE@ 

+1Sg:%>mân VERB_ENDLEX ;
+2Sg:%>man VERB_ENDLEX ;
+3Sg:%>hk VERB_ENDLEX ;

!           ********
! **** Unspecified Actor ****
!           ********

+X+3Sg:%>mihk VERB_ENDLEX ;
!! +X+4Sg:%>mihiyik # ;

LEXICON VTIm_CNJ_NULL_PL_SUFFIX   !!= @CODE@ 

+1Pl:%>mâhk VERB_ENDLEX ;
+12Pl:%>mahk VERB_ENDLEX ;
+2Pl:%>mêk VERB_ENDLEX ;
+3Pl:%>hkik VERB_ENDLEX ;
+4Sg/Pl:%>miyit VERB_ENDLEX ;

!           ********
! **** Unspecified Actor ****
!           ********

+X+3Sg:%>mihki VERB_ENDLEX ;
!! +X+4Pl:%>mihiyiki # ;

! ===================================================================================================================
! ===================================================================================================================
! ===================================================================================================================
! ===================================================================================================================


! ********************
!!!! IMPERATIVE !!!!
! ********************

! ===========================================================
! ===========================================================
!!!!! -t final verbs (refers to the stem ending) !!!!!
! ===========================================================
! ===========================================================


LEXICON VTIm_IMP_NULL
VTIm_IMP_SG_SUFFIX ;
VTIm_IMP_NULL_PL_SUFFIX ;

LEXICON VTIm_IMP_SG_SUFFIX
+Imm+2Sg:%>0   VERB_ENDLEX ; ! 
+Del+2Sg:%>môhkan   VERB_ENDLEX ; !


LEXICON VTIm_IMP_NULL_PL_SUFFIX
+Imm+12Pl:%>tân	  VERB_ENDLEX ; ! ! <a> to <ê> / _ %>tân
+Imm+2Pl:%>mok	 VERB_ENDLEX ; !
+Del+12Pl:%>môhkahk   VERB_ENDLEX ; !
+Del+2Pl:%>môhkêk   VERB_ENDLEX ; !


! ===================================================================================================================
! ===================================================================================================================
! ===================================================================================================================
! ===================================================================================================================


! ****************************
!!!! FUTURE CONDITIONAL !!!!
! ****************************

! ===========================================================
! ===========================================================
!!!!! -t final verbs (refers to the stem ending) !!!!!
! ===========================================================
! ===========================================================


LEXICON VTIm_FUT_CON_NULL   !!= @CODE@ 

VTIm_FUT_CON_NULL_SG_SUFFIX ;
VTIm_FUT_CON_NULL_PL_SUFFIX ;

LEXICON VTIm_FUT_CON_NULL_SG_SUFFIX   !!= @CODE@ 

+1Sg:%>mâni VERB_ENDLEX ;
+2Sg:%>mani VERB_ENDLEX ;
+3Sg:%>hki VERB_ENDLEX ;

!           ********
! **** Unspecified Actor ****
!           ********

+X:%>mihki VERB_ENDLEX ;
!! +X+4Sg:%>mihiyiki # ;

LEXICON VTIm_FUT_CON_NULL_PL_SUFFIX   !!= @CODE@ 

+1Pl:%>mâhki VERB_ENDLEX ;
+12Pl:%>mahko VERB_ENDLEX ;
+12Pl:%>mahki VERB_ENDLEX ;
+2Pl:%>mêko VERB_ENDLEX ;
+3Pl:%>hkwâwi VERB_ENDLEX ;
+4Sg/Pl:%>miyici VERB_ENDLEX ;

!           ********
! **** Unspecified Actor ****
!           ********

!! +X+3Sg:%>mihkwâwi # ;
!! +X+4Sg:%>mihiyikwâwi # ;

! !! !!! Transitive inanimate (TA)
! !      ===========================
! !      ===========================
!!!@@@!!! SG/PL ALWAYS LISTED UNDER THE SINGULAR CONT.LEX

!!! AEW == Ours: VTA1, VTA2, VTA 3, VTA 4 == VTA (We also have plural only lexica), VTA5 == VTAi

!!! STEM CLASS

LEXICON VTA  !!= * @CODE@ Multi-Syllabic stems 
+V+TA:0 VTA_WICI ;

LEXICON VTA_PL  !!= * @CODE@ Multi-Syllabic stems plural only forms
+V+TA:0 VTA_PL_WICI ;

LEXICON VTAt  !!= * @CODE@ Multi-Syllabic t/s-final alternating stems
+V+TA:0 VTAt_WICI ;

LEXICON VTAi  !!= * @CODE@ Mono-Syllabic stems
+V+TA:0 VTAi_WICI ;


!!! WICI !!= * @CODE@ -RULES OUT TAs FROM TAKING THE COM.


LEXICON VTA_WICI  !!= * @CODE@ -Vw stem-ending verbs; where + i suf deletes w and i
@U.wici.NULL@ VTA_ORDER ;

LEXICON VTA_PL_WICI  !!= * @CODE@ -Vw stem-ending verbs; when deletes w and i plural only forms 
@U.wici.NULL@ VTA_PL_ORDER ;

LEXICON VTAt_WICI  !!= * @CODE@ -t ending stems
@U.wici.NULL@ VTAt_ORDER ;

LEXICON VTAi_WICI  !!= * @CODE@ single mora stems
@U.wici.NULL@ VTAi_ORDER ;


!!! ORDERS

LEXICON VTA_ORDER !!= @CODE@ Note: Imp and Fut Con don't take tense
@U.order.indep@+Ind:@U.order.indep@ VTA_IND_TENSE ; !
@U.order.cnj@+Cnj:@U.order.cnj@ VTA_CNJ_TENSE ; !
@U.order.imp@+Imp:@U.order.imp@  VTA_IMP_PERSON ; !
@U.order.FutCon@+Fut+Cond:@U.order.FutCon@ VTA_FUT_CON_PERSON ; !

LEXICON VTA_PL_ORDER !!= @CODE@ Note: Imp and Fut Con don't take tense 
@U.order.indep@+Ind:@U.order.indep@ VTA_PL_IND_TENSE ; !
@U.order.cnj@+Cnj:@U.order.cnj@ VTA_PL_CNJ_TENSE ; !
@U.order.imp@+Imp:@U.order.imp@  VTA_PL_IMP_PERSON ; !
@U.order.FutCon@+Fut+Cond:@U.order.FutCon@ VTA_PL_FUT_CON_PERSON ; !

LEXICON VTAi_ORDER !!= @CODE@ Note: Imp and Fut Con don't take tense ; Conjugates as TA regular except in 2sg IMM IMP
@U.order.indep@+Ind:@U.order.indep@ VTA_IND_TENSE ; !
@U.order.cnj@+Cnj:@U.order.cnj@ VTA_CNJ_TENSE ; !
@U.order.imp@+Imp:@U.order.imp@  VTAi_IMP_PERSON ; !
@U.order.FutCon@+Fut+Cond:@U.order.FutCon@ VTA_FUT_CON_PERSON ; !

LEXICON VTAt_ORDER !!= @CODE@ Note: Imp and Fut Con don't take tense ; Conjugates as TA regular except in 2sg IMM IMP
@U.order.indep@+Ind:@U.order.indep@ VTA_IND_TENSE ; !
@U.order.cnj@+Cnj:@U.order.cnj@ VTA_CNJ_TENSE ; !
@U.order.imp@+Imp:@U.order.imp@  VTAt_IMP_PERSON ; !
@U.order.FutCon@+Fut+Cond:@U.order.FutCon@ VTA_FUT_CON_PERSON ; !

!!! Tenses

LEXICON VTA_IND_TENSE !!= @CODE@ plural only
@U.tense.Prs@+Prs:@U.tense.Prs@ VTA_IND_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VTA_IND_PERSON ; !
@U.tense.FutDef@+Fut+Def:@U.tense.FutDef@ VTA_IND_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VTA_IND_PERSON ; !

LEXICON VTA_CNJ_TENSE !!= @CODE@ plural only
@U.tense.Prs@+Prs:@U.tense.Prs@ VTA_CNJ_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VTA_CNJ_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VTA_CNJ_PERSON ; !intentional

LEXICON VTA_PL_IND_TENSE !!= @CODE@ plural only
@U.tense.Prs@+Prs:@U.tense.Prs@ VTA_PL_IND_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VTA_PL_IND_PERSON ; !
@U.tense.FutDef@+Fut+Def:@U.tense.FutDef@ VTA_PL_IND_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VTA_PL_IND_PERSON ; !

LEXICON VTA_PL_CNJ_TENSE !!= @CODE@ plural only
@U.tense.Prs@+Prs:@U.tense.Prs@ VTA_PL_CNJ_PERSON ; !
@U.tense.Prt@+Prt:@U.tense.Prt@ VTA_PL_CNJ_PERSON ; !
@U.tense.FutInt@+Fut+Int:@U.tense.FutInt@ VTA_PL_CNJ_PERSON ; !intentional



!!! PERSON


LEXICON VTA_IND_PERSON !!= @CODE@ 
@U.person.NI@ VTA_IND_NI ;
@U.person.KI@ VTA_IND_KI ;
@U.person.NULL@ VTA_IND_NULL ;

LEXICON VTA_CNJ_PERSON !!= @CODE@ 
@U.person.NULL@ VTA_CNJ_NULL ;

LEXICON VTA_FUT_CON_PERSON !!= @CODE@ 
@U.person.NULL@ VTA_FUT_CON_NULL ;

LEXICON VTA_IMP_PERSON !!= @CODE@ 
@U.person.NULL@ VTA_IMP_NULL ;

LEXICON VTA_PL_IND_PERSON !!= @CODE@ 
@U.person.NI@ VTA_IND_NI_PL_SUFFIX ;
@U.person.KI@ VTA_IND_KI_PL_SUFFIX ;
@U.person.NULL@ VTA_IND_NULL_PL_SUFFIX ;

LEXICON VTA_PL_CNJ_PERSON !!= @CODE@ 
@U.person.NULL@ VTA_CNJ_NULL_PL_SUFFIX ;

LEXICON VTA_PL_FUT_CON_PERSON !!= @CODE@ 
@U.person.NULL@ VTA_FUT_CON_NULL_PL_SUFFIX ;

LEXICON VTA_PL_IMP_PERSON !!= @CODE@ 
@U.person.NULL@ VTA_IMP_NULL_PL_SUFFIX ;


!!!! VTAt (imp is only difference)

LEXICON VTAt_IMP_PERSON !!= @CODE@ no -i in 2sg+3SgO
@U.person.NULL@ VTAt_IMP_NULL ;

!!!! VTAi (imp is only difference)

LEXICON VTAi_IMP_PERSON !!= @CODE@ 
@U.person.NULL@ VTAi_IMP_NULL ;


!!! SUFFIXES


! ********************
!!!! Independent !!!!
! ********************



LEXICON VTA_IND_NI   !!= @CODE@  NOTE: No local, as local forms are always with ki-
VTA_IND_NI_SG_SUFFIX ;
VTA_IND_NI_PL_SUFFIX ;

! ~~~~~~~~~~~~~~~~~~~~~~
!!!!!! SINGULAR !!!!!! 
! ~~~~~~~~~~~~~~~~~~~~~~

LEXICON VTA_IND_NI_SG_SUFFIX   !!= @CODE@

!!!!!!!!!!!!!!!!!!!!
!!!!!! Direct !!!!!!
!!!!!!!!!!!!!!!!!!!!

!!!!!!!! Mixed !!!!!!!

+1Sg+3SgO:%>âw		  VERB_ENDLEX ;
+1Sg+4Sg/PlO:%>i2mâwa	  VERB_ENDLEX ; ! From NS 152

!!!!!!!!!!!!!!!!!!!!!
!!!!!! Inverse !!!!!!
!!!!!!!!!!!!!!!!!!!!!

!!!!!!!! Mixed !!!!!!!

+3Sg+1SgO:%>i2k        VERB_ENDLEX ;
+4Sg/Pl+1SgO:%>i2koyiwa	   VERB_ENDLEX ; ! FROM NS 152

!           ********
! **** Unspecified Actor ****
!           ********  NOTE: always mixed

+X+1SgO:%>i2kawin   VERB_ENDLEX ;


! ~~~~~~~~~~~~~~~~~~~~
!!!!!! PLURAL !!!!!! 
! ~~~~~~~~~~~~~~~~~~~~

LEXICON VTA_IND_NI_PL_SUFFIX   !!= @CODE@ 

!!!!!!!!!!!!!!!!!!!!
!!!!!! Direct !!!!!!
!!!!!!!!!!!!!!!!!!!!

!!!!!!!! Mixed !!!!!!!

+1Sg+3PlO:%>âwak	  VERB_ENDLEX ;

+1Pl+3SgO:%>ânân	  VERB_ENDLEX ;
+1Pl+3PlO:%>ânânak	  VERB_ENDLEX ;
+1Pl+4Sg/PlO:%>i2mânâna	  VERB_ENDLEX ; ! From NS 152

!!!!!!!!!!!!!!!!!!!!!
!!!!!! Inverse !!!!!!
!!!!!!!!!!!!!!!!!!!!!

!!!!!!!! Mixed !!!!!!!

+3Sg+1PlO:%>i2konân	  VERB_ENDLEX ;

+3Pl+1SgO:%>i2kwak	  VERB_ENDLEX ;
+3Pl+1SgO:%>i2kowak	  VERB_ENDLEX ;
+3Pl+1PlO:%>i2konânak  VERB_ENDLEX ;
+4Sg/Pl+1PlO:%>i2konâna	   VERB_ENDLEX ; ! FROM NS 152

!           ********
! **** Unspecified Actor ****
!           ********  NOTE: always mixed

+X+1PlO:%>i2kawinân   VERB_ENDLEX ;


!!!!!! KI- PREFIXING FORMS !!!!!!
! =================================

LEXICON VTA_IND_KI   !!= @CODE@  
VTA_IND_KI_SG_SUFFIX ;
VTA_IND_KI_PL_SUFFIX ;

! ~~~~~~~~~~~~~~~~~~~~~~
!!!!!! SINGULAR !!!!!! 
! ~~~~~~~~~~~~~~~~~~~~~~

LEXICON VTA_IND_KI_SG_SUFFIX   !!= @CODE@ 

!!!!!!!!!!!!!!!!!!!!
!!!!!! Direct !!!!!!
!!!!!!!!!!!!!!!!!!!!

!!!!!!!! Local !!!!!!!

+2Sg+1SgO:%>in         VERB_ENDLEX ;

!!!!!!!! Mixed !!!!!!!

+2Sg+3SgO:%>âw		  VERB_ENDLEX ;
+2Sg+4Sg/PlO:%>i2mâwa	  VERB_ENDLEX ; ! From NS 152

!!!!!!!!!!!!!!!!!!!!!
!!!!!! Inverse !!!!!!
!!!!!!!!!!!!!!!!!!!!!


!!!!!!!! Local !!!!!!!

+1Sg+2SgO:%>i2tin         VERB_ENDLEX ;

!!!!!!!! Mixed !!!!!!!

+3Sg+2SgO:%>i2k		  VERB_ENDLEX ;

+4Sg/Pl+2SgO:%>i2koyiwa	   VERB_ENDLEX ; ! FROM NS 152

!           ********
! **** Unspecified Actor ****
!           ********  NOTE: always mixed

+X+2SgO:%>i2kawin   VERB_ENDLEX ;

! ~~~~~~~~~~~~~~~~~~~~
!!!!!! PLURAL !!!!!! 
! ~~~~~~~~~~~~~~~~~~~~

LEXICON VTA_IND_KI_PL_SUFFIX   !!= @CODE@ 

!!!!!!!!!!!!!!!!!!!!
!!!!!! Direct !!!!!!
!!!!!!!!!!!!!!!!!!!!


!!!!!!!! Local !!!!!!!

+2Sg+1PlO:%>inân       VERB_ENDLEX ;
+2Pl+1SgO:%>inâwâw     VERB_ENDLEX ;
+2Pl+1PlO:%>inân       VERB_ENDLEX ;

!!!!!!!! Mixed !!!!!!!

+2Sg+3PlO:%>âwak VERB_ENDLEX ;

+2Pl+3SgO:%>âwâw	  VERB_ENDLEX ;
+2Pl+3PlO:%>âwâwak	  VERB_ENDLEX ;
+2Pl+4Sg/PlO:%>i2mâwâwa	  VERB_ENDLEX ; ! From NS 152

+12Pl+3SgO:%>ânaw	  VERB_ENDLEX ; ! 12Pl forms
+12Pl+3PlO:%>ânawak	  VERB_ENDLEX ;
+12Pl+4Sg/PlO:%>i2mânawa	  VERB_ENDLEX ; ! From NS 152


!!!!!!!!!!!!!!!!!!!!!
!!!!!! Inverse !!!!!!
!!!!!!!!!!!!!!!!!!!!!


!!!!!!!! Local !!!!!!!

+1Sg+2PlO:%>i2tinâwâw         VERB_ENDLEX ;
+1Pl+2SgO:%>i2tinân         VERB_ENDLEX ; !OBS
+1Pl+2PlO:%>i2tinân         VERB_ENDLEX ;

!!!!!!!! Mixed !!!!!!!

+3Sg+12PlO:%>i2konaw	  VERB_ENDLEX ;
+3Sg+2PlO:%>i2kowâw	  VERB_ENDLEX ;

+3Pl+2SgO:%>i2kwak	  VERB_ENDLEX ;
+3Pl+2SgO:%>i2kowak	  VERB_ENDLEX ;
+3Pl+12PlO:%>i2konawak VERB_ENDLEX ;
+3Pl+2PlO:%>i2kowâwak  VERB_ENDLEX ;

+4Sg/Pl+12PlO:%>i2konawa	   VERB_ENDLEX ; ! FROM NS 152
+4Sg/Pl+2PlO:%>i2kowâwa	   VERB_ENDLEX ; ! FROM NS 152


!           ********
! **** Unspecified Actor ****
!           ********  NOTE: always mixed

+X+12PlO:%>i2kawinânaw	   VERB_ENDLEX ;
+X+12PlO:%>i2kawinaw	   VERB_ENDLEX ;
+X+2PlO:%>i2kawinâwâw	   VERB_ENDLEX ;


!!!!!! NULL PREFIXING FORMS !!!!!!
! ==================================

LEXICON VTA_IND_NULL   !!= @CODE@  NOTE: never local
VTA_IND_NULL_SG_SUFFIX ;
VTA_IND_NULL_PL_SUFFIX ;


! ~~~~~~~~~~~~~~~~~~~~~~
!!!!!! SINGULAR !!!!!! 
! ~~~~~~~~~~~~~~~~~~~~~~

LEXICON VTA_IND_NULL_SG_SUFFIX   !!= @CODE@ 

!!!!!!!!!!!!!!!!!!!!
!!!!!! Direct !!!!!!
!!!!!!!!!!!!!!!!!!!!

!!!!!!!! Mixed !!!!!!!

+3Sg+4Sg/PlO:%>êw		  VERB_ENDLEX ; ! Implementing fourth, fifth persons 
+3Sg+5Sg/PlO:%>i2mêw		  VERB_ENDLEX ; ! Implementing fourth, fifth persons 
+4Sg/Pl+5Sg/PlO:%>êyiwa	  VERB_ENDLEX ; ! Implementing fourth, fifth persons

!!!!!!!!!!!!!!!!!!!!!
!!!!!! Inverse !!!!!!
!!!!!!!!!!!!!!!!!!!!!

!!!!!!!! Mixed !!!!!!!

+4Sg/Pl+3SgO:%>i2k	   VERB_ENDLEX ; ! FROM NS 152
+4Sg/Pl+3SgO:%>i2kow	   VERB_ENDLEX ; ! FROM NS 152
+5Sg/Pl+4Sg/PlO:%>i2koyiwa	   VERB_ENDLEX ; ! FROM NS 152


!           ********
! **** Unspecified Actor ****
!           ********  NOTE: always mixed

+X+3SgO:%>âw   VERB_ENDLEX ;
+X+4Sg/PlO:%>âyiwa   VERB_ENDLEX ;


! ~~~~~~~~~~~~~~~~~~~~
!!!!!! PLURAL !!!!!! 
! ~~~~~~~~~~~~~~~~~~~~

LEXICON VTA_IND_NULL_PL_SUFFIX   !!= @CODE@ 

!!!!!!!!!!!!!!!!!!!!
!!!!!! Direct !!!!!!
!!!!!!!!!!!!!!!!!!!!

!!!!!!!! Mixed !!!!!!!

+3Pl+4Sg/PlO:%>êwak	  VERB_ENDLEX ; ! Implementing fourth, fifth persons
+3Pl+5Sg/PlO:%>i2mêwak	VERB_ENDLEX ;	

!!!!!!!!!!!!!!!!!!!!!
!!!!!! Inverse !!!!!!
!!!!!!!!!!!!!!!!!!!!!

!!!!!!!! Mixed !!!!!!!


+4Sg/Pl+3PlO:%>i2kwak	   VERB_ENDLEX ; ! FROM NS 152
+4Sg/Pl+3PlO:%>i2kowak	   VERB_ENDLEX ; ! FROM NS 152

!           ********
! **** Unspecified Actor ****
!           ********  NOTE: always mixed

+X+3PlO:%>âwak   VERB_ENDLEX ;


! ===================================================================================================================
! ===================================================================================================================
! ===================================================================================================================
! ===================================================================================================================


! ********************
!!!! Conjunct !!!!
! ********************



LEXICON VTA_CNJ_NULL
VTA_CNJ_NULL_SG_SUFFIX ;
VTA_CNJ_NULL_PL_SUFFIX ;

!! ~~~~~~~~~~~~~~~~~~~~~~
!!!!!! SINGULAR !!!!!! 
! ~~~~~~~~~~~~~~~~~~~~~~

LEXICON VTA_CNJ_NULL_SG_SUFFIX

!!!!!!!!!!!!!!!!!!!!
!!!!!! Direct !!!!!!
!!!!!!!!!!!!!!!!!!!!


!!!!!!! Local !!!!!!!

+2Sg+1SgO:%>iyan       VERB_ENDLEX ;

!!!!!!! Mixed !!!!!!!

+1Sg+3SgO:%>ak	   VERB_ENDLEX ;
+2Sg+3SgO:%>at	   VERB_ENDLEX ;

+1Sg+4Sg/PlO:%>i2mak	   VERB_ENDLEX ; ! From NS 152
+2Sg+4Sg/PlO:%>i2mat	   VERB_ENDLEX ; ! From NS 152

!!!!!!!! Third/Fourth/Fifth Persons !!!!!!!!

+3Sg+4Sg/PlO:%>ât	   VERB_ENDLEX ;

+4Sg/Pl+5Sg/PlO:%>âyit      VERB_ENDLEX ; ! From NS 152

+3Sg+5Sg/PlO:%>i2mât	   VERB_ENDLEX ;


!!!!!!!!!!!!!!!!!!!!!
!!!!!! Inverse !!!!!!
!!!!!!!!!!!!!!!!!!!!!

!!!!!!! Local !!!!!!!

+1Sg+2SgO:%>i2tân         VERB_ENDLEX ;

!!!!!!! Mixed !!!!!!!

+3Sg+1SgO:%>it		   VERB_ENDLEX ;
+3Sg+2SgO:%>i2sk		   VERB_ENDLEX ;

+4Sg/Pl+1SgO:%>i2mit	   VERB_ENDLEX ; ! FROM NS 152
+4Sg/Pl+2SgO:%>i2misk	   VERB_ENDLEX ;

!!!!!!!! Third/Fourth/Fifth Persons !!!!!!!!

+4Sg/Pl+3SgO:%>i2kot	   VERB_ENDLEX ; ! FROM NS 152
+5Sg/Pl+4Sg/PlO:%>i2koyit	   VERB_ENDLEX ; ! FROM NS 152

!           ********
! **** Unspecified Actor ****
!           ********  NOTE: always mixed

+X+1SgO:%>i2kawiyân	   VERB_ENDLEX ;
+X+2SgO:%>i2kawiyan	   VERB_ENDLEX ;

+X+3SgO:%>i2ht	   VERB_ENDLEX ;
+X+4Sg/PlO:%>i2miht	   VERB_ENDLEX ;


! ~~~~~~~~~~~~~~~~~~~~
! ~~~~~~~~~~~~~~~~~~~~
!!!!!! PLURAL !!!!!! 
! ~~~~~~~~~~~~~~~~~~~~ 
! ~~~~~~~~~~~~~~~~~~~~
LEXICON VTA_CNJ_NULL_PL_SUFFIX

!!!!!!!!!!!!!!!!!!!!
!!!!!! Direct !!!!!!
!!!!!!!!!!!!!!!!!!!!


!!!!!!! Local !!!!!!!

+2Sg+1PlO:%>iyâhk       VERB_ENDLEX ;

+2Pl+1PlO:%>iyâhk       VERB_ENDLEX ;
+2Pl+1SgO:%>iyêk       VERB_ENDLEX ;

!!!!!!! Mixed !!!!!!!

+1Sg+3PlO:%>akik	   VERB_ENDLEX ;

+2Sg+3PlO:%>acik	   VERB_ENDLEX ;

+1Pl+3SgO:%>âyâhk	   VERB_ENDLEX ;
+1Pl+3PlO:%>âyâhkik	   VERB_ENDLEX ;

+12Pl+3SgO:%>âyahk	   VERB_ENDLEX ;
+12Pl+3PlO:%>âyahkok   VERB_ENDLEX ;

+2Pl+3SgO:%>âyêk	   VERB_ENDLEX ;
+2Pl+3PlO:%>âyêkok	   VERB_ENDLEX ;

+1Pl+4Sg/PlO:%>i2mâyâhk	   VERB_ENDLEX ; ! From NS 152
+12Pl+4Sg/PlO:%>i2mâyahk   VERB_ENDLEX ; ! From NS 152
+2Pl+4Sg/PlO:%>i2mâyêk   VERB_ENDLEX ; ! From NS 152

!!!!!!!! Third/Fourth/Fifth Persons !!!!!!!!

+3Pl+4Sg/PlO:%>âcik      VERB_ENDLEX ; ! From NS 152

+3Pl+5Sg/PlO:%>i2mâcik      VERB_ENDLEX ; ! From NS 152


!!!!!!!!!!!!!!!!!!!!!
!!!!!! Inverse !!!!!!
!!!!!!!!!!!!!!!!!!!!!

!!!!!!! Local !!!!!!!

+1Pl+2SgO:%>i2tâhk         VERB_ENDLEX ; 

+1Pl+2PlO:%>i2tâhk         VERB_ENDLEX ; 
+1Sg+2PlO:%>i2takok         VERB_ENDLEX ;

!!!!!!! Mixed !!!!!!!

+3Pl+1SgO:%>icik	   VERB_ENDLEX ;

+3Pl+2SgO:%>i2skik	   VERB_ENDLEX ;

+3Sg+1PlO:%>i2koyâhk	   VERB_ENDLEX ;
+3Pl+1PlO:%>i2koyâhkik  VERB_ENDLEX ;

+3Sg+12PlO:%>i2koyahk   VERB_ENDLEX ;
+3Pl+12PlO:%>i2koyahkok VERB_ENDLEX ;

+3Sg+2PlO:%>i2koyêk	   VERB_ENDLEX ;
+3Pl+2PlO:%>i2koyêkok   VERB_ENDLEX ;

+4Sg/Pl+1PlO:%>i2koyâhk	   VERB_ENDLEX ; ! FROM NS 152
+4Sg/Pl+12PlO:%>i2koyahk	   VERB_ENDLEX ;
+4Sg/Pl+2PlO:%>i2koyêk	   VERB_ENDLEX ;

!!!!!!!! Third/Fourth/Fifth Persons !!!!!!!!

+4Sg/Pl+3PlO:%>i2kocik	   VERB_ENDLEX ; ! FROM NS 152

!           ********
! **** Unspecified Actor ****
!           ********  NOTE: always mixed

+X+1PlO:%>i2kawiyâhk	   VERB_ENDLEX ;
+X+12PlO:%>i2kawiyahk	   VERB_ENDLEX ;
+X+2PlO:%>i2kawiyêk	   VERB_ENDLEX ;

+X+3PlO:%>i2hcik	   VERB_ENDLEX ;


! ===================================================================================================================
! ===================================================================================================================
! ===================================================================================================================
! ===================================================================================================================


! ********************
!!!! Imperative !!!!
! ********************

! ==========================
! ==========================
!!!!! Regular verbs !!!!!
! ==========================
! ==========================

LEXICON VTA_IMP_NULL
VTA_IMP_NULL_SG_SUFFIX ;
VTA_IMP_NULL_PL_SUFFIX ;

! ~~~~~~~~~~~~~~~~~~~~~~
!!!!!! SINGULAR !!!!!! 
! ~~~~~~~~~~~~~~~~~~~~~~

LEXICON VTA_IMP_NULL_SG_SUFFIX

!!!!!!!!!!!!!!!!!!!!
!!!!!! Direct !!!!!!
!!!!!!!!!!!!!!!!!!!!


!!!!!!! Local !!!!!!!

+Imm+2Sg+1SgO:%>in VERB_ENDLEX ;

+Del+2Sg+1SgO:%>ii2hkan VERB_ENDLEX ;


!!!!!!! Mixed !!!!!!!

+Imm+2Sg+3SgO:0 VERB_ENDLEX ;

+Del+2Sg+3SgO:%>âhkan VERB_ENDLEX ;


LEXICON VTA_IMP_NULL_PL_SUFFIX

!!!!!!!!!!!!!!!!!!!!
!!!!!! Direct !!!!!!
!!!!!!!!!!!!!!!!!!!!


!!!!!!! Local !!!!!!!

+Imm+2Sg+1PlO:%>inân VERB_ENDLEX ;
+Imm+2Pl+1PlO:%>inân VERB_ENDLEX ;
+Imm+2Pl+1SgO:%>ik VERB_ENDLEX ;

+Del+2Sg+1PlO:%>ii2hkâhk VERB_ENDLEX ;
+Del+2Pl+1PlO:%>ii2hkâhk VERB_ENDLEX ;
+Del+2Pl+1SgO:%>ii2hkêk VERB_ENDLEX ;


!!!!!!! Mixed !!!!!!!

+Imm+2Sg+3PlO:%>ik VERB_ENDLEX ;

+Imm+12Pl+3SgO:%>âtân VERB_ENDLEX ;
+Imm+12Pl+3PlO:%>âtânik VERB_ENDLEX ;

+Imm+2Pl+3SgO:%>ihk VERB_ENDLEX ;
+Imm+2Pl+3PlO:%>ihkok VERB_ENDLEX ;

+Del+2Sg+3PlO:%>âhkanik VERB_ENDLEX ;

+Del+12Pl+3SgO:%>âhkahk VERB_ENDLEX ;
+Del+12Pl+3PlO:%>âhkahkok VERB_ENDLEX ;
 
+Del+2Pl+3SgO:%>âhkêk VERB_ENDLEX ;
+Del+2Pl+3PlO:%>âhkêkok VERB_ENDLEX ;

! =================================
! =================================
!!!!! irregular stems !!!!!
! =================================
! =================================

!!!! VTIt
LEXICON VTAt_IMP_NULL
VTAt_IMP_NULL_SG_SUFFIX ;
VTAt_IMP_NULL_PL_SUFFIX ;

! ~~~~~~~~~~~~~~~~~~~~~~
!!!!!! SINGULAR !!!!!! 
! ~~~~~~~~~~~~~~~~~~~~~~

LEXICON VTAt_IMP_NULL_SG_SUFFIX

!!!!!!!!!!!!!!!!!!!!
!!!!!! Direct !!!!!!
!!!!!!!!!!!!!!!!!!!!


!!!!!!! Local !!!!!!!

+Imm+2Sg+1SgO:%>in VERB_ENDLEX ;

+Del+2Sg+1SgO:%>ii2hkan VERB_ENDLEX ;


!!!!!!! Mixed !!!!!!!

+Imm+2Sg+3SgO:0 VERB_ENDLEX ;

+Del+2Sg+3SgO:%>âhkan VERB_ENDLEX ;


LEXICON VTAt_IMP_NULL_PL_SUFFIX

!!!!!!!!!!!!!!!!!!!!
!!!!!! Direct !!!!!!
!!!!!!!!!!!!!!!!!!!!


!!!!!!! Local !!!!!!!

+Imm+2Sg+1PlO:%>inân VERB_ENDLEX ;
+Imm+2Pl+1SgO:%>ik VERB_ENDLEX ;
+Imm+2Pl+1PlO:%>inân VERB_ENDLEX ;


+Del+2Sg+1PlO:%>ii2hkâhk VERB_ENDLEX ;
+Del+2Pl+1PlO:%>ii2hkâhk VERB_ENDLEX ;
+Del+2Pl+1SgO:%>ii2hkêk VERB_ENDLEX ;


!!!!!!! Mixed !!!!!!!

+Imm+2Sg+3PlO:%>ik VERB_ENDLEX ;

+Imm+12Pl+3SgO:%>âtân VERB_ENDLEX ;
+Imm+12Pl+3PlO:%>âtânik VERB_ENDLEX ;

+Imm+2Pl+3SgO:%>i2hk VERB_ENDLEX ;
+Imm+2Pl+3PlO:%>i2hkok VERB_ENDLEX ;

+Del+2Sg+3PlO:%>âhkanik VERB_ENDLEX ;

+Del+12Pl+3SgO:%>âhkahk VERB_ENDLEX ;
+Del+12Pl+3PlO:%>âhkahkok VERB_ENDLEX ;
 
+Del+2Pl+3SgO:%>âhkêk VERB_ENDLEX ;
+Del+2Pl+3PlO:%>âhkêkok VERB_ENDLEX ;


!!!! VTIi

LEXICON VTAi_IMP_NULL
VTAi_IMP_NULL_SG_SUFFIX ;
VTAi_IMP_NULL_PL_SUFFIX ;

! ~~~~~~~~~~~~~~~~~~~~~~
!!!!!! SINGULAR !!!!!! 
! ~~~~~~~~~~~~~~~~~~~~~~

LEXICON VTAi_IMP_NULL_SG_SUFFIX

!!!!!!!!!!!!!!!!!!!!
!!!!!! Direct !!!!!!
!!!!!!!!!!!!!!!!!!!!


!!!!!!! Local !!!!!!!

+Imm+2Sg+1SgO:%>in VERB_ENDLEX ;

+Del+2Sg+1SgO:%>ii2hkan VERB_ENDLEX ;


!!!!!!! Mixed !!!!!!!

+Imm+2Sg+3SgO:%>i VERB_ENDLEX ;

+Del+2Sg+3SgO:%>âhkan VERB_ENDLEX ;


LEXICON VTAi_IMP_NULL_PL_SUFFIX

!!!!!!!!!!!!!!!!!!!!
!!!!!! Direct !!!!!!
!!!!!!!!!!!!!!!!!!!!


!!!!!!! Local !!!!!!!

+Imm+2Sg+1PlO:%>inân VERB_ENDLEX ;
+Imm+2Pl+1SgO:%>ik VERB_ENDLEX ;
+Imm+2Pl+1PlO:%>inân VERB_ENDLEX ;


+Del+2Sg+1PlO:%>ii2hkâhk VERB_ENDLEX ;
+Del+2Pl+1PlO:%>ii2hkâhk VERB_ENDLEX ;
+Del+2Pl+1SgO:%>ii2hkêk VERB_ENDLEX ;


!!!!!!! Mixed !!!!!!!

+Imm+2Sg+3PlO:%>ik VERB_ENDLEX ;

+Imm+12Pl+3SgO:%>âtân VERB_ENDLEX ;
+Imm+12Pl+3PlO:%>âtânik VERB_ENDLEX ;

+Imm+2Pl+3SgO:%>i2hk VERB_ENDLEX ;
+Imm+2Pl+3PlO:%>i2hkok VERB_ENDLEX ;

+Del+2Sg+3PlO:%>âhkanik VERB_ENDLEX ;

+Del+12Pl+3SgO:%>âhkahk VERB_ENDLEX ;
+Del+12Pl+3PlO:%>âhkahkok VERB_ENDLEX ;
 
+Del+2Pl+3SgO:%>âhkêk VERB_ENDLEX ;
+Del+2Pl+3PlO:%>âhkêkok VERB_ENDLEX ;



! ===================================================================================================================
! ===================================================================================================================
! ===================================================================================================================
! ===================================================================================================================


! ****************************
!!!! FUTURE CONDITIONAL !!!!
! ****************************




LEXICON VTA_FUT_CON_NULL
VTA_FUT_CON_NULL_SG_SUFFIX ;
VTA_FUT_CON_NULL_PL_SUFFIX ;
! ~~~~~~~~~~~~~~~~~~~~~~
!!!!!! SINGULAR !!!!!! 
! ~~~~~~~~~~~~~~~~~~~~~~

LEXICON VTA_FUT_CON_NULL_SG_SUFFIX

!!!!!!!!!!!!!!!!!!!!
!!!!!! Direct !!!!!!
!!!!!!!!!!!!!!!!!!!!


!!!!!!! Local !!!!!!!

+2Sg+1SgO:%>iyani       VERB_ENDLEX ;

!!!!!!! Mixed !!!!!!!

+1Sg+3SgO:%>aki	   VERB_ENDLEX ;
+2Sg+3SgO:%>aci	   VERB_ENDLEX ;

+1Sg+4Sg/PlO:%>i2maki	   VERB_ENDLEX ;
+2Sg+4Sg/PlO:%>i2maci	   VERB_ENDLEX ;

!!!!!!!! Third/Fourth/Fifth Persons !!!!!!!!

+3Sg+4Sg/PlO:%>âci	   VERB_ENDLEX ;

+4Sg/Pl+5Sg/PlO:%>âyici      VERB_ENDLEX ; ! From NS 152

+3Sg+5Sg/PlO:%>i2mâci      VERB_ENDLEX ; ! From NS 152


!!!!!!!!!!!!!!!!!!!!!
!!!!!! Inverse !!!!!!
!!!!!!!!!!!!!!!!!!!!!

!!!!!!! Local !!!!!!!

+1Sg+2SgO:%>i2tâni	   VERB_ENDLEX ;


!!!!!!! Mixed !!!!!!!

+3Sg+1SgO:%>ici		   VERB_ENDLEX ;
+3Sg+2SgO:%>i2ski		   VERB_ENDLEX ;

+4Sg/Pl+1SgO:%>i2mici	   VERB_ENDLEX ;
+4Sg/Pl+2SgO:%>i2miski	   VERB_ENDLEX ;

!!!!!!!! Third/Fourth/Fifth Persons !!!!!!!!

+4Sg/Pl+3SgO:%>i2koci		   VERB_ENDLEX ;
+5Sg/Pl+4Sg/PlO:%>i2koyici		   VERB_ENDLEX ;


! ~~~~~~~~~~~~~~~~~~~~
! ~~~~~~~~~~~~~~~~~~~~
!!!!!! PLURAL !!!!!! 
! ~~~~~~~~~~~~~~~~~~~~ 
! ~~~~~~~~~~~~~~~~~~~~

LEXICON VTA_FUT_CON_NULL_PL_SUFFIX

!!!!!!!!!!!!!!!!!!!!
!!!!!! Direct !!!!!!
!!!!!!!!!!!!!!!!!!!!


!!!!!!! Local !!!!!!!

+2Sg+1PlO:%>iyâhki       VERB_ENDLEX ;

+2Pl+1PlO:%>iyâhki       VERB_ENDLEX ;
+2Pl+1SgO:%>iyêko       VERB_ENDLEX ;

!!!!!!! Mixed !!!!!!!

+1Sg+3PlO:%>akwâwi	   VERB_ENDLEX ;

+2Sg+3PlO:%>atwâwi	   VERB_ENDLEX ;

+1Pl+3SgO:%>âyâhki	   VERB_ENDLEX ;
+1Pl+3PlO:%>âyâhkwâwi	   VERB_ENDLEX ;

+12Pl+3SgO:%>âyahko	   VERB_ENDLEX ;
+12Pl+3SgO:%>âyahki	   VERB_ENDLEX ; ! Variant in Alberta
+12Pl+3PlO:%>âyahkwâwi   VERB_ENDLEX ;

+2Pl+3SgO:%>âyêko	   VERB_ENDLEX ;
+2Pl+3PlO:%>âyêkwâwi	   VERB_ENDLEX ;

+1Pl+4Sg/PlO:%>i2mâyâhki	   VERB_ENDLEX ;
+12Pl+4Sg/PlO:%>i2mâyahko	   VERB_ENDLEX ;
+12Pl+4Sg/PlO:%>i2mâyahki	   VERB_ENDLEX ;
+2Pl+4Sg/PlO:%>i2mâyêko	   VERB_ENDLEX ;


+X+1SgO:%>i2kawiyâni    VERB_ENDLEX ;
+X+2SgO:%>i2kawiyani    VERB_ENDLEX ;
+X+1PlO:%>i2kawiyâhki   VERB_ENDLEX ;
+X+12PlO:%>i2kawiyahki   VERB_ENDLEX ;
! +X+12PlO:%>i2kawiyahko   # ;
+X+2PlO:%>i2kawiyêko   VERB_ENDLEX ;

+X+3SgO:%>i2hci   VERB_ENDLEX ;
+X+3PlO:%>i2htwâwi  VERB_ENDLEX ;
+X+4Sg/PlO:%>i2mihci  VERB_ENDLEX ;

!!!!!!!! Third/Fourth/Fifth Persons !!!!!!!!

+3Pl+4Sg/PlO:%>âtwâwi     VERB_ENDLEX ; ! From NS 152

+3Pl+5Sg/PlO:%>i2mâtwâwi      VERB_ENDLEX ; ! From NS 152


!!!!!!!!!!!!!!!!!!!!!
!!!!!! Inverse !!!!!!
!!!!!!!!!!!!!!!!!!!!!

!!!!!!! Local !!!!!!!

+1Pl+2SgO:%>i2tâhki VERB_ENDLEX ;

+1Pl+2PlO:%>i2tâhki	   VERB_ENDLEX ;
+1Sg+2PlO:%>i2tako	   VERB_ENDLEX ;

!!!!!!! Mixed !!!!!!!

+3Pl+1SgO:%>itwâwi	   VERB_ENDLEX ;

+3Pl+2SgO:%>i2skwâwi		   VERB_ENDLEX ;

+3Sg+1PlO:%>i2koyâhki		   VERB_ENDLEX ;
+3Sg+12PlO:%>i2koyahki		   VERB_ENDLEX ;
+3Pl+1PlO:%>i2koyâhkwâwi		   VERB_ENDLEX ;

+3Sg+12PlO:%>i2koyahko 		   VERB_ENDLEX ;
+3Pl+12PlO:%>i2koyahkwâwi		   VERB_ENDLEX ;

+3Sg+2PlO:%>i2koyêko		   VERB_ENDLEX ; 
+3Pl+2PlO:%>i2koyêkwâwi		   VERB_ENDLEX ;

+4Sg/Pl+1PlO:%>i2koyâhki	   VERB_ENDLEX ;
+4Sg/Pl+12PlO:%>i2koyahko	   VERB_ENDLEX ;
+4Sg/Pl+12PlO:%>i2koyahki	   VERB_ENDLEX ;
+4Sg/Pl+2PlO:%>i2koyêko	   VERB_ENDLEX ;


!!!!!!!! Third/Fourth/Fifth Persons !!!!!!!!

+4Sg/Pl+3PlO:%>i2kotwâwi		   VERB_ENDLEX ;

LEXICON VERB_ENDLEX
@D.cnj.CC@@R.joiner.NULL@+Err/Orth:@D.cnj.CC@@R.joiner.NULL@ # ;
@D.cnj.CC@@D.joiner.NULL@ # ;
! Adding changed conjunct trigger: IC
@R.cnj.CC@@R.joiner.NULL@+Err/Orth:@R.cnj.CC@@R.joiner.NULL@%^IC # ;
@R.cnj.CC@@D.joiner.NULL@:@R.cnj.CC@@D.joiner.NULL@%^IC # ;

!! End of verb affixes code
