! Phonology 

define dimRule [ t -> c || _ ?* %/  [ i s ( i s ) | s i ] [ %/ | .#. ] ] ;

define ên2ânRule [ ê -> â || %/ _ %/ n %/ ] ; 

! VG-i > V̅

define aGiRule [ [ a | â ] [ w | y | ý ] %/ i -> â ] ;

define eGiRule [ ê [ w | y | ý ] %/ i -> ê ] ;

define iGiRule [ [ i | î ] [ w | y | ý ] %/ [ i | î ] -> î ] ;

define oGiRule [ [ o | ô ] [ w | y | ý ] %/ i -> ô ] ;

! Same vowel collapses rules

define aCollapseRule [ [ a | â ] %/ [ a | â ] -> â ] ;

define eCollapseRule [ ê %/ ê -> ê ] ;

define iCollapseRule [ [ i | î ] %/ [ i | î ] -> î ] ;

define oCollapseRule [ [ o | ô ] %/ [ o | ô ] -> ô ] ;

! Morpheme-final w-

! Cw-o > C{o,ô}

define CwC2oRule [ w -> o || [ c | h | k | m | n | p | s | t ] _ %/ [ c | h | k | m | n | p | s | t ] ] ; 

define Cw2ooRule [ w %/ ô  -> ô || [ c | h | k | m | n | p | s | t ] _ ] ;

define wi2oRule [ w %/ i -> %/ o ] ;

define Cwo2ooRule [ w %/ [ o | ô ] -> ô || [ c | h | k | m | n | p | s | t ] _ ] ;

! Cw-ô > Cô

! define Cwoo2oo2Rule [ w %/ ô -> ô || [ c | h | k | m | n | p | s | t ] _ ] ;

! Cw-wV > CowV OR CwV

define wwRule [ w %/ w -> w || [ c | h | k | m | n | p | s | t ] _ [ a | â | ê | i | î | o | ô ] ] ;

! {m,n} > h / __ -{p,t,k} OR Ø > i / {m,n} __ -{p,t,k,c} 

define N2hRule [ [ m | n ] %/ -> h || _ [ p | t | k | c ] ] ;

! T > s / _ i, i:
! t > c / _ i, i:
! T, t > t / _ e (> i), elsewhere

define TsRule [ T -> s || _ %/ [ i | î ] ] ;

define tcRule [ t -> c || _ %/ [ i | î ] ] ;

define tc2Rule [ t (->) c || _ %/ [ i | î ] ] ;

define TtRule [ T -> t ] ;

define e2iRule [ e -> i || %/ _ ] ;

define iEpenthesisRule [ %/ -> %/ i || [ c | h | k | m | n | p | s | t  | w | y | ý ]  _ [ c | h | k | m | n | p | s | t |  w | y | ý ] ] ;

define boundaryRule [ %/ -> 0 ] ;

read regex [ dimRule
.o. ên2ânRule
.o. TsRule
.o. tcRule
.o. TtRule
.o. e2iRule
.o. iEpenthesisRule
.o. aGiRule
.o. eGiRule
.o. iGiRule
.o. oGiRule
.o. aCollapseRule
.o. eCollapseRule
.o. iCollapseRule
.o. oCollapseRule
.o. CwC2oRule
.o. Cw2ooRule
.o. wi2oRule
.o. Cwo2ooRule
.o. wwRule 
.o. N2hRule
.o. tc2Rule
.o. boundaryRule
] ;

! define Phonology
! invert net
! save stack crk-phon.fst

