Alphabet
 a b c d e f g h i j k l m n o p q r s t u v w x y z
 A B C D E F G H I J K L M N O P Q R S T U V W X Y Z 
 â ê î ô
 Â Ê Î Ô
 
 t2:0
 %>:0 
 %^LC:0 ! Locative suffix alternation trigger
 %^DIM:0 ! Diminutive alternation trigger
 %^EGLOT:0 ! Conjunctive êh trigger
 ;

Sets

 Vow = a e i o u â ê î ô
       A E I O U Â Ê Î Ô ;

 LongVow = â ê î ô Â Ê Î Ô ;
 
 Cns = b c d f g h j k l m n p q r s t v w x z ;
 
 Nas = m n ;
 NonNas = b c d f g h j k l p q r s t v w x z ;

 Dummy = %> ;


!! !!!Rules

Rules

"h glottal stop for initial vowel stems in Conjunctive"
%^EGLOT:h <=> _ :Vow ;

"Suffix vowel deletion in vowel final stems"
!! __@RULENAME@__ deletes stem vowel in vowel final stems
Vx:0 <=> _ %>:0 Vow ;
	where Vx in ( a e i o â ê î ô ) ; 

"i deletion when ii in some special cases"
!! __@RULENAME@__ deletes stem vowel in vowel final stems !
i:0 <=> _ %>:0 %>:0 i ;


"w Insertion in Front of Vowel Initial Suffix after ksv c"
!! __@RULENAME@__ inserts w after k in front of plural -ak
!! Note that this rule probably is wrong, and that the -w- is
!! a morphological affix rather than a phonological variant.
0:w <=> [ k: | o: | ô: ] %>:0 _ a k ; ! sikâk:sikâkwak ôhô:ôhôwak cahcahkayo:cahcahkayowak
         h: p: %>:0 _ a k ; ! sihkihp:sihkihpwak
 
 
!"Stem Final n" 
! __@RULENAME@__  This rule is applying in places where it should not. The usefulness of this rule needs to be rethought before it is re-isntated
!n:0 <=> _ %>:0 Cns ; 


"locative alternations o"
!! __@RULENAME@__ 
i:o <=> k %>:0 _  %^LC:0   ;

! first
"locative alternations aaw 1"
!! __@RULENAME@__ (first)
Vx:0 <=> _ [ w: | y: ] %>:0  i: %^LC:0   ;
    where Vx in ( i a o ) ;
    
! second
"locative alternations aaw 2"
!! __@RULENAME@__ (second)
Cx:0 <=> [ a: | i: | o: ] _  %>:0  i: %^LC:0  ;
    where Cx in ( w y ) ;

! third
"locative alternations aaw 3a"
!! __@RULENAME@__ (third)
i:â <=> [ ô | ê | a: ] [ w: | y: ]  %>:0  _ %^LC:0  ;

"locative alternation iihk 3i"
!! __@RULENAME@__ 
i:î <=> i: y:  %>:0  _ %^LC:0  ;

"locative alternation oohk io"
!! __@RULENAME@__ 
i:ô <=> o: y:  %>:0  _ %^LC:0  ;          
                                    !change the suffix initial i to a long o 
									!e.g. mîcimâpoy > mîcimâpoyôhk
                                    !then delete y in second
                                    !then delete o in first
                                    
"locative alternation naahk 1"		
!! __@RULENAME@__ adds nâhk after êw 1: change w to n
w:n <=> [ê|ô] _  %>:0 i: %^LC:0  ;

!"Double i-vowel with locatives"
!!i:0 <=> Vow %> _ %^LC:0  ;


"possessive prefixes"
!! __@RULENAME@__ 
t2:t <=> _ %>:0 Vow ;


"i to a in prefixes in front of nasals"
!! __@RULENAME@__ 
i:a <=> .#. n _ %>:0 [ n | m ] i	;


"Double consonant deletion"
!! __@RULENAME@__ 
Cx:0 <=> _ %>:0 Cx ;

	where Cx in Cns ;

!"Double vowel deletion" "Suffix vowel deletion in vowel final stems"
!!! __@RULENAME@__ 
!Vx:0 <=> _ %>:0 Vx ;
!	where Vx in Vow ;


!! !!Rules for generating the correct surface forms of -isis the diminutive suffix

"Diminutives - all stem t change to c with diminutives" 
!! __@RULENAME@__ Okimasis 2004: 13, Rule 2.
t:c <=> [ Vow | %>:0 ]  _ Vow :* %^DIM:0 ;  ! we'll see if there are exceptions


"In Vow+y final stems with Diminutives, part 1: delete y" 
!! __@RULENAME@__ Okimasis 2004: 13, Rule 3. Vw final stems lose the w, shorth vowels are legthened
y:0 <=> Vow _ %>:0 i: s: %^DIM:0 i: s: ;

"In Vow+w final stems with Diminutives, part 1: delete y" 
!! __@RULENAME@__ Okimasis 2004: 13, Rule 3. Vw final stems lose the w, shorth voewels are legthened
w:0 <=> Vow _ %>:0 i: s: %^DIM:0 i: s: ;

"In Vow+y final stems with Diminutives, part 2: lengthen stem final vowel" 
!! __@RULENAME@__ Okimasis 2004: 13, Rule 3. Vw final stems lose the w, shorth voewels are legthened
[ o:ô | i:î | a:â ] <=> _ [ y: | w: ] %>:0 i: s: %^DIM:0 i: s: ;

"In Vow+y final stems with Diminutives, part 3: delete i in suffix" 
!! __@RULENAME@__ Okimasis 2004: 13, Rule 3. Vw final stems lose the w, shorth voewels are legthened
i:0 <=> [ o: | a: | e: | ê: | :î ] [ y:| w: ] %>:0 _ s: %^DIM:0 i: s: ;

!"Vowel lengthening in iw final nouns with diminutives" !Is covered by part 2
!i:î <=> _ [ y:| w: ] %>:0 i: s: %^DIM:0 i: s: ;


"Diminutives rule change ending to os with k-final stems 1" 
!! __@RULENAME@__ Okimasis 2004: 14, Examples listed in 5.
s:0 <=> k %>:0 _ %^DIM:0 ;

"Diminutives rule change ending to os with k-final stems 2" 
!! __@RULENAME@__ Okimasis 2004: 14, Examples listed in 5.
i:o <=> k %>:0 _ s: %^DIM:0 ;

"Diminutives rule change ending delete i with nouns ending in kwa"
i:0 <=> k: w: a: %>:0 _ s: %^DIM:0 ;













