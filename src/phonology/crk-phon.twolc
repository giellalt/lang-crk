Alphabet
 a b c d e f g h i j k l m n o p q r s t u v w x y z
 A B C D E F G H I J K L M N O P Q R S T U V W X Y Z 
 â ê î ô
 Â Ê Î Ô
 
 %>:0 
 %^LC:0 ! Locative suffix alternation trigger
 
@U.verb.1sgindep@ 
@U.verb.2sgindep@ 
@U.verb.3sgindep@ 
@U.verb.3osgindep@ 
@U.verb.3isgindep@ 
@U.verb.1plindep@ 
@U.verb.12plindep@ 
@U.verb.2plindep@ 
@U.verb.3plindep@ 

@U.verb.1sgsbj@ 
@U.verb.2sgsbj@ 
@U.verb.3sgsbj@ 
@U.verb.3osgsbj@ 
@U.verb.3isgsbj@ 
@U.verb.1plsbj@ 
@U.verb.12plsbj@ 
@U.verb.2plsbj@ 
@U.verb.3plsbj@ 

@U.verb.2sgimp@
@U.verb.1plimp@
@U.verb.2plimp@

 ;

Sets

 Vow = a e i o u â ê î ô 
       A E I O U Â Ê Î Ô ;

 LongVow = â ê î ô Â Ê Î Ô ;
 
 Cns = b c d f g h j k l m n p q r s t v w x z ;
 
 Nas = m n ;
 NonNas = b c d f g h j k l p q r s t v w x z ;

 Dummy = @U.verb.1sgindep@ 
         @U.verb.2sgindep@ 
         @U.verb.3sgindep@ 
         @U.verb.3osgindep@ 
         @U.verb.3isgindep@ 
         @U.verb.1plindep@ 
         @U.verb.12plindep@ 
         @U.verb.2plindep@ 
         @U.verb.3plindep@ 
         
         @U.verb.1sgsbj@ 
         @U.verb.2sgsbj@ 
         @U.verb.3sgsbj@ 
         @U.verb.3osgsbj@ 
         @U.verb.3isgsbj@ 
         @U.verb.1plsbj@ 
         @U.verb.12plsbj@ 
         @U.verb.2plsbj@ 
         @U.verb.3plsbj@ 
         
         @U.verb.2sgimp@
         @U.verb.1plimp@
         @U.verb.2plimp@

@U.noun.1sg@
@U.noun.2sg@
@U.noun.3sg@
@U.noun.1pl@
@U.noun.12pl@
@U.noun.2pl@
@U.noun.3pl@

@U.noun.abs@ 

;


!! !!!Rules

Rules

"Suffix a Deletion in Vowel Final Stems"
!! __RULENAME__ gives maskwa + ak = plural maskwak
 a:0 <=> Vow %>: _ Cns ;

"w Insertion in Front of Vowel Initial Suffix after k and m"
!! __RULENAME__ inserts w after k, m in front of plural -ak
!! Note that this rule probably is wrong, and that the -w- is
!! a morphological affix rather than a phonological variant.
 0:w <=> [ k | m ] %>: _ a k ;
 
!€ wâpask0>ak
!€ wâpaskw0ak 
 
 

"Suffix Final n" 
!! __RULENAME__  
 n:0 <=> _ (Dummy:) %>:0 Cns ;

"locative alternations o"
i:o <=> k _  (Dummy:*) %^LC: ;


! first
"locative alternations aaw 1"
Vx:0 <=> _ [ w: | y: ] (Dummy:*) i: %^LC: ;
    where Vx in ( i a o ) ;
    
! second
"locative alternations aaw 2"
Cx:0 <=> [a:|i:|o:] _  (Dummy:*) i: %^LC: ;
    where Cx in ( w y ) ;

! third
"locative alternations aaw 3a"
i:â <=> [ô|ê |a:] [ w: | y: ]  (Dummy:*) _ %^LC: ;
!	<=> :n _ ^LC: ;

"locative alternation iihk 3i"
i:î <=> i: y:  (Dummy:*) _ %^LC: ;

"locative alternation oohk io"
i:ô <=> o: y:  (Dummy:*) _ %^LC: ;          !change the suffix initial i to a long o 
									!e.g. mîcimâpoy > mîcimâpoyôhk
                                    !then delete y in second
                                    !then delete o in first
                                    
"locative alternation naahk 1"		!adds nâhk after êw 1: change w to n
w:n <=> [ê|ô] _  (Dummy:*) i: %^LC: ;

  
 