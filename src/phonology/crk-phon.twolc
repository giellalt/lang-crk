Alphabet
 a b c d e f g h i j k l m n o p q r s t u v w x y z
 A B C D E F G H I J K L M N O P Q R S T U V W X Y Z 
 â ê î ô
 Â Ê Î Ô
 
 t2:0      !!= * @CODE@ t between prefix and vowel-initial stem
 y2:y      !!= * @CODE@ t between prefix and vowel-initial stem
 i2:0      !!= * @CODE@ i can change to o and ô in the possession element
 %>:0      !!= * @CODE@ affix boundary
 %^LC:0    !!= * @CODE@ Locative suffix alternation trigger
 %^DIM:0   !!= * @CODE@ Diminutive alternation trigger
 %^EGLOT:0 !!= * @CODE@ Conjunctive êh trigger
 %^POS:0   !!= * @CODE@ Possessive alternation trigger
 %^EA:0    !!= * @CODE@ ê to â in 1, 2 person of ê verbs

 ;

Sets

 Vow = a e i o u â ê î ô
       A E I O U Â Ê Î Ô ;

 LongVow = â ê î ô Â Ê Î Ô ;
 
 Cns = b c d f g h j k l m n p q r s t v w x z  ;
 
 Nas = m n ;
 NonNas = b c d f g h j k l p q r s t v w x z  ;

 Dummy = %> %^LC %^DIM %^EGLOT %^POS %^EA ;
 
Definitions 

 Affb = :* %>:0 :* ; 
 Pos = :* %^POS:0 :* ;
 Dim = :* %^DIM:0 :* ;
 Loc = :* %^LOC:0 :* ;
 PosDim = :* [ %^POS:0 | %^DIM:0 ]  :* ;
 Plur = %>:0 a .#. ;

!! !!!Rules

Rules

"t:c in diminutives" 
!! __@RULENAME@__ Okimasis 2004: 13, Rule 2.
t:c <=>  _ :*  %>:0 Dim .#. ; ! cehcapiwinisis


"t2:t between prefix and vowel"
!! __@RULENAME@__ 
t2:t  <=> _ %>:0 Vow ;

"Diminutives - t2 changes to c" 
!! !! __@RULENAME@__
t2:c <=>  _ %>:0 Vow Dim .#. ; 

"i:o alternations" 
!! __@RULENAME@__  
i:o <=> Cns w: %>:0 ( %^POS:0 ) ( %>:0 ) _  ; !  

"Suffix vowel deletion in vowel final stems" 
!! __@RULENAME@__ : niska, wâwi+a wâwi+ihk wâwi+is, oskâyi+a
Vx:0 <=> Cns _ %>:0 ( %^POS:0 ) ( %>:0 ) [ Vow | i: ] ;
         y: _   %>:0 [ a | i: ]   ;
	where Vx in ( a e o â ê î ô i ) ;  ! esa:esihk, esa:esak, esa:esis



"w:0 alternations" 
!! __@RULENAME@__  
w:0 <=> \Pos Cns _ %>:0 ( %^POS:0 ) ( %>:0 )  i:  ; !  askihkw:askihkos nitihkom
        \Pos Cns _ ( %>:0 ) ( %^POS:0 )  .#. ;



"i:ii- alternations"
!! __@RULENAME@__ , Okimasis p. 13 pikiw : pikîs
i:î <=> Cns _ [ w: | y: ] %>:0  i: ( s ) ( m ) [ %^DIM:0 | %^LC:0 | %^POS:0 ] ;
	    Cns _ [ w: | y: ] %>:0 %^POS:0 ( %>:0 )  i: ;

"o:oo- alternations"
!! __@RULENAME@__ , Okimasis p. 135 
!! okimâw:nitôkimâm, nitohtôsâpom tohtôsâpoy tohtôsâpôhk, cohcôsâpôs, cahcahkâyôhk, cahcahkâyôs
o:ô <=> t2: %>:0 _  [ k | s ] :* i: m  ; ! okimâw:nitôkimâm, oskâyi ! careful: only in front of k and s first
 		 %>:0 t _  h :* i: m  ; ! nitohtôsâpom tohtôsâpoy
 		\Pos _ [ w: | y: ] %>:0 i: ( s ) ( m ) [ %^DIM:0 | %^LC:0 | %^POS:0 ] ; ! tohtôsâpôhk, cohcôsâpôs, cahcahkâyôhk, cahcahkâyôs


"a:aa alternations" 
!! __@RULENAME@__  
a:â <=>  _ y: %>:0  :* i: [ n: | w: | y: | s: | %^LC:0 ]  ;  ! 


"y:0 alternations" 
!! __@RULENAME@__  
y:0 <=> \Pos [ i: | o: | a: ]  _ %>:0   :*  i: [ n: | w: | y: | s: | %^LC:0 ]  ; 
!        [ i: | o: ]  _ ( %>:0 )  ( %^POS:0 )  i:  %>:0 %^LC:0 h: ; 


"i:0 after w/y "
!! __@RULENAME@__ (first) : Okimasis 2004: 26, Rule 2, 8: Vow + (w/y) + ihk 
!! cahcahkâyow+ihk, tohtôsâpoy+ihk, cahcahkâyow+is, cohcôsâpoy+is, o+pikiw+iyiwa	 maniway+i
i:0 <=>  \PosDim [ o: | i: ]  [ w: | y: ] %>:0  _ %^LC:0 ;   ! cahcahkâyow+ihk, tohtôsâpoy+ihk
         [ Vow: | o: ] [ w: | y: ] %>:0  _ s %^DIM:0 ; ! cahcahkâyow+is, cohcôsâpoy+is
         \Pos [ a: | i: | â: ] [ w: | y: ] %>:0 ( %^POS:0 ) ( %>:0 ) _  ; !o+pikiw+iyiwa	 maniway+i


!! !! VERBS

"h glottal stop for initial vowel stems in verbs: Conjunctive"
!! __@RULENAME@__
%^EGLOT:h <=>  _  %>:0 Vow: ;


"e and ee to aa rule in first and second person"
!! __@RULENAME@__
Vx:â <=> _ %>:0 %^EA:0 ;
	where Vx in (ê e) ;

"a to ee rule in first and second person"
!! __@RULENAME@__
a:ê <=> _ %>:0 %^EA:0 ; ! wâpahta:niwâpahtên


!"ee to aa rule in first and second person"
!!! __@RULENAME@__
!ê:â <=> _ %>:0 %^EA:0 ;

"n:0 before k-suffix"
!! __@RULENAME@__
n:0 <=> Vow _ %>:0 ( h ) k ; !pimisin+hk
	









