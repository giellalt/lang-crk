Alphabet
!! !!!Alphabet

 a b c d e f g h i j k l m n o p q r s t u v w x y z
 A B C D E F G H I J K L M N O P Q R S T U V W X Y Z 
 â ê î ô
 Â Ê Î Ô
   %-

 t2:0      !!= * @CODE@ t between prefix and vowel-initial stem
 t3:t      !!= * @CODE@ t to s in VTA-4
 w2:w      !!= * @CODE@ for keeping the final -w in mow
 %>:0      !!= * @CODE@ suffix boundary
 %<:0      !!= * @CODE@ prefix boundary
           ! To discuss:
           ! * affix boundaries should be visible, i.e. %>
           ! * do we want to differentiate between prefix boundaries %<
           !   and suffix boundaries %> ? And the same for derivation boundaries
           !   « and »?
 %^LOC:0   !!= * @CODE@ Locative suffix alternation trigger
 %^DIM:0   !!= * @CODE@ Diminutive alternation trigger
 %^EGLOT:0 !!= * @CODE@ Conjunctive êh trigger
 %^POS:0   !!= * @CODE@ Possessive alternation trigger
 %^EA:0    !!= * @CODE@ ê to â in 1, 2 person of ê verbs
 %^TS:0    !!= * @CODE@  t to s in VTA-4 verbs before some i-init suf
 %^NII:0   !!= * @CODE@  n to h or 0 in VII verbs when before k-init suf
 %^WI:0     !!= * @CODE@  w to 0 in vta-2 verbs when before k-init suf
 %^IO:0   !!= * @CODE@  VTACw w + i becom o
 ;

Sets

 Vow = a e i o u y â ê î ô
       A E I O U Y Â Ê Î Ô ;

 LongVow = â ê î ô Â Ê Î Ô ;
 
 Cns = b c d f g h j k l m n p q r s t v w x z  ;
  Lts = a e i o u â ê î ô b c d f g h j k l m n p q r s t v w x y z %- %> %^LOC %^EA ;

 Nas = m n ;
 NonNas = b c d f g h j k l p q r s t v w x z  ;

 Dummy = %> %^LOC %^DIM %^EGLOT %^POS %^EA %^TS %^NII ;
 
Definitions 

 Affb = :* %>:0 :* ; 
 Pos = :* %^POS:0 :* ;
 Dim = :* %^DIM:0 :* ;
 Loc = :* %^LOC:0 :* ;
 PosDim = :* [ %^POS:0 | %^DIM:0 ]  :* ;
 Plur = %>:0 a .#. ;

!! !!!Rules

Rules

"o:w in possessive prefix"
!! __@RULENAME@__ , w- in front of î, a, â (?) in possessive forms: 
!! mahkwan:wahkwan, mîpit:wîpit, mîyaw:wîyaw
o:w <=> .#. _  %>:0 [ î: | a: | â: ] [ Cns | y ] :* %^POS:0  ;  !
 
"o:0 in possessive prefix"
!! __@RULENAME@__ , Okimasis p. 140 
o:0 <=> .#. _  %>:0 [ ô: | o: ] Cns  ;  !
! ohtâwîmâw:ôhtâwiya 

"i:0 after possessive prefix"
!! __@RULENAME@__ , ospiton 
i:0 <=> .#. o %>:0 _ Cns  ;  ! o+ispiton

"t2:t between prefix and vowel"
!! __@RULENAME@__ , joiner for nouns with initial vowel.
t2:t  <=> _ %>:0 Vow Lts:+  %^POS:0 Lts:* .#. ;

"t:c in diminutives" 
!! __@RULENAME@__ Okimasis 2004: 13, Rule 2.
t:c <=>  _ :*  %>:0 Dim .#. ; ! cehcapiwinisis




"Diminutives - t2 changes to c" 
!! !! __@RULENAME@__ , for the joiner. There are exeptions, but for they we have to make rules, or mark them with a dummy
t2:c <=>  _ %>:0 Vow Dim .#. ; 



"i:o alternations" 
!! __@RULENAME@__  for suffix initial vowel, stem final -Cw
i:o <=> Cns w: %>:0 ( %^POS:0 ) ( %>:0 ) _  ; !  

"Vowel final stems: vowel deletion when suffix has initial a or i" 
!! __@RULENAME@__ , when the suffix  niska, wâwi+a wâwi+ihk wâwi+is, oskâyi+a
Vx:0 <=> Cns _ %>:0 ( %^POS:0 ) ( %>:0 ) [ Vow | i: ] ;
         y: _   %>:0 [ a | i: ]   ;
	where Vx in ( a e o â ê î ô i ) ;  ! esa:esihk, esa:esak, esa:esis



"w:0 alternations" 
!! __@RULENAME@__  in -Cw stems
w:0 <=> \Pos Cns _ %>:0 ( %^POS:0 ) ( %>:0 )  i:  ; !  askihkw:askihkos nitihkom
        \Pos Cns _ ( %>:0 ) ( %^POS:0 )  .#. ;
        \Pos  [ o: | â: | a: ] _ %>:0 ( %^POS:0 )  i: [ s: | m: | n: | w: | y: | %^LOC:0 ] ; !cahcahkâyow , okimâw, ôtênaw
        \Pos  [ ê: | â: ] _ %>:0 ( %^POS:0 )  i: [ s: | m: ] ( %>:0 ) [ %^DIM:0 | %^POS:0 ]  ; !iskwêw, okimâw 
        \Pos  Vow _ %>:0 i: n â %^LOC:0 ; ! 



"i:ii- alternations in stem"
!! __@RULENAME@__ , Okimasis p. 13 pikiw : pikîs
i:î <=> Cns _ [ w: | y: ] %>:0  i: ( s ) ( m ) [ %^DIM:0 | %^LOC:0 | %^POS:0 ] ;
	    Cns _ [ w: | y: ] %>:0 %^POS:0 ( %>:0 )  i: ;

"o:oo- alternations in stem"
!! __@RULENAME@__ , initially in stem, and for stem vowel when final -oy/ow
!! okimâw:nitôkimâm, nitohtôsâpom tohtôsâpoy tohtôsâpôhk, cohcôsâpôs, cahcahkâyôhk, cahcahkâyôs
o:ô <=> t2: %>:0 _  [ k | s ] :* i: m  ; ! okimâw:nitôkimâm, oskâyi ! careful: only in front of k and s first
 		 %>:0 t _  h :* i: m  ; ! nitohtôsâpom tohtôsâpoy
 		\Pos _ [ w: | y: ] %>:0 i: ( s ) ( m ) ( n ) ( â ) [ %^DIM:0 | %^LOC:0 | %^POS:0 ] ; ! tohtôsâpôhk, cohcôsâpôs, cahcahkâyôhk, cahcahkâyôs


"a:aa alternations in stem" 
!! __@RULENAME@__  for stem vowel when final -ay
a:â <=> _ [ w: | y: ] %>:0  [ i: ] ( s ) ( m ) [ %^DIM:0 | %^LOC:0 | %^POS:0 ] ; ! 
        Cns _ [ w: | y: ] %>:0 %^POS:0 ( %>:0 )  i:0  ; ! maniway+i > maniwâ 


"y:0 alternations" 
!! __@RULENAME@__  for stem vowel when final -Vy
y:0 <=> \Pos [ i: | o: | a: ]  _ %>:0   :*  i: [ n: | m | w: | y: | s: | %^LOC:0 ]  ; 
!        [ i: | o: ]  _ ( %>:0 )  ( %^POS:0 )  i:  %>:0 %^LOC:0 h: ; 




"i:0 in suffix for stem final -Vw/Vy "
!! __@RULENAME@__ (first) : Okimasis 2004: 26, Rule 2, 8: Vow + (w/y) + ihk 
!! cahcahkâyow+ihk, tohtôsâpoy+ihk, cahcahkâyow+is, cohcôsâpoy+is, o+pikiw+iyiwa	 maniway+i
i:0 <=>  \PosDim [ o: | i: ]  [ w: | y: ] %>:0  _ %^LOC:0 ;   ! cahcahkâyow+ihk, tohtôsâpoy+ihk
         [ Vow: | o: ] [ w: | y: ] %>:0  _ [ s | m ] %^DIM:0 ; ! cahcahkâyow+is, cohcôsâpoy+is
         \Pos [ a: | i: | â: | o: ] [ w: | y: ] %>:0 ( %^POS:0 ) ( %>:0 ) _   ; !o+pikiw+iyiwa maniway+i
         \Pos  ê: w: %>:0 ( %^POS:0 )  _ [ s: | m: ] ( %>:0 ) [ %^DIM:0 | %^POS:0 ]  ; !iskwêw  
         Vow  w: %>:0 _ n â %^LOC:0 ;


!! !! VERBS

"h glottal stop for initial vowel stems in verbs: Conjunctive"
!! __@RULENAME@__
%^EGLOT:h <=>  _  %>:0 Vow: ;


"e and ee to aa rule in first and second person"
!! __@RULENAME@__
Vx:â <=> _ %>:0 %^EA:0 ;
	where Vx in (ê e) ;

"a to ee rule in first and second person"
!! __@RULENAME@__
a:ê <=> _ %>:0 %^EA:0 ; ! wâpahta:niwâpahtên


! "n:0 before k-suffix"
!! __@RULENAME@__
! n:0 <=>  [ i | o ] _ %>:0 ( h ) k ; !pimisin+hk Wolvengrey p. 394 "n usually/ sometimes dropped", not common in VAI-stems
	
! "n:h before k-suffix"
!! __@RULENAME@__
! n:h <=> a _ %>:0 k ; !mâyâtan:emâyâtahk Wolvengrey p. 393 II, but also in VAI-stems

"t3:s in VTA-4 verbs"
t3:s <=> _ %>:0 %^TS:0 i ;


"n:h before k suf in VII"
!! __@RULENAME@__
n:h <=> a _ %>:0 %^NII:0 k ;

"i:0 in VTA-Vw"
!! __@RULENAME@__
i:0 <=> %^WI:0 _ [ k | t ] ;

"w:0 in VTA-Vw"
!! __@RULENAME@__
w:0 <=>  _ %>:0 %^WI:0 i:0 ;

"n:0 before k suf in VII"
!! __@RULENAME@__
n:0 <=> [ o | i ] _ %>:0 %^NII:0 k ;

"i:o i to o when after %^IO in VTACw"
!! __@RULENAME@__
i:o <=> %^IO:0 _ k ;

"VTACw w to nothing when before %^IO"
!! __@RULENAME@__
w:0 <=> _ %>:0 %^IO:0 i:o ;
