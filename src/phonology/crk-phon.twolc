Alphabet
 a b c d e f g h i j k l m n o p q r s t u v w x y z
 A B C D E F G H I J K L M N O P Q R S T U V W X Y Z 
 â ê î ô
 Â Ê Î Ô
 
 t2:0
 %>:0 
 %^LC:0 ! Locative suffix alternation trigger
 %^DIM:0 ! Diminutive alternation trigger
 ;

Sets

 Vow = a e i o u â ê î ô 
       A E I O U Â Ê Î Ô ;

 LongVow = â ê î ô Â Ê Î Ô ;
 
 Cns = b c d f g h j k l m n p q r s t v w x z ;
 
 Nas = m n ;
 NonNas = b c d f g h j k l p q r s t v w x z ;

 Dummy = %> ;


!! !!!Rules

Rules

"Suffix a Deletion in Vowel Final Stems"
!! __@RULENAME@__ gives maskwa + ak = plural maskwak
 Vx:0 <=> _ %>:0 :Vow ;
 	where Vx in ( a e i o â ê î ô ) ;

"w Insertion in Front of Vowel Initial Suffix after k and m"
!! __@RULENAME@__ inserts w after k, m in front of plural -ak
!! Note that this rule probably is wrong, and that the -w- is
!! a morphological affix rather than a phonological variant.
 0:w <=> [ k | m ] %>:0 _ a k ;
 
!€ wâpask0>ak
!€ wâpaskw0ak 
 
 

"Suffix Final n" 
!! __@RULENAME@__  
 n:0 <=> _ %>:0 Cns ;

"locative alternations o"
i:o <=> k %>:0 _  %^LC:0   ;

! first
"locative alternations aaw 1"
Vx:0 <=> _ [ w: | y: ] %>:0  i: %^LC:0   ;
    where Vx in ( i a o ) ;
    
! second
"locative alternations aaw 2"
Cx:0 <=> [a:|i:|o:] _  %>:0  i: %^LC:0  ;
    where Cx in ( w y ) ;

! third
"locative alternations aaw 3a"
i:â <=> [ ô | ê | a: ] [ w: | y: ]  %>:0  _ %^LC:0  ;
!	<=> :n _ ^LC: ;

"locative alternation iihk 3i"
i:î <=> i: y:  %>:0  _ %^LC:0  ;

"locative alternation oohk io"
i:ô <=> o: y:  %>:0  _ %^LC:0  ;          !change the suffix initial i to a long o 
									!e.g. mîcimâpoy > mîcimâpoyôhk
                                    !then delete y in second
                                    !then delete o in first
                                    
"locative alternation naahk 1"		!adds nâhk after êw 1: change w to n
w:n <=> [ê|ô] _  %>:0 i: %^LC:0  ;

  
"possessive prefxes"
t2:t <=> _ %>:0 Vow ;
! t2:t <=> _ Vow ;

"i to a in prefixes in front of nasals"
i:a <=> .#. n _ %>:0 [ n | m ] i	;

"Diminutives - all stem t change to c with diminutives"
t:c <=> :* _ :* %^DIM:0 ;

"double consonant deletion"
Cx:0 <=> _ %>:0 Cx ;
	where Cx in Cns ;

"Antti's rule"
s:0 <=> t:c %>:0 _ %^DIM:0 ;

"Diminutives rule drop w, lengthen vowel 1"
[ w:0 | y:0 ]  <=> [ a: | i: | o: ] _ %>:0 s: %^DIM:0 ;

"Diminutives rule drop w, lengthen vowel 2"
[ i:î | a:â | o:ô ] <=> _ [ w:0 | y:0 ] %>:0 s: %^DIM:0 ;

"Diminutives rule change ending to os with k-final stems 1"
s:0 <=> k %>:0 _ %^DIM:0 ;


"Diminutives rule change ending to os with k-final stems 2"
i:o <=> k %>:0 s:0 %^DIM:0 _ ;










 