Alphabet
 a b c d e f g h i j k l m n o p q r s t u v w x y z
 A B C D E F G H I J K L M N O P Q R S T U V W X Y Z 
 â ê î ô
 Â Ê Î Ô
 
 t2:0      !!= * @CODE@ t between prefix and vowel-initial stem
 i2:0      !!= * @CODE@ i can change to o and ô in the possession element
 %>:0      !!= * @CODE@ affix boundary
 %^LC:0    !!= * @CODE@ Locative suffix alternation trigger
 %^DIM:0   !!= * @CODE@ Diminutive alternation trigger
 %^EGLOT:0 !!= * @CODE@ Conjunctive êh trigger
 %^POS:0   !!= * @CODE@ Possessive alternation trigger
 %^EA:0    !!= * @CODE@ ê to â in 1, 2 person of ê verbs

 ;

Sets

 Vow = a e i o u â ê î ô
       A E I O U Â Ê Î Ô ;

 LongVow = â ê î ô Â Ê Î Ô ;
 
 Cns = b c d f g h j k l m n p q r s t v w x z  ;
 
 Nas = m n ;
 NonNas = b c d f g h j k l p q r s t v w x z  ;

 Dummy = %> %^LC %^DIM %^EGLOT %^POS %^EA ;
 
Definitions 
 
 Pos = :* %^POS:0 :* ;
 Dim = :* %^DIM:0 :* ;
 Loc = :* %^LOC:0 :* ;

!! !!!Rules

Rules

"h glottal stop for initial vowel stems in Conjunctive"
!! __@RULENAME@__
%^EGLOT:h <=>  _  %>:0 Vow: ;


"Suffix vowel deletion in vowel final stems" 
!! __@RULENAME@__
Vx:0 <=> Cons _ %>:0 ( %^POS:0 ) ( %>:0 )  Vow ;
	where Vx in ( a e o â ê î ô ) ;  !niska

"i deletion in vowel final stems"
!! __@RULENAME@__ 
i:0 <=> _ %>:0 %>:0 i ; 
        _ w:0 %>:0 %^POS:0 i: ;
        Vow w: _ %>:0 [ a: | i: ] ; !wâwi+a wâwi+ihk

"w Insertion in Front of Vowel Initial Suffix after ksv c"
!! __@RULENAME@__ inserts w after k in front of plural -ak
!! Note that this rule probably is wrong, and that the -w- is
!! a morphological affix rather than a phonological variant.
0:w <=> [ k: | o: | ô: ] %>:0 _ a k ; ! sikâk:sikâkwak ôhô:ôhôwak cahcahkayo:cahcahkayowak ninekihikwak ninekihik - ak
         h: p: %>:0 _ a k ; ! sihkihp:sihkihpwak
 


"locative alternations o"
!! __@RULENAME@__ Okimasis 2004: 26, Rule 1: -k + ohk
i:o <=> k %>:0 _  %^LC:0 ; 

! first
"locative alternations: Vow:0 in front of  w/y"
!! __@RULENAME@__ (first) : Okimasis 2004: 26, Rule 2, 8: delete i, a, o: -Vow + (w/y) + ihk  OBS
Vx:0 <=> Cons  _ [ w: | y: ] %>:0  i: %^LC:0 ;   
    where Vx in ( i a o ) ; ! cahcahkâyow > cahcahkâyôhk, tohtôsâpoyôhk cahcahkâyowôhk

"o:oo- alternation initially in possessive forms"
!! __@RULENAME@__ , Okimasis p. 135  OBS
o:ô <=> t2: %>:0 _  k :* i2:   ; ! okimâw:nitôkimâm ! careful: only in front of k
 		 %>:0 t _  h :* i2:0  ; ! nitohtôsâpom tohtôsâpoy
 		_ [ w: | y: ] %>:0 i2: m: ; ! cahcahkâyow > nicahcahkâyôm
		_ [ w: | y: ] %>:0 %^DIM:0 s: i: ;
      
! second
"locative alternations: x/y:0"
!! __@RULENAME@__ (second) : Okimasis 2004: 26, Rule 2: delete w / y
!Cx:0 <=> [ a: | i: | o: ] _  %>:0  i: %^LC:0 ;  ! cahcahkâyow > cahcahkâyôhk
Cx:0 <=> .#. \Pos [ a: | i: | o: ] _  %>:0  i: %^LC:0 ;  
    where Cx in ( w y ) ;

! third
"locative alternations: i:aa"
!! __@RULENAME@__ (third) : Okimasis 2004: 26, Rule 2: -âhk
i:â <=>  [ ô | ê | a: ] [ w: | y: ]  %>:0  _ %^LC:0 ; 

!"locative alternation iihk 3i"
!!! __@RULENAME@__  Okimasis 2004: 26, Rule 3: -îhk
!i:î <=> i: y:  %>:0  _ %^LC:0 .#. ; 

"locative alternation oohk io"
!! __@RULENAME@__ Okimasis 2004: 26, Rule 4: -ôhk
i:ô <=> o: [ y: | w: ]  %>:0  _ %^LC:0  ;          
                                    !change the suffix initial i to a long o 
									!e.g. mîcimâpoy > mîcimâpoyôhk
                                    !then delete y in second
                                    !then delete o in first
                                    ! cahcahkâyow > cahcahkâyôhk
                                    
"locative alternation naahk 1"		
!! __@RULENAME@__ adds nâhk after êw/ôw, change w to n. Okimasis 2004: 26, Rule 5
w:n <=> [ ê | ô ] _  %>:0 i: %^LC:0  ;


"t2:t between prefix and vowel"
!! __@RULENAME@__ 
t2:t  <=> _ %>:0 Vow ;

"i2:o in possessive element"
!! __@RULENAME@__ 
i2:o <=> k %>:0  _ m  ;

"i2:i in possessive element"
!! __@RULENAME@__ 
i2:i <=> [ n | s | p ] ( %>:0 )  _ m  ;


"i2:0 in possessive element"
!! __@RULENAME@__ 
i2:0 <=> [ â | ô | ê | o: | i: ] ( w: )  %>:0  _ [ m | s ]  ; ! possessive element

"w:0 in front of possessive suffix"
!! __@RULENAME@__ 
[ w:0 | y:0 ] <=> [ Vow | o: ] _  %>:0 [ %^POS:0 | i2: | %^DIM:0 ] [ i: | m: | s: ] ; ! okimâw, maniway

 
"o:w in possessive prefix"
!! __@RULENAME@__ , w- in front of î, a, â (?) in possessive forms
o:w <=> .#. _  %>:0 [ î: | a: | â: ] [ Cns | y ] :* %^POS:0  ;  !
! mahkwan:wahkwan, mîpit:wîpit, mîyaw:wîyaw
 
"o:0 in possessive prefix"
!! __@RULENAME@__ , Okimasis p. 140 
o:0 <=> .#. _  %>:0 [ ô: | o: ] Cns  ;  !
! ohtâwîmâw:ôhtâwiya 
 
"i to a in prefixes in front of nasals"
!! __@RULENAME@__ 
i:a <=> .#. n _ %>:0 [ n | m ] i	;


"Double consonant deletion"
!! __@RULENAME@__ 
Cx:0 <=> _ %>:0 Cx ;
	where Cx in ( b c d f g h j k l m n p q r s v w x z ) ;

!"Double vowel deletion" "Suffix vowel deletion in vowel final stems"
!Vx:0 <=> _ %>:0 Vx ;
!	where Vx in Vow ;


!! !!Rules for generating the correct surface forms of -isis the diminutive suffix

"Diminutives - all stem t change to c" 
!! __@RULENAME@__ Okimasis 2004: 13, Rule 2.
t:c <=>  _ :*  %>:0 Dim .#. ; ! cehcapiwinisis

!"Diminutives - t2 changes to c" 
!  unsure about this
!t2:c <=>  _ %>:0 Vow Dim .#. ; 

"In Vow+y/w final stems with Diminutives, part 1: delete y/w" 
!! __@RULENAME@__ Okimasis 2004: 13, Rule 3. Vw final stems lose the w, shorth vowels are lengthened
Vx:0 <=> Vow _ %>:0 i: s: %^DIM:0 i: s: ;
	where Vx in (w y) ;


"In Vow+y/w final stems with Diminutives, part 2: lengthen stem final vowel" 
!! __@RULENAME@__ Okimasis 2004: 13, Rule 3. Vw final stems lose the w, shorth vowels are lengthened
[ i:î | a:â ] <=> Cons _ [ y: | w: ] %>:0 i: s: %^DIM:0 i: s: ; ! moved o:ô up to another rule


"a:aa in Vow+y/w final stems with Diminutives" 
!! __@RULENAME@__
a:â  <=> Cons _ [ y:0 | w:0 ] %>:0 %^POS:0  i: ;  !Not functioning waniwayiyiw > waniwâiyiw

![ o:ô | i:î | a:â ] <=> _ [ y: | w: ] %>:0 i: s: %^DIM:0 i: s: ;

"In Vow+y/w final stems with Diminutives, part 3: delete i in suffix" 
!! __@RULENAME@__ Okimasis 2004: 13, Rule 3. Vw final stems lose the w, shorth voewels are lengthened
i:0 <=> [ o: | a: | e: | ê: | :î | ô: ] [ y:| w: ] %>:0 _ s: %^DIM:0 i: s: ;

!"Vowel lengthening in iw final nouns with diminutives" !Is covered by part 2
!i:î <=> _ [ y:| w: ] %>:0 i: s: %^DIM:0 i: s: ;


"Diminutives rule change ending to os with k-final stems 1" 
!! __@RULENAME@__ Okimasis 2004: 14, Examples listed in 5.
s:0 <=> k %>:0 _ %^DIM:0 ;

"Diminutives rule change ending to os with k-final stems 2" 
!! __@RULENAME@__ Okimasis 2004: 14, Examples listed in 5.
i:o <=> k %>:0 _ s: %^DIM:0 ;

"Diminutives rule change ending delete i with nouns ending in kwa"
!! __@RULENAME@__
i:0 <=>  a: %>:0 _ s: %^DIM:0 ;
        .#. o: %>:0 _ [ t: | k | m | n | s | h | c | w ] ; !prefix kinshipterms


!"Remove locative i when poss -wâ"
!i:0 <=> â: ( w:0 ) %>:0 _ %^LC:0 ; ! kitiskwêmiwâhk or kitiskwêsisiwâwihk  CHECK!


"e and ee to aa rule in first and second person"
!! __@RULENAME@__
Vx:â <=> _ %>:0 %^EA:0 ;
	where Vx in (ê e) ;

!"ee to aa rule in first and second person"
!!! __@RULENAME@__
!ê:â <=> _ %>:0 %^EA:0 ;










