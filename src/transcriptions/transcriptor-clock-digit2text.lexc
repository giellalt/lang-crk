! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@hum.uit.no

! ==================== !
! The Plains Cree clock !
! ==================== !

! Note! This is NOT checked with native speakers nor
! textbooks, just based upon guessing from a dictionary.

Multichar_Symbols

@U.TIME.1@ 
@U.TIME.2@ 
@U.TIME.3@ 
@U.TIME.4@ 
@U.TIME.5@ 
@U.TIME.6@ 
@U.TIME.7@ 
@U.TIME.8@ 
@U.TIME.9@ 
@U.TIME.10@ 
@U.TIME.11@ 
@U.TIME.12@ 
+Use/NA
+Use/NG


LEXICON Root

! Allowing sloppy 2:30 in answer
@U.TIME.1@1:@U.TIME.1@+Use/NA     AFTpunkt ;    !01:00 - 01:19
@U.TIME.2@1:@U.TIME.2@+Use/NA     BEFpunkt ;    !01:20 - 01:59
@U.TIME.2@2:@U.TIME.2@+Use/NA     AFTpunkt ;    !02:00 - 02:19
@U.TIME.3@2:@U.TIME.3@+Use/NA     BEFpunkt ;    !02:20 - 02:59
@U.TIME.3@3:@U.TIME.3@+Use/NA     AFTpunkt ;    !03:00 - 03:19
@U.TIME.4@3:@U.TIME.4@+Use/NA     BEFpunkt ;    !03:20 - 03:59
@U.TIME.4@4:@U.TIME.4@+Use/NA     AFTpunkt ;    !04:00 - 04:19
@U.TIME.5@4:@U.TIME.5@+Use/NA     BEFpunkt ;    !04:20 - 04:59
@U.TIME.5@5:@U.TIME.5@+Use/NA     AFTpunkt ;    !05:00 - 05:19
@U.TIME.6@5:@U.TIME.6@+Use/NA     BEFpunkt ;    !05:20 - 05:59
@U.TIME.6@6:@U.TIME.6@+Use/NA     AFTpunkt ;    !06:00 - 06:19
@U.TIME.7@6:@U.TIME.7@+Use/NA     BEFpunkt ;    !06:20 - 06:59
@U.TIME.7@7:@U.TIME.7@+Use/NA     AFTpunkt ;    !07:00 - 07:19
@U.TIME.8@7:@U.TIME.8@+Use/NA     BEFpunkt ;    !07:20 - 07:59
@U.TIME.8@8:@U.TIME.8@+Use/NA     AFTpunkt ;    !08:00 - 08:19
@U.TIME.9@8:@U.TIME.9@+Use/NA     BEFpunkt ;    !08:20 - 08:59
@U.TIME.9@9:@U.TIME.9@+Use/NA     AFTpunkt ;    !09:00 - 09:19
@U.TIME.10@9:@U.TIME.10@+Use/NA   BEFpunkt ;    !09:20 - 09:59

! Correct is 02:30
@U.TIME.1@%01:@U.TIME.1@     AFTpunkt ;    !01:00 - 01:19
@U.TIME.2@%01:@U.TIME.2@     BEFpunkt ;    !01:20 - 01:59
@U.TIME.2@%02:@U.TIME.2@     AFTpunkt ;    !02:00 - 02:19
@U.TIME.3@%02:@U.TIME.3@     BEFpunkt ;    !02:20 - 02:59
@U.TIME.3@%03:@U.TIME.3@     AFTpunkt ;    !03:00 - 03:19
@U.TIME.4@%03:@U.TIME.4@    BEFpunkt ;    !03:20 - 03:59
@U.TIME.4@%04:@U.TIME.4@    AFTpunkt ;    !04:00 - 04:19
@U.TIME.5@%04:@U.TIME.5@     BEFpunkt ;    !04:20 - 04:59
@U.TIME.5@%05:@U.TIME.5@    AFTpunkt ;    !05:00 - 05:19
@U.TIME.6@%05:@U.TIME.6@     BEFpunkt ;    !05:20 - 05:59
@U.TIME.6@%06:@U.TIME.6@    AFTpunkt ;    !06:00 - 06:19
@U.TIME.7@%06:@U.TIME.7@     BEFpunkt ;    !06:20 - 06:59
@U.TIME.7@%07:@U.TIME.7@     AFTpunkt ;    !07:00 - 07:19
@U.TIME.8@%07:@U.TIME.8@    BEFpunkt ;    !07:20 - 07:59
@U.TIME.8@%08:@U.TIME.8@     AFTpunkt ;    !08:00 - 08:19
@U.TIME.9@%08:@U.TIME.9@    BEFpunkt ;    !08:20 - 08:59
@U.TIME.9@%09:@U.TIME.9@     AFTpunkt ;    !09:00 - 09:19
@U.TIME.10@%09:@U.TIME.10@   BEFpunkt ;    !09:20 - 09:59

@U.TIME.10@1%0:@U.TIME.10@   AFTpunkt ;     !10:00 - 10:19
@U.TIME.11@1%0:@U.TIME.11@   BEFpunkt ;     !10:20 - 10:59
@U.TIME.11@11:@U.TIME.11@    AFTpunkt ;     !11:00 - 11:59
@U.TIME.12@11:@U.TIME.12@    BEFpunkt ;     !11:19 - 11:59
@U.TIME.1@12:@U.TIME.1@    BEFpunkt ;     !11:19 - 11:59
@U.TIME.12@12:@U.TIME.12@    AFTpunkt ;     !11:19 - 11:59
!@U.TIME.12@%0%0:@U.TIME.12@    BEFpunkt ;     !11:19 - 11:59
12%:%0%0:nîsosâp     TIPAHIKAN        ;     !12:00
12%.%0%0:nîsosâp+Use/NA       TIPAHIKAN        ;     !12.00
12:nîsosâp+Use/NA       TIPAHIKAN        ;     !12.00
12%:%0%0:âpihtâkîsikâw+Use/NA        NOON        ;     !12:00
12%.%0%0:âpihtâkîsikâw+Use/NA        NOON        ;     !12.00
12:âpihtâkîsikâw+Use/NA       NOON        ;     !12.00
12%:%0%0:âpihtâtipiskâw+Use/NA        NOON        ;     !12:00
12%.%0%0:âpihtâtipiskâw+Use/NA        NOON        ;     !12.00
12:âpihtâtipiskâw+Use/NA       NOON        ;     !12.00



LEXICON BEFpunkt
%::+Use/NA BEF ;
%.:+Use/NA BEF ;

LEXICON AFTpunkt
%:: AFT ;  ! 2:00 = göökte
%.: AFT ;  ! 2.00 = göökte
:  HOUR ;  ! 2    = göökte

LEXICON BEF
3%0:                      HALFHOUR ;
31:nîstanaw% kêkâmitâtahtosâp%  TO ;
32:nîstanaw% ayinânêwosâp%  TO ;
33:nîstanaw% têpakohposâp%  TO ;
34:nîstanaw% nikotwâsosâp%  TO ;
35:nîstanaw% niyânanosâp%  TO ;
36:nîstanaw% nêwosâp%  TO ;
37:nîstanaw% nistosâp%  TO ;
38:nîstanaw% nîsosâp%  TO ; 
39:nîstanaw% pêyakosâp%  TO ;
4%0:nîstanaw%  TO ;
41:kîkâmitâtahtosâp%  TO ;
42:ayinânêwosâp%  TO ;
43:têpakohposâp%  TO ;
44:nikotwâsiksâp%  TO ;
45:niyânanosâp%  TO ;
46:nêwosâp%  TO ;
47:nistosâp%  TO ;
48:nîsosâp%  TO ;
49:pêyakosâp%  TO ;
5%0:mitâtaht%  TO ;
51:kîkâmitâtaht%  TO ;
52:ayinânêw%  TO ;
53:têpakohp%  TO ;
54:nikotwâsik%  TO ;
55:niyânan%  TO ;  
56:nêwo%  TO ;  
57:nisto%  TO ;
58:nîso%  TO ; 
59:pêyak%  TO ;

LEXICON AFT
%0%0:  HOUR ;
%01:pêyak%  OVER ;
%02:nîso%  OVER ;
%03:nisto%  OVER ;
%04:nêwo%  OVER ;
%05:niyânan%  OVER ;
%06:nikotwâsik%  OVER ;
%07:têpakohp%  OVER ;
%08:ayinânêw%  OVER ;
%09:kêkâmitâtaht%  OVER ;
1%0:mitâtaht%  OVER ;
11:pêyakosâp%  OVER ;
12:nîsosâp%  OVER ;
13:nistosâp%  OVER ;
14:nêwosâp%  OVER ;
15:niyânanosâp%  OVER ; 
16:nikotwâsiksâp%  OVER ;
17:têpakohposâp%  OVER ;
18:ayinânêwosâp%  OVER ;
19:kîkâmitâtahtosâp%  OVER ;
2%0:nîstanaw%  OVER ;
21:nîstanaw% pêyakosâp%  OVER ;
22:nîstanaw% nîsosâp%  OVER ;
23:nîstanaw% nistosâp%  OVER ;
24:nîstanaw% nêwosâp%  OVER ;
25:nîstanaw% niyânanosâp%  OVER ;  
26:nîstanaw% nikotwâsosâp%  OVER ;  
27:nîstanaw% têpakohposâp%  OVER ;
28:nîstanaw% ayinânêwosâp%  OVER ; 
29:nîstanaw% kêkâmitâtahtosâp%  OVER ;

LEXICON TO
 :cipahikanis% pâmayês%  HOUR ;

LEXICON OVER
 :cipahikanis% miyâskam%  HOUR ;

LEXICON HALFHOUR
@U.TIME.1@:nîsosâp@U.TIME.1@          HALF ;           
@U.TIME.2@:pêyak@U.TIME.2@         HALF ;         
@U.TIME.3@:nîso@U.TIME.3@          HALF ;          
@U.TIME.4@:nisto@U.TIME.4@       HALF ;       
@U.TIME.5@:nêwo@U.TIME.5@         HALF ;         
@U.TIME.6@:niyânan@U.TIME.6@         HALF ;         
@U.TIME.7@:nikotwâsik@U.TIME.7@       HALF ;       
@U.TIME.8@:têpakohp@U.TIME.8@       HALF ;       
@U.TIME.9@:ayinânêw@U.TIME.9@         HALF ;         
@U.TIME.10@:kêkâmitâtaht@U.TIME.10@       HALF ;       
@U.TIME.11@:mitâtaht@U.TIME.11@   HALF ;   
@U.TIME.12@:pêyakosâp@U.TIME.12@     HALF ; 

LEXICON HALF
 :% mînâpihtaw # ; !Not obligatory to add tipahikan
 +Use/NG:% mînâpihtaw TIPAHIKAN ; 


LEXICON HOUR
@U.TIME.1@:pêyak@U.TIME.1@          TIPAHIKAN ;           
@U.TIME.2@:nîso@U.TIME.2@         TIPAHIKAN ;         
@U.TIME.3@:nisto@U.TIME.3@          TIPAHIKAN ;          
@U.TIME.4@:nêwo@U.TIME.4@       TIPAHIKAN ;       
@U.TIME.5@:niyânan@U.TIME.5@         TIPAHIKAN ;         
@U.TIME.6@:nikotwâsik@U.TIME.6@         TIPAHIKAN ;         
@U.TIME.7@:têpakohp@U.TIME.7@       TIPAHIKAN ;       
@U.TIME.8@:ayinânêw@U.TIME.8@       TIPAHIKAN ;       
@U.TIME.9@:kêkâmitâtaht@U.TIME.9@         TIPAHIKAN ;         
@U.TIME.10@:mitâtaht@U.TIME.10@       TIPAHIKAN ;       
@U.TIME.11@:pêyakosâp@U.TIME.11@   TIPAHIKAN ;   
@U.TIME.12@:nîsosâp@U.TIME.12@     TIPAHIKAN ; 

LEXICON TIPAHIKAN
 :% tipahikan # ;

LEXICON NOON
 : # ;
