! "Inflections" of English definitions for Cree verbs

define Consonant [ b | c | d | f | g | h | j | k | l | m | n | p | q | r | s | t | v | w | x | z ]  ;

define Vowel [ a | e | i | o | u | â | ê | î | ô | á | é | í | ó | ú | à | è | ì | ò | ù ] ;

define Capital [ A | B | C | D | E | F | G | H | I | J | K | L | M | N | O | P | Q | R | S | T | U | V | W | X | Y | Z ] ;

define Alphabet [ Vowel | Consonant | "y" | Capital ] ;

define Punctuation [ "." | "," | ":" | ";" | "!" | "?" | "-" | "/" | "|" | "\" | %' | %" | "(" | ")" | "[" | "]" | "{" | "}" ] ;

define Symbols [ "@" | "#" | "$" | "€" | "£" | "§" | "%" | "&" | "+" | "*" | "<" | ">" | "+" | "=" | "~" | "^" ] ;

define Number [ 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9 ] ;

define Letters [ Alphabet | Punctuation | Symbols | Number | " " ] ;

! define Bx [ " " | .#. | Punctuation ] ;

! define Px [ "," | ";" | .#. ] ;

define Clause [ "Cnj+" | "Rel+" ] ;

define TenseAspect [ "Prs+" | "Prt+" | "Def+" | "Fut+" | "Int+" | "Cond+" | "Inf+" | "Imm+" | "Del+" ] ;

define Progressive [ "Prog+" ] ;

define Repetitive [ "Rept+" ] ;

define Negation [ "Neg+" ] ;

define Subject [ "0Sg+" | "1Sg+" | "2Sg+" | "3Sg+" | "1Du+" | "2Du+" | "3Du+" | "1Distr+" | "2Distr+" | "3Distr+" | "0Pl+" | "1Pl+" | "21Pl+" | "2Pl+" | "3Pl+" | "4Sg+" | "4Pl+" | "4Sg/Pl+" | "5Sg/Pl+" | "X+" | "XPl+" ] ;

define DirectObject [ "0SgO+" | "1SgO+" | "2SgO+" | "3SgO+" | "1DuO+" | "2DuO+" | "3DuO+" | "1DistrO+" | "2DistrO+" | "3DistrO+" | "0PlO+" | "1PlO+" | "21PlO+" | "2PlO+" | "3PlO+" | "4Sg/PlO+" | "5Sg/PlO+" | "XO+" | "XPlO+" ] ;

define IndirectObject [ "1SgIO+" | "2SgIO+" | "3SgIO+" | "1DuIO+" | "2DuIO+" | "3DuIO+" | "1PlIO+" | "21PlIO+" | "2PlIO+" | "3PlIO+" | "4Sg/PlIO+" | "5Sg/PlIO+" | "XIO+" | "XPlIO+" ] ;

! Variables for tenses/aspects/moods

define RsubjectPrs3Sg [ "@R.subject.3Sg@" | "@R.subject.4Sg@" | "@R.subject.1Distr@" | "@R.subject.2Distr@" | "@R.subject.3Distr@" | "@R.subject.X@" | "@R.subject.0Sg@" ] ;

define DsubjectPrs3Sg [ "@D.subject.3Sg@" "@D.subject.4Sg@" "@D.subject.1Distr@" "@D.subject.2Distr@" "@D.subject.3Distr@" "@D.subject.X@" "@D.subject.0Sg@" ] ;

define Rsubject2SgPl [ "@R.subject.2Sg@" | "@R.subject.2Du@" | "@R.subject.2Pl@" ] ;

define Dsubject2SgPl [ "@D.subject.2Sg@" "@D.subject.2Du@" "@D.subject.2Pl@" ] ;

define RsubjectDuPl [ "@R.subject.1Du@" | "@R.subject.2Du@" | "@R.subject.3Du@" | "@R.subject.1Pl@" | "@R.subject.21Pl@" | "@R.subject.2Pl@" | "@R.subject.3Pl@" | "@R.subject.4Pl@" | "@R.subject.4Sg/Pl@" | "@R.subject.5Sg/Pl@" | "@R.subject.XPl@" | "@R.subject.0Pl@" ] ;

define DsubjectDuPl [ "@D.subject.1Du@" "@D.subject.2Du@" "@D.subject.3Du@" "@D.subject.1Pl@" "@D.subject.21Pl@" "@D.subject.2Pl@" "@D.subject.3Pl@" "@D.subject.4Pl@" "@D.subject.4Sg/Pl@" "@D.subject.5Sg/Pl@" "@D.subject.XPl@" "@D.subject.0Pl@" ] ;

define Px [ "." | "," | ":" | ";" | "!" | "?" | .#. ] ;

define Bx [ Px | " " ] ;

! (Word-boundary character set - non-printable characters and space)
define Wx [ \[ Letters ] | " " ] ; 

! Marking non-subject occurrences of 'it'
regex [ {it} -> IT || " " _ [ "." | "," | .#. ] ,, {it} -> IT || Alphabet " " _ ,, {it} -> IT || "(" _ ,, {it} -> IT || _ ")" ] ;
define itNonSubjectMarking

regex [ {is} -> ISZ || [ Wx | Bx ] [ {s/he} | {he} | {she} | {it} ] { } _ [ Wx | Bx ] ]
.o. [ [ {s/he} | {he} | {she} | {they}  ] -> SUBJ
, {it} -> SUBJinanim
, [ {him/herself} | {himself} | {herself} | {themselves} | {themself} ] -> RFLX
, {s.o.'s} -> POSS
, [ {s.o.} | {him} | {them} ] -> DOBJ
, {s.t.} -> DOBJinanim
, {to s.b.} -> {to } IOBJ
, {for s.b.} -> {for } IOBJ
, {there is} -> {there } ISZ
|| [ Wx | Bx ] _ [ Wx | Bx ] ] ;
define Standardization

regex [ s -> {>s} "@D.neg@" "@D.prog.Prog@" [ "@R.tense.Prs@" | "@R.tense.Cond@" ] RsubjectPrs3Sg
, s -> {>ed} "@D.neg@" "@D.prog.Prog@" "@R.tense.Prt@"
, s -> {>ing} "@R.prog.Prog@"
, s -> {>} [ [ [ "@R.tense.Prs@" | "@R.tense.Cond@" ] DsubjectPrs3Sg "@D.neg@" "@D.prog.Prog@" ]
| [ [ "@R.tense.Prs@" | "@R.tense.Cond@" | "@R.tense.Prt@" ] "@R.neg.Neg@" "@D.prog.Prog@" ]
| [ [ "@R.tense.Fut@" | "@R.tense.Def@" | "@R.tense.Int@" | "@R.tense.Inf@" | "@R.tense.Imm@" | "@R.tense.Del@" ] "@D.prog.Prog@" ] ]
|| [ SUBJ | SUBJinanim ] Wx+ Alphabet+ _ [ Wx | Bx ] ] ;
! ] ;
define VerbInflection

regex [ {ha>} -> {have}
, {ha>ed}-> {had}
, {ha>ing} -> {hav>ing}
, {doe>} -> {do}
, {doe>ed} -> {did}
, {doe>ing} -> {do>ing}
|| Wx _ Wx ] ;
define IrregularVerbs

! Be-copula inflection
regex [ IS | ISZ -> {be} [ "@R.tense.Def@" | "@R.tense.Int@" | "@R.tense.Imm@" | "@R.tense.Del@" | "@R.tense.Inf@" ]
, IS | ISZ -> {am} "@R.subject.1Sg@" [ "@R.tense.Prs@" | "@R.tense.Cond@" | "@R.tense.Fut@" ]
, IS | ISZ -> {are} [ "@R.subject.2Sg@" | RsubjectDuPl ] [ "@R.tense.Prs@" | "@R.tense.Fut@" | "@R.tense.Cond@" ]
, IS | ISZ -> {is} RsubjectPrs3Sg [ "@R.tense.Prs@" | "@R.tense.Fut@" | "@R.tense.Cond@" ]
, IS | ISZ -> {was} [ "@D.subject.2Sg@" DsubjectDuPl ] "@R.tense.Prt@"
, IS | ISZ -> {were} [ "@R.subject.2Sg@" | RsubjectDuPl ] "@R.tense.Prt@"
|| Wx _ Wx ] ;
define BeInflection

regex [ [..] -> { who} "@R.clause.Rel@"
| "@D.clause.Rel@"
|| SUBJ _
,, [..] -> { which} "@R.clause.Rel@"
| "@D.clause.Rel@"
|| SUBJinanim _
] ;
define Relativization

regex [
! Present tense / (Future) conditional
[..] -> [ [ "@R.tense.Prs@" | "@R.tense.Cond@" ] "@D.neg@" "@D.prog@"
| { does not} [ "@R.tense.Prs@" | "@R.tense.Cond@" ] "@R.neg.Neg@" "@D.prog@" RsubjectPrs3Sg
| { do not} [ "@R.tense.Prs@" | "@R.tense.Cond@" ] "@R.neg.Neg@" "@D.prog@" DsubjectPrs3Sg
| { } IS [ "@R.tense.Prs@" | "@R.tense.Cond@" ] "@D.neg@" "@R.prog.Prog@"
| { } IS { not} [ "@R.tense.Prs@" | "@R.tense.Cond@" ] "@R.neg.Neg@" "@R.prog.Prog@"
! Past tense
| "@R.tense.Prt@" "@D.neg@" "@D.prog@"
| { did not} "@R.tense.Prt@" "@R.neg.Neg@" "@D.prog@"
| { } IS "@R.tense.Prt@" "@D.neg@" "@R.prog.Prog@"
| { } IS { not} "@R.tense.Prt@" "@R.neg.Neg@" "@R.prog.Prog@"
! Future prospective
| { } IS { going to} "@R.tense.Fut@" "@D.neg@" "@D.prog@"
| { } IS { not going to} "@R.tense.Fut@" "@R.neg.Neg@" "@D.prog@"
| { } IS { going to be} "@R.tense.Fut@" "@D.neg@" "@R.prog.Prog@"
| { } IS { not going to be} "@R.tense.Fut@" "@R.neg.Neg@" "@R.prog.Prog@"
! Future definite
| { will} "@R.tense.Def@" "@D.neg@" "@D.prog@"
| { will not} "@R.tense.Def@" "@R.neg.Neg@" "@D.prog@"
| { will be} "@R.tense.Def@" "@D.neg@" "@R.prog.Prog@"
| { will not be} "@R.tense.Def@" "@R.neg.Neg@" "@R.prog.Prog@"
! Future intentional
| { wants to} "@R.tense.Int@" "@D.neg@" "@D.prog@" RsubjectPrs3Sg
| { want to} "@R.tense.Int@" "@D.neg@" "@D.prog@" DsubjectPrs3Sg
| { does not want to} "@R.tense.Int@" "@R.neg.Neg@" "@D.prog@" RsubjectPrs3Sg
| { do not want to} "@R.tense.Int@" "@R.neg.Neg@" "@D.prog@" DsubjectPrs3Sg
| { wants to be} "@R.tense.Int@" "@D.neg@" "@R.prog.Prog@" RsubjectPrs3Sg
| { want to be} "@R.tense.Int@" "@D.neg@" "@R.prog.Prog@" DsubjectPrs3Sg
| { does not want to be} "@R.tense.Int@" "@R.neg.Neg@" "@R.prog.Prog@" RsubjectPrs3Sg
| { do not want to be} "@R.tense.Int@" "@R.neg.Neg@" "@R.prog.Prog@" DsubjectPrs3Sg
! Infinitive
| { to} "@R.tense.Inf@" "@D.neg@" "@D.prog@"
| { to not} "@R.tense.Inf@" "@R.neg.Neg@" "@D.prog@"
| { to be} "@R.tense.Inf@" "@D.neg@" "@R.prog.Prog@"
| { to not be} "@R.tense.Inf@" "@R.neg.Neg@" "@R.prog.Prog@"
! Immediate imperative
| "@R.tense.Imm@" "@D.neg@" "@D.prog@"
| "@R.tense.Imm@" "@R.neg.Neg@" "@D.prog@" Dsubject2SgPl
| { do not} "@R.tense.Imm@" "@R.neg.Neg@" "@D.prog@" Rsubject2SgPl
| { be} "@R.tense.Imm@" "@R.neg.Neg@" "@R.prog.Prog@" Dsubject2SgPl
| { be} "@R.tense.Imm@" "@D.neg@" "@R.prog.Prog@"
| { do not be} "@R.tense.Imm@" "@R.neg.Neg@" "@R.prog.Prog@" Rsubject2SgPl
! Delayed imperative
| "@R.tense.Del@" "@D.neg@" "@D.prog@"
| "@R.tense.Del@" "@R.neg.Neg@" "@D.prog@" Dsubject2SgPl
| { do not} "@R.tense.Del@" "@R.neg.Neg@" "@D.prog@" Rsubject2SgPl
| { be} "@R.tense.Del@" "@R.neg.Neg@" "@R.prog.Prog@" Dsubject2SgPl
| { be} "@R.tense.Del@" "@D.neg@" "@R.prog.Prog@"
| { do not be} "@R.tense.Del@" "@R.neg.Neg@" "@R.prog.Prog@" Rsubject2SgPl ]
|| Wx [ SUBJ | SUBJinanim ] _ " " \ISZ
] ;
define AuxiliaryPhrase

regex [
! Present tense / (Future) conditional
ISZ -> [ IS [ "@R.tense.Prs@" | "@R.tense.Cond@" ] "@D.neg@" "@D.prog@"
| IS { not} [ "@R.tense.Prs@" | "@R.tense.Cond@" ] "@R.neg.Neg@" "@D.prog@" ! RsubjectPrs3Sg
! | IS { not} [ "@R.tense.Prs@" | "@R.tense.Cond@" ] "@R.neg.Neg@" "@D.prog@" DsubjectPrs3Sg ! Duplicate?
| IS { being} [ "@R.tense.Prs@" | "@R.tense.Cond@" ] "@D.neg@" "@R.prog.Prog@"
| IS { not being} [ "@R.tense.Prs@" | "@R.tense.Cond@" ] "@R.neg.Neg@" "@R.prog.Prog@"
! Past tense
| IS "@R.tense.Prt@" "@D.neg@" "@D.prog@"
| IS { not} "@R.tense.Prt@" "@R.neg.Neg@" "@D.prog@"
| IS { being} "@R.tense.Prt@" "@D.neg@" "@R.prog.Prog@"
| IS { not being} "@R.tense.Prt@" "@R.neg.Neg@" "@R.prog.Prog@"
! Future prospective
| IS { going to be} "@R.tense.Fut@" "@D.neg@" "@D.prog@"
| IS { not going to be} "@R.tense.Fut@" "@R.neg.Neg@" "@D.prog@"
| IS { going to be being} "@R.tense.Fut@" "@D.neg@" "@R.prog.Prog@"
| IS { not going to be being} "@R.tense.Fut@" "@R.neg.Neg@" "@R.prog.Prog@"
! Future definite
| {will be} "@R.tense.Def@" "@D.neg@" "@D.prog@"
| {will not be} "@R.tense.Def@" "@R.neg.Neg@" "@D.prog@"
| {will be being} "@R.tense.Def@" "@D.neg@" "@R.prog.Prog@"
| {will not be being} "@R.tense.Def@" "@R.neg.Neg@" "@R.prog.Prog@"
! Future intentional
| {wants to be} "@R.tense.Int@" "@D.neg@" "@D.prog@" RsubjectPrs3Sg
| {want to be} "@R.tense.Int@" "@D.neg@" "@D.prog@" DsubjectPrs3Sg
| {does not want to be} "@R.tense.Int@" "@R.neg.Neg@" "@D.prog@" RsubjectPrs3Sg
| {do not want to be} "@R.tense.Int@" "@R.neg.Neg@" "@D.prog@" DsubjectPrs3Sg
| {wants to be being} "@R.tense.Int@" "@D.neg@" "@R.prog.Prog@" RsubjectPrs3Sg
| {want to be being} "@R.tense.Int@" "@D.neg@" "@R.prog.Prog@" DsubjectPrs3Sg
| {does not want to be being} "@R.tense.Int@" "@R.neg.Neg@" "@R.prog.Prog@" RsubjectPrs3Sg
| {do not want to be being} "@R.tense.Int@" "@R.neg.Neg@" "@R.prog.Prog@" DsubjectPrs3Sg
! Infinitive
| {to be} "@R.tense.Inf@" "@D.neg@" "@D.prog@"
| {to not be} "@R.tense.Inf@" "@R.neg.Neg@" "@D.prog@"
| {to be being} "@R.tense.Inf@" "@D.neg@" "@R.prog.Prog@"
| {to not be being} "@R.tense.Inf@" "@R.neg.Neg@" "@R.prog.Prog@"
! Immediate imperative
| {be} "@R.tense.Imm@" "@D.neg@" "@D.prog@"
| {be} "@R.tense.Imm@" "@R.neg.Neg@" "@D.prog@" Dsubject2SgPl
| {do not be} "@R.tense.Imm@" "@R.neg.Neg@" "@D.prog@" Rsubject2SgPl
| {be being} "@R.tense.Imm@" "@R.prog.Prog@" Dsubject2SgPl
| {be being} "@R.tense.Imm@" "@D.neg.Neg@" "@R.prog.Prog@" Rsubject2SgPl
| {do not be being} "@R.tense.Imm@" "@R.neg.Neg@" "@R.prog.Prog@" Rsubject2SgPl
! Delayed imperative
| {be} "@R.tense.Del@" "@D.neg@" "@D.prog@"
| {be} "@R.tense.Del@" "@R.neg.Neg@" "@D.prog@" Dsubject2SgPl
| {do not be} "@R.tense.Del@" "@R.neg.Neg@" "@D.prog@" Rsubject2SgPl
| {be being} "@R.tense.Del@" "@R.prog.Prog@" Dsubject2SgPl
| {be being} "@R.tense.Del@" "@D.neg.Neg@" "@R.prog.Prog@" Rsubject2SgPl
| {do not be being} "@R.tense.Del@" "@R.neg.Neg@" "@R.prog.Prog@" Rsubject2SgPl ]
|| [ {there } | SUBJinanim { } | SUBJ { } ] _
] ;
define BeAuxiliaryPhrase

regex [
[..] -> "@R.clause.Cnj@" "@D.tense.Inf@" "@D.tense.Imm@" "@D.tense.Del@" {as }
| "@R.tense.Cond@" "@D.clause@" {when }
| "@R.tense.Inf@" "@D.clause@" {for }
| [ "@R.tense.Imm@" | "@R.tense.Del@" ] Dsubject2SgPl "@D.neg@" {let }
| [ "@R.tense.Imm@" | "@R.tense.Del@" ] Dsubject2SgPl "@R.neg@" {do not let }
| "@D.clause.Cnj@" "@D.tense.Cond@" "@D.tense.Inf@" [ [ "@D.tense.Imm@" "@D.tense.Del@" ] | [ [ "@R.tense.Imm@" | "@R.tense.Del@" ] Rsubject2SgPl ] ]
|| _ [ SUBJ | SUBJinanim | {there } [ IS | {be} | {will} | {to} ] ] ! context needs checking for redundancy
] ;
define PhrasePrefix

regex [
[..] -> { now} "@R.tense.Imm@"
| { later} "@R.tense.Del@"
| "@D.tense.Imm@" "@D.tense.Del@"
|| _ Px ]
.o. [ [..] -> { again and again} "@R.rept.Rept@" | "@D.rept.Rept@"
|| _ Px ] ;
define PhraseSuffix

! Indirect Object Conversion
regex [ IOBJ -> {me} "@R.recipient.1Sg@"
, IOBJ -> {you} "@R.recipient.2Sg@"
, IOBJ -> {him/her} "@R.recipient.3Sg@"
, IOBJ -> {us both} "@R.recipient.1Du@"
, IOBJ -> {you both} "@R.recipient.2Du@"
, IOBJ -> {them both} "@R.recipient.3Du@"
, IOBJ -> {us} "@R.recipient.1Pl@"
, IOBJ -> {you and us} "@R.recipient.21Pl@"
, IOBJ -> {you all} "@R.recipient.2Pl@"
, IOBJ -> {them} "@R.recipient.3Pl@"
, IOBJ -> {him/her/them} "@R.recipient.4Sg/Pl@"
, IOBJ -> {him/her/them over there} "@R.recipient.5Sg/Pl@"
, IOBJ -> {someone} "@R.recipient.X@"
, IOBJ -> {some people} "@R.recipient.XPl@"
, IOBJ -> {someone} "@D.recipient@"
|| Wx [ {to } | {for } ] _ ] ;
define IObjectConversion

! Subject Inverse Conversion
regex [ SUBJ -> {me} "@R.subject.1Sg@" [ "@R.tense.Imm@" | "@R.tense.Del@" | "@R.tense.Inf@" ]  
, SUBJ -> {you} "@R.subject.2Sg@" "@R.tense.Inf@"
, SUBJ -> {(you)} "@R.subject.2Sg@" [ "@R.tense.Imm@" | "@R.tense.Del@" ]  
, SUBJ -> {him/her} "@R.subject.3Sg@" [ "@R.tense.Imm@" | "@R.tense.Del@" | "@R.tense.Inf@" ]  
, SUBJinanim -> {it} "@R.subject.3Sg@" [ "@R.tense.Imm@" | "@R.tense.Del@" | "@R.tense.Inf@" ]  
, SUBJ -> {us} "@R.subject.1Pl@" [ "@R.tense.Imm@" | "@R.tense.Del@" | "@R.tense.Inf@" ]  
, SUBJ -> {us both} "@R.subject.1Du@" [ "@R.tense.Imm@" | "@R.tense.Del@" | "@R.tense.Inf@" ]  
, SUBJ -> {each and every one of us} "@R.subject.1Distr@" [ "@R.tense.Imm@" | "@R.tense.Del@" | "@R.tense.Inf@" ]  
, SUBJ -> {you and us} "@R.subject.21Pl@" [ "@R.tense.Imm@" | "@R.tense.Del@" | "@R.tense.Inf@" ]  
, SUBJ -> {you both} "@R.subject.2Du@" "@R.tense.Inf@"
, SUBJ -> {each and every one of you} "@R.subject.2Distr@" "@R.tense.Inf@"
, SUBJ -> {you all} "@R.subject.2Pl@" "@R.tense.Inf@"
, SUBJ -> {(you both)} "@R.subject.2Du@" [ "@R.tense.Imm@" | "@R.tense.Del@" ]  
, SUBJ -> {(you all)} "@R.subject.2Pl@" [ "@R.tense.Imm@" | "@R.tense.Del@" ]  
, SUBJ | SUBJinanim -> {them} "@R.subject.3Pl@" [ "@R.tense.Imm@" | "@R.tense.Del@" | "@R.tense.Inf@" ]  
, SUBJ | SUBJinanim -> {them both} "@R.subject.3Du@" [ "@R.tense.Imm@" | "@R.tense.Del@" | "@R.tense.Inf@" ]  
, SUBJ | SUBJinanim -> {each and every one of them} "@R.subject.3Distr@" [ "@R.tense.Imm@" | "@R.tense.Del@" | "@R.tense.Inf@" ]  
, SUBJinanim -> {it over there} "@R.subject.4Sg@" [ "@R.tense.Imm@" | "@R.tense.Del@" | "@R.tense.Inf@" ]  
, SUBJinanim -> {them over there} "@R.subject.4Pl@" [ "@R.tense.Imm@" | "@R.tense.Del@" | "@R.tense.Inf@" ]  
, SUBJ -> {him/her/them} "@R.subject.4Sg/Pl@" [ "@R.tense.Imm@" | "@R.tense.Del@" | "@R.tense.Inf@" ]  
, SUBJ -> {him/her/them over there} "@R.subject.5Sg/Pl@" [ "@R.tense.Imm@" | "@R.tense.Del@" | "@R.tense.Inf@" ]  
, SUBJ -> {someone} "@R.subject.X@" [ "@R.tense.Imm@" | "@R.tense.Del@" | "@R.tense.Inf@" ]
, SUBJ -> {people} "@R.subject.XPl@" [ "@R.tense.Imm@" | "@R.tense.Del@" | "@R.tense.Inf@" ]
|| Wx _ Wx ]

! Subject Conversion
| [ SUBJ -> {I} "@R.subject.1Sg@" "@D.tense.Imm@" "@D.tense.Del@" "@D.tense.Inf@" 
, SUBJ  -> {you} "@R.subject.2Sg@" "@D.tense.Imm@" "@D.tense.Del@" "@D.tense.Inf@" 
, SUBJ -> {he/she} "@R.subject.3Sg@" "@D.tense.Imm@" "@D.tense.Del@" "@D.tense.Inf@" 
, SUBJinanim -> {it} "@R.subject.3Sg@" "@D.tense.Imm@" "@D.tense.Del@" "@D.tense.Inf@" 
, SUBJ -> {we} "@R.subject.1Pl@" "@D.tense.Imm@" "@D.tense.Del@" "@D.tense.Inf@" 
, SUBJ -> {we both} "@R.subject.1Du@" "@D.tense.Imm@" "@D.tense.Del@" "@D.tense.Inf@" 
, SUBJ -> {each and every one of us} "@R.subject.1Distr@" "@D.tense.Imm@" "@D.tense.Del@" "@D.tense.Inf@" 
, SUBJ -> {we and you} "@R.subject.21Pl@" "@D.tense.Imm@" "@D.tense.Del@" "@D.tense.Inf@" 
, SUBJ -> {you both} "@R.subject.2Du@" "@D.tense.Imm@" "@D.tense.Del@" "@D.tense.Inf@" 
, SUBJ -> {each and every one of you} "@R.subject.2Distr@" "@D.tense.Imm@" "@D.tense.Del@" "@D.tense.Inf@" 
, SUBJ -> {you all} "@R.subject.2Pl@" "@D.tense.Imm@" "@D.tense.Del@" "@D.tense.Inf@" 
, SUBJ | SUBJinanim -> {they} "@R.subject.3Pl@" "@D.tense.Imm@" "@D.tense.Del@" "@D.tense.Inf@" 
, SUBJ | SUBJinanim -> {they both} "@R.subject.3Du@" "@D.tense.Imm@" "@D.tense.Del@" "@D.tense.Inf@" 
, SUBJ | SUBJinanim -> {each and every one of them} "@R.subject.3Distr@" "@D.tense.Imm@" "@D.tense.Del@" "@D.tense.Inf@" 
, SUBJ | SUBJinanim -> {it over there} "@R.subject.4Sg@" "@D.tense.Imm@" "@D.tense.Del@" "@D.tense.Inf@" 
, SUBJ | SUBJinanim -> {they over there} "@R.subject.4Pl@" "@D.tense.Imm@" "@D.tense.Del@" "@D.tense.Inf@" 
, SUBJ | SUBJinanim -> {s/he/they} "@R.subject.4Sg/Pl@" "@D.tense.Imm@" "@D.tense.Del@" "@D.tense.Inf@" 
, SUBJ -> {s/he/they over there} "@R.subject.5Sg/Pl@" "@D.tense.Imm@" "@D.tense.Del@" "@D.tense.Inf@" 
, SUBJ -> {someone} "@R.subject.X@" "@D.tense.Imm@" "@D.tense.Del@" "@D.tense.Inf@"
, SUBJ -> {people} "@R.subject.XPl@" "@D.tense.Imm@" "@D.tense.Del@" "@D.tense.Inf@"
|| Wx _ Wx ] ;
define SubjectConversion

! Object Conversion
regex [ DOBJ -> {me} "@R.object.1Sg@"
, DOBJ -> {you} "@R.object.2Sg@"
, DOBJ -> {him/her} "@R.object.3Sg@"
, DOBJ  -> {us} "@R.object.1Pl@"
, DOBJ  -> {us both} "@R.object.1Du@"
, DOBJ  -> {each and every one of us} "@R.object.1Distr@"
, DOBJ -> {you and us} "@R.object.21Pl@"
, DOBJ -> {you both} "@R.object.2Du@"
, DOBJ -> {each and every one of you} "@R.object.2Distr@"
, DOBJ -> {you all} "@R.object.2Pl@"
, DOBJ  -> {them both} "@R.object.3Du@"
, DOBJ  -> {each and every one of them} "@R.object.3Distr@"
, DOBJ  -> {them} "@R.object.3Pl@"
, DOBJ  -> {him/her/them} "@R.object.4Sg/Pl@"
, DOBJ  -> {him/her/them over there} "@R.object.5Sg/Pl@"
, DOBJ -> {someone} "@R.object.X@"
, DOBJ -> {people} "@R.object.XPl@"
, DOBJ -> {someone} "@D.object@" ! Allows for generation of VTA phrase even if object feature is not specified
, DOBJinanim -> {it} "@D.object@"
|| Wx _ Wx ]
,, [ [..] -> "@D.object@" || _ .#. ] ;
define ObjectConversion

! Reflexive Conversion
regex [ RFLX -> {myself} "@R.subject.1Sg@"
, RFLX -> {yourself} "@R.subject.2Sg@"
, RFLX -> {him/herself} "@R.subject.3Sg@"
, RFLX -> {ourselves} "@R.subject.1Du@"
, RFLX -> {ourselves} "@R.subject.1Pl@"
, RFLX -> {your/ourselves} "@R.subject.21Pl@"
, RFLX -> {yourselves} "@R.subject.2Du@"
, RFLX -> {yourselves} "@R.subject.2Pl@"
, RFLX -> {themselves} "@R.subject.3Pl@"
, RFLX -> {themselves} "@R.subject.3Du@"
, RFLX -> {him/her/themselves} "@R.subject.4Sg/Pl@"
, RFLX -> {him/her/themselves} "@R.subject.5Sg/Pl@"
, RFLX -> {him/herself} "@R.subject.X@"
, RFLX -> {themselves} "@R.subject.XPl@"
|| Wx _ ] ;
define ReflexiveConversion

! Possessive Conversion
regex [ POSS -> {my} "@R.subject.1Sg@"
, POSS -> {your} "@R.subject.2Sg@"
, POSS -> {his/her} "@R.subject.3Sg@"
, POSS -> {our} "@R.subject.1Pl@"
, POSS -> {our} "@R.subject.1Du@"
, POSS -> {your and our} "@R.subject.21Pl@"
, POSS -> {your} "@R.subject.2Du@"
, POSS -> {your} "@R.subject.2Pl@"
, POSS -> {their} "@R.subject.3Du@"
, POSS -> {their} "@R.subject.3Pl@"
, POSS -> {his/her/their} "@R.subject.4Sg/Pl@"
, POSS -> {his/her/their} "@R.subject.5Sg/Pl@"
, POSS -> {someone's} "@R.subject.X@"
, POSS -> {their} "@R.subject.XPl@"
|| Wx _ Wx ] ;
define PossessiveConversion

! Conversion of tags into flag-diacritics
regex [ "Cnj+" -> "@P.clause.Cnj@" , "Rel+" -> "@P.clause.Rel@" ]
.o. [ "Prs+" -> "@P.tense.Prs@" , "Prt+" -> "@P.tense.Prt@" , "Def+" -> "@P.tense.Def@" , "Fut+" -> "@P.tense.Fut@" , "Int+" -> "@P.tense.Int@" , "Cond+" -> "@P.tense.Cond@" , "Imm+" -> "@P.tense.Imm@" , "Del+" -> "@P.tense.Del@" , "Inf+" -> "@P.tense.Inf@" ]
.o. [ "Prog+" -> "@P.prog.Prog@" ]
.o. [ "Rept+" -> "@P.rept.Rept@" ]
.o. [ "Neg+" -> "@P.neg.Neg@" ]
.o. [ "0Sg+" -> "@P.subject.0Sg@" , "1Sg+" -> "@P.subject.1Sg@" , "2Sg+" -> "@P.subject.2Sg@", "3Sg+" -> "@P.subject.3Sg@" , "1Du+" -> "@P.subject.1Du@" , "2Du+" -> "@P.subject.2Du@" , "3Du+" -> "@P.subject.3Du@" , "1Distr+" -> "@P.subject.1Distr@" , "2Distr+" -> "@P.subject.2Distr@", "3Distr+" -> "@P.subject.3Distr@", "0Pl+" -> "@P.subject.0Pl@" , "1Pl+" -> "@P.subject.1Pl@" , "21Pl+" -> "@P.subject.21Pl@", "2Pl+" -> "@P.subject.2Pl@" , "3Pl+" -> "@P.subject.3Pl@" , "4Sg+" -> "@P.subject.4Sg@" , "4Pl+" -> "@P.subject.4Pl@" , "4Sg/Pl+" -> "@P.subject.4Sg/Pl@" , "5Sg/Pl+" -> "@P.subject.5Sg/Pl@", "X+" -> "@P.subject.X@" , "XPl+" -> "@P.subject.XPl@" ]
.o. [ "0SgO+" -> "@P.object.0Sg@" , "1SgO+" -> "@P.object.1Sg@" , "2SgO+" -> "@P.object.2Sg@", "3SgO+" -> "@P.object.3Sg@" , "1DuO+" -> "@P.object.1Du@" , "2DuO+" -> "@P.object.2Du@", "3DuO+" -> "@P.object.3Du@" , "1DistrO+" -> "@P.object.1Distr@", "2DistrO+" -> "@P.object.2Distr@" , "3DistrO+" -> "@P.object.3Distr@" , "0PlO+" -> "@P.object.0Pl@" , "1PlO+" -> "@P.object.1Pl@" , "21PlO+" -> "@P.object.21Pl@", "2PlO+" -> "@P.object.2Pl@" , "3PlO+" -> "@P.object.3Pl@" ,  "4Sg/PlO+" -> "@P.object.4Sg/Pl@" , "5Sg/PlO+" -> "@P.object.5Sg/Pl@", "XO+" -> "@P.object.X@" , "XPlO+" -> "@P.object.XPl@" ]
.o. [ "1SgIO+" -> "@P.recipient.1Sg@" , "2SgIO+" -> "@P.recipient.2Sg@" , "3SgIO+" -> "@P.recipient.3Sg@" ,  "1DuIO+" -> "@P.recipient.1Du@" , "2DuIO+" -> "@P.recipient.2Du@" , "3DuIO+" -> "@P.recipient.3Du@" , "1PlIO+" -> "@P.recipient.1Pl@" , "21PlIO+" -> "@P.recipient.21Pl@" , "2PlIO+" -> "@P.recipient.2Pl@" , "3PlIO+" -> "@P.recipient.3Pl@" , "4Sg/PlIO+" -> "@P.recipient.4Sg/Pl@" , "5Sg/PlIO+" -> "@P.recipient.5Sg/Pl@" , "XIO+" -> "@P.recipient.X@" , "XPlIO+" -> "@P.recipient.XPl@" ] ;
define Tag2FlagConversion

! Undoing special marking of non-subject 'it'
regex [ IT -> {it} || [ Wx | Bx ] _ [ Wx | Bx ] ] ;
define itNonSubjectUnmarking

! Resolving irregularities of the spelling of English (irregular and regular) verb wordforms

read lexc src/transcriptions/cw-eng-verbs-prs3Sg2inf.lexc
define EngInf

regex [ EngInf -> || [ Wx | Bx ] _ [ Wx | Bx ] ] ;
define EngInf2Inf

read lexc src/transcriptions/cw-eng-verbs-prs3Sg2prt.lexc
define EngPrt

regex [ EngPrt -> || [ Wx | Bx ] _ [ Wx | Bx ] ] ;
define EngPrt2Prt

read lexc src/transcriptions/cw-eng-verbs-prs3Sg2prog.lexc
define EngProg

regex [ EngProg -> || [ Wx | Bx ] _ [ Wx | Bx ] ] ;
define EngProg2Prog

source inc/eng-phrase-inflection/phonology-eng.xfscript
define EngPhon

regex [ ">" -> 0 || Alphabet+ _ Alphabet* ] ;
define RemoveSuffixBoundary

! Definition of input and conversion
regex [ (Clause) TenseAspect Subject (DirectObject) (IndirectObject) (Progressive) (Repetitive) (Negation) Letters+ ] ;
define Input

regex [ Input
.o. itNonSubjectMarking
.o. Standardization
.o. Tag2FlagConversion
.o. VerbInflection
.o. IrregularVerbs
.o. AuxiliaryPhrase
.o. BeAuxiliaryPhrase
.o. PhrasePrefix
.o. PhraseSuffix
.o. Relativization
.o. BeInflection
.o. IObjectConversion
.o. SubjectConversion
.o. ObjectConversion
.o. ReflexiveConversion
.o. PossessiveConversion
.o. itNonSubjectUnmarking
! Resolving orthographically correct forms for listed English inflected wordforms 
.o. EngInf2Inf
.o. EngPrt2Prt
.o. EngProg2Prog
! Making best guess of correct wordform for non-listed wordforms
.o. EngPhon
.o. RemoveSuffixBoundary
] ;

twosided flag-diacritics
