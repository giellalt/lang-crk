# Copyright © 2015–2019 Alberta Language Technology Lab (ALTLab)
# http://altlab.artsrn.ualberta.ca/
#
# This program is free software; you can redistribute and/or modify
# this file under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Affero General Public License for more details.
#
# You should have received a copy of the GNU Affero General Public License
# along with this program. If not, see <https://www.gnu.org/licenses/>.

# Plains Cree long vowel variation.

i (->) %', # short-i elision

# Diacrictic normalization:
â -> {ā}, # a + combining macron U+0304
ê -> {ē}, # e + combining macron U+0304
î -> {ī}, # i + combining macron U+0304
ô -> {ō}, # o + combining macron U+0304

â -> ā, # a macron U+0101
ê -> ē, # e macron U+0113
î -> ī, # i macron U+012B
ô -> ō, # o macron U+014D

â -> {â}, # a + combining circumflex accent U+0302
ê -> {ê}, # e + combining circumflex accent U+0302
î -> {î}, # i + combining circumflex accent U+0302
ô -> {ô}, # o + combining circumflex accent U+0302

# Long vowel markings are frequently dropped:

â (->) a,
ê (->) e,  # TODO: should this be UN-conditional?
î (->) i,
ô (->) o,

# NS 152 materials consistantly write some vowels as long where Arok's write
# them as short.
# E.G. NS 152 give 'askîy' and Arok gives 'askiy.'
a (->) â,
i (->) î,
o (->) ô,

# Dealing with spaces instead of hyphens as separators in multipart words

%- -> [ %- ]+,  # Multiple hyphens into a single hyphen
%- -> [ %  | %  | %  ]+, # Multiple non-breaking or standard spaces SPACE + NBSP + NNBSP (in syllabics) to single hyphen

# Dealing with English-influenced divergences from SRO, and misspellings based on modern pronunciation

# Vowels
[ i | î ] (->) {ee},
[ a | â ] (->) u,

# Consonants
k (->) g,   # intervocalic voiced pronunciation to stops, maybe context should be added (including <c> overgenerates)
t (->) d,   # intervocalic voiced pronunciation to stops, maybe context should be added
c (->) j,   # English influence on spelling <c>

# Glides
# e.g., "tanisi ê-isiyihkâsoyan -> "tan'si ê-sikâsowan"
# TODO: figure out what conditions this (probably /o/ or /i/)
# These rules without context are EXCESSIVELY productive!
# y (->) w,
# w (->) y,

# Proto-Algonquian dialectal /y/
y (->) ý,

{iw} (->) {ow},      # Misspelling based on assimilated pronunciation of underlying /i/ as /o/ before /w/
{ow} (->) {iw},

c (->) {ch},        # English influence on spelling <c>
c (->) {tch},       # English influence on spelling <c>
c (->) {ts} ,,      # Another English influence on spelling <c>

[..] (->) h || [ a | i | o | â | ê | î | ô ] _ [ c | k | p | t ],,  # Excessive preaspiration before stops
   h (->) 0 || [ a | i | o | â | ê | î | ô ] _ [ c | k | p | t ]    # Missing preaspiration before stops

;


# Explanation:
# lexical side (->) input
# Or, in other words:
# correct (->) in use out there
