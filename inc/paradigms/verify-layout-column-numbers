#!/bin/sh

# Checks that the number of columns are consistent.
#
# Usage:
#   ./verify-layout-column-numbers FILE.layout.csv
#

awk -vRED="$(tput setaf 1)" -vNORMAL="$(tput sgr0)" '
    {
        # Count number of tabs; number of columns can be derived from tab count.
        tabs = split($0, _unused, "\t") - 1;
        delete _unused;

        tab_count[tabs]++;
        if (tabs > max_tab_count)
            max_tab_count = tabs;
    }

END {
    if (length(tab_count) == 1) {
        # The file has uniform tab counts. We good!
        exit 0;
    }

    print RED "Found inconsistent column counts:" NORMAL, length(tab_count);
    for (value in tab_count) {
        print "Found", value, "tab(s)/" value + 1, "column(s) in", tab_count[value], "row(s)";
    }
    print "Maximum tab count:", max_tab_count;
    print "Maximum column count:", max_tab_count + 1;
    exit 2;
}
' "$1"
